////////////////////////////////////////
//Aldeth's betrayal and reconciliation
////////////////////////////////////////


COPY_EXISTING ~ALDETH.DLG~ ~override~
	LPF GET_DLG_STATE_RESPONSE
		INT_VAR idx_state = 21
				idx_response = 1
		RET state21_weight = idx_state_trigger
			state21_response1_action = response_action
	END
BUT_ONLY

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Betrayal/JA#DEAGB.D~ USING ~JA#BGT_AdvPack/languages/%s/JA#DEAGAN.tra~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Betrayal/JA#ALDETH_BETRAYAL.D~ USING ~JA#BGT_AdvPack/languages/%s/JA#ALDETH.tra~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Betrayal/JA#ALDETH_RECONCILIATION.D~ USING ~JA#BGT_AdvPack/languages/%s/JA#ALDETH.tra~


COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Betrayal/JA#CUDEC.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Betrayal/JA#CUDED.BAF~
COMPILE ~JA#BGT_AdvPack/Betrayal/JA#DEAGB.BAF~
COMPILE ~JA#BGT_AdvPack/Betrayal/JA#MGUAR.BAF~

EXTEND_TOP ~%EBaldursGate%.BCS~ ~JA#BGT_AdvPack/Betrayal/AR7800.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%SWBaldursGate_MerchantLeague_L1%.BCS~ ~JA#BGT_AdvPack/Betrayal/AR7227.BAF~ EVALUATE_BUFFER


COPY ~JA#BGT_AdvPack/Betrayal/JA#DEAGB.CRE~ ~override~
	SAY NAME1 #3224 //~Deagan~ (string ref is true for BGT and BG:EE)
	SAY NAME2 #3224 //~Deagan~ (string ref is true for BGT and BG:EE)


COPY_EXISTING ~%SWBaldursGate_MerchantLeague_L2%.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY CASE_SENSITIVE ~CreateCreature("ALDETH",\[217\.491\],[0S])~ ~CreateCreature("ALDETH",[1244.720],2)~
	END
BUT_ONLY

ACTION_IF GAME_IS ~bgee eet~ BEGIN
	COPY_EXISTING ~%SWBaldursGate_MerchantLeague_L2%.BCS~ ~override~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_EVALUATE ~OR(\([0-9]+\))[%WNL%%MNL%%LNL%%TAB% ]+Exists("Aldeth")~ BEGIN
				SET result = MATCH1 - 1
			END ~OR(%result%)~
			REPLACE_TEXTUALLY EXACT_MATCH ~ApplySpellRES("ohdestbg","Aldeth")~ ~NoAction()~
			REPLACE_TEXTUALLY ~\([%WNL%%MNL%%LNL%%TAB% ]\)Global("AldethMove","GLOBAL",2)~ ~\1GlobalGT("AldethMove","GLOBAL",1)~
		END
	BUT_ONLY
	COPY_EXISTING ~%SWBaldursGate_MerchantLeague_L3%.BCS~ ~override~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY EXACT_MATCH ~ActionOverride("Aldeth",EscapeAreaMove("BG0128",217,491,S))~ ~ActionOverride("Aldeth",EscapeAreaMove("BG0128",1244,720,S))~
		END
	BUT_ONLY
END

COPY_EXISTING ~MERCHG.CRE~ ~override~
	LPF REMOVE_CRE_SCRIPT STR_VAR script_old = ~SEEENEMY~ END
	LPF REPLACE_CRE_SCRIPT STR_VAR script_old = ~MERCHG~ script_new = ~JA#MGUAR~ END

COPY_EXISTING ~BRANDI.CRE~ ~override~
	LPF ADD_CRE_SCRIPT INT_VAR offset_start = SCRIPT_CLASS STR_VAR script_new = ~JA#MGUAR~ END

COPY_EXISTING ~DABRON.CRE~ ~override/JA#DABRO.CRE~
	LPF REMOVE_CRE_SCRIPT STR_VAR script_old = ~DABRON~ END
	WRITE_ASCII DIALOG ~None~ #8
	WRITE_BYTE 0x270 255 // Allegiance: ENEMY
