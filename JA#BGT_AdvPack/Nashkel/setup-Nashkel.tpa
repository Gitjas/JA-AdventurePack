////////////
// Nashkel
////////////


COPY ~JA#BGT_AdvPack/Nashkel/JA#VOLSE.CRE~ ~override~
	SAY NAME1 @28
	SAY NAME2 @28


// Add Bardolan's respawn to Nashkel area if he disappeared because of bg1re encounter
COPY_EXISTING ~BARDOL.CRE~ ~override~
	WRITE_ASCII DEATHVAR ~Bardolan~ #32
BUT_ONLY

<<<<<<<< ...inlined/bardolan_ar4800_bg1re.baf
IF
	Exists("Bardolan")
	Global("C#LC_BardolanMoves","GLOBAL",1)
	Global("C#JA#BardolanDespawn","MYAREA",0)
THEN
RESPONSE #100
	SetGlobal("C#JA#BardolanDespawn","MYAREA",1)
	ActionOverride("Bardolan",EscapeArea())
	Continue()
END

IF
	!Exists("C#BARDOL")
	!Exists("Bardolan")
	OR(2)
		Global("C#LC_BardolanMoves","GLOBAL",0)
		Global("C#LC_BardolanMoves","GLOBAL",5)
	Global("C#JA#BardolanRespawn","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("C#JA#BardolanRespawn","MYAREA",1)
		CreateCreature("BARDOL",[1104.722],11)
		ActionOverride("Bardolan",SetNumTimesTalkedTo(1))
		Continue()
END
>>>>>>>>
EXTEND_TOP ~%Nashkel_BCS%.BCS~ ~...inlined/bardolan_ar4800_bg1re.baf~ EVALUATE_BUFFER


COPY_EXISTING ~%NashkelMines_L4_BCS%.BCS~ ~override~
			  ~%NashkelMines_MulaheysCave_BCS%.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~IncrementChapter("Chptxt-?3")~ ~\0 EraseJournalEntry(@1039)~
	END
BUT_ONLY

COPY_EXISTING ~NALIN.DLG~ ~override~
			  ~OUBLEK.DLG~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~SetGlobal("OublekBounty1","GLOBAL",1)~ ~EraseJournalEntry(999999) \0~
		REPLACE 999999 @1040 // extra step only necessary in *.DLG
	END
BUT_ONLY


COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Nashkel/ja#bardol.d~

ADD_JOURNAL EXISTING @1039
ADD_JOURNAL EXISTING @1040


COPY_EXISTING ~INNKN2.CRE~ ~override~
	SAY NAME1 @2118
	SAY NAME2 @2118
BUT_ONLY

COPY_EXISTING ~INN4801.STO~ ~override~
	SAY STORE_NAME @2119
BUT_ONLY


COPY_EXISTING ~%Nashkel%.ARE~ ~override~
	LPF ALTER_AREA_REGION
		INT_VAR info_point  = RESOLVE_STR_REF(@2119)
		STR_VAR region_name = ~Info4803~
	END
BUT_ONLY

COPY_EXISTING ~%Nashkel_BelchingDragonTavern%.ARE~ ~override~
	// add new actor
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 410
		fj_loc_y          = 250
		fj_dest_x         = 410
		fj_dest_y         = 250
		fj_orientation    = 2
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Volo Wench~
		fj_cre_resref     = ~JA#VOLSE~
	END
BUT_ONLY
