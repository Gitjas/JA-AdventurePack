///////////
//NASHKEL
///////////

/* changes to INNKN2.CRE etc. and patching of JA#AMNNA.bcs are tweaks.

JA#BARDOL.D is added content for Bardolan after the first meeting at the bridge. */

COMPILE ~JA#BGT_AdvPack/Nashkel/JA#BARDOL.D~

/* compatibility with bg1npc and bg1re if JAP is installed after them / if bg1re lets Bardolan disappear from main area: Bardolan will be respawned after/no bg1re encounter
*/

/* Bardolan needs DV */

COPY_EXISTING ~BARDOL.CRE~ ~override/bardol.cre~
	WRITE_ASCII 0x280 ~Bardolan~ #32 // script name / DV


/*Add Bardolan's respawn to Nashkel area if he disappeared because of another mod */

<<<<<<<< ...inlined/bardolan_ar4800.baf
IF
	!Exists("C#BARDOL") //bg1re
	!Exists("Bardolan")
	OR(2)
		Global("C#LC_BardolanMoves","GLOBAL",0)
		Global("C#LC_BardolanMoves","GLOBAL",5)
	Global("C#JA#_BardolanRespawn","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("C#JA#_BardolanRespawn","MYAREA",1)
		CreateCreature("bardol",[1104.722],11)
		ActionOverride("Bardolan",SetNumTimesTalkedTo(1))
		Continue()
END
>>>>>>>>
EXTEND_TOP ~%Nashkel%.BCS~ ~...inlined/bardolan_ar4800.baf~


COPY_EXISTING ~INNKN2.CRE~ ~override~
	SAY NAME1 @2118
	SAY NAME2 @2118

COPY_EXISTING ~INN4801.STO~ ~override~
	SAY 0xc @2119


<<<<<<<< ...inlined/amnisna_del.baf
IF
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		MoveToPoint([961.541])
		MoveToPoint([3988.2516])
END
>>>>>>>>
COPY_EXISTING ~AMNISNA.BCS~ ~override~
	REPLACE_BCS_BLOCK
		~...inlined/amnisna_del.baf~
		~JA#BGT_AdvPack/empty~
	ON_MISMATCH
		PATCH_WARN ~WARNING: Something went wrong while patching AMNISNA.BCS! Trying to replace with variable coords...~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY EVALUATE_REGEXP
			~IF[%WNL%%MNL%%LNL%%TAB% ]+!See(NearestEnemyOf(Myself))[%WNL%%MNL%%LNL%%TAB% ]+THEN[%WNL%%MNL%%LNL%%TAB% ]+RESPONSE #100[%WNL%%MNL%%LNL%%TAB% ]+\(MoveToPoint(\[[0-9]+\.[0-9]+\])[%WNL%%MNL%%LNL%%TAB% ]+\)+END~
			~~
		END
	END
BUT_ONLY


COPY_EXISTING ~%Nashkel%.ARE~ ~override~
	GET_OFFSET_ARRAY actors_array ARE_V10_ACTORS
	PHP_EACH actors_array AS actors_num => actors_offset BEGIN
		READ_ASCII actors_offset actors_name (32) NULL
		READ_SHORT actors_offset+0x20 actors_pos_x
		READ_ASCII actors_offset+0x80 actors_filename (32) NULL
		//Flaming Fist Dialog and Script changes
		PATCH_IF ((~%actors_pos_x%~ = 500) AND (~%actors_filename%~ STR_EQ  ~AMNIS~)) BEGIN
			WRITE_SHORT actors_offset + 0x20        912
			WRITE_SHORT actors_offset + 0x22        604
			WRITE_SHORT actors_offset + 0x24        912
			WRITE_SHORT actors_offset + 0x26        604
			WRITE_SHORT actors_offset + 0x34        7
		END
		PATCH_IF ((~%actors_pos_x%~ = 614) AND (~%actors_filename%~ STR_EQ  ~AMNIS~)) BEGIN
			WRITE_SHORT actors_offset + 0x20        1132
			WRITE_SHORT actors_offset + 0x22        515
			WRITE_SHORT actors_offset + 0x24        1132
			WRITE_SHORT actors_offset + 0x26        515
			WRITE_SHORT actors_offset + 0x34        4
		END
		PATCH_IF ((~%actors_pos_x%~ = 634) AND (~%actors_filename%~ STR_EQ  ~AMNIS~)) BEGIN
			WRITE_SHORT actors_offset + 0x20        3386
			WRITE_SHORT actors_offset + 0x22        1797
			WRITE_SHORT actors_offset + 0x24        3386
			WRITE_SHORT actors_offset + 0x26        1797
			WRITE_SHORT actors_offset + 0x34        2
		END
		PATCH_IF ((~%actors_pos_x%~ = 663) AND (~%actors_filename%~ STR_EQ  ~AMNIS~)) BEGIN
			WRITE_SHORT actors_offset + 0x20        2917
			WRITE_SHORT actors_offset + 0x22        2855
			WRITE_SHORT actors_offset + 0x24        2917
			WRITE_SHORT actors_offset + 0x26        2855
			WRITE_SHORT actors_offset + 0x34        1
		END
		PATCH_IF ((~%actors_pos_x%~ = 688) AND (~%actors_filename%~ STR_EQ  ~AMNIS~)) BEGIN
			WRITE_SHORT actors_offset + 0x20        3113
			WRITE_SHORT actors_offset + 0x22        2986
			WRITE_SHORT actors_offset + 0x24        3113
			WRITE_SHORT actors_offset + 0x26        2986
			WRITE_SHORT actors_offset + 0x34        6
		END
	END
BUT_ONLY
