////////////
// Nashkel
////////////


COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Nashkel/JA#BARDOL.D~

// Add Bardolan's respawn to Nashkel area if he disappeared because of bg1re encounter
COPY_EXISTING ~BARDOL.CRE~ ~override~
	WRITE_ASCII DEATHVAR ~Bardolan~ #32
BUT_ONLY

<<<<<<<< ...inlined/bardolan_ar4800_bg1re.baf
IF
	Exists("Bardolan")
	Global("C#LC_BardolanMoves","GLOBAL",1)
	Global("C#JA#_BardolanDespawn","MYAREA",0)
THEN
RESPONSE #100
	SetGlobal("C#JA#_BardolanDespawn","MYAREA",1)
	ActionOverride("Bardolan",EscapeArea())
	Continue()
END

IF
	!Exists("C#BARDOL")
	!Exists("Bardolan")
	OR(2)
		Global("C#LC_BardolanMoves","GLOBAL",0)
		Global("C#LC_BardolanMoves","GLOBAL",5)
	Global("C#JA#_BardolanRespawn","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("C#JA#_BardolanRespawn","MYAREA",1)
		CreateCreature("bardol",[1104.722],11)
		ActionOverride("Bardolan",SetNumTimesTalkedTo(1))
		Continue()
END
>>>>>>>>
EXTEND_TOP ~%Nashkel_BCS%.BCS~ ~...inlined/bardolan_ar4800_bg1re.baf~ EVALUATE_BUFFER


COPY_EXISTING ~INNKN2.CRE~ ~override~
	SAY NAME1 @2118
	SAY NAME2 @2118

COPY_EXISTING ~INN4801.STO~ ~override~
	SAY 0xc @2119


<<<<<<<< ...inlined/amnisna_del.baf
IF
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		MoveToPoint([961.541])
		MoveToPoint([3988.2516])
END
>>>>>>>>
COPY_EXISTING ~AMNISNA.BCS~ ~override~
	REPLACE_BCS_BLOCK
		~...inlined/amnisna_del.baf~
		~JA#BGT_AdvPack/empty~
	ON_MISMATCH
		PATCH_WARN ~WARNING: Something went wrong while patching AMNISNA.BCS! Trying to replace with variable coords...~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY EVALUATE_REGEXP
			~IF[%WNL%%MNL%%LNL%%TAB% ]+!See(NearestEnemyOf(Myself))[%WNL%%MNL%%LNL%%TAB% ]+THEN[%WNL%%MNL%%LNL%%TAB% ]+RESPONSE #100[%WNL%%MNL%%LNL%%TAB% ]+\(MoveToPoint(\[[0-9]+\.[0-9]+\])[%WNL%%MNL%%LNL%%TAB% ]+\)+END~
			~~
		END
	END
BUT_ONLY


COPY_EXISTING ~%Nashkel%.ARE~ ~override~
	GET_OFFSET_ARRAY actors ARE_V10_ACTORS
	PHP_EACH actors AS i => actor BEGIN
		READ_SHORT actor+0x20 actor_pos_x
		READ_ASCII actor+0x80 actor_filename
		PATCH_MATCH ~%actor_pos_x%~ WITH
			500 WHEN (~%actor_filename%~ STR_EQ ~AMNIS~) BEGIN
				WRITE_SHORT actor+0x20 912
				WRITE_SHORT actor+0x22 604
				WRITE_SHORT actor+0x24 912
				WRITE_SHORT actor+0x26 604
				WRITE_SHORT actor+0x34 7
			END
			614 WHEN (~%actor_filename%~ STR_EQ ~AMNIS~) BEGIN
				WRITE_SHORT actor+0x20 1132
				WRITE_SHORT actor+0x22 515
				WRITE_SHORT actor+0x24 1132
				WRITE_SHORT actor+0x26 515
				WRITE_SHORT actor+0x34 4
			END
			634 WHEN (~%actor_filename%~ STR_EQ ~AMNIS~) BEGIN
				WRITE_SHORT actor+0x20 3386
				WRITE_SHORT actor+0x22 1797
				WRITE_SHORT actor+0x24 3386
				WRITE_SHORT actor+0x26 1797
				WRITE_SHORT actor+0x34 2
			END
			663 WHEN (~%actor_filename%~ STR_EQ ~AMNIS~) BEGIN
				WRITE_SHORT actor+0x20 2917
				WRITE_SHORT actor+0x22 2855
				WRITE_SHORT actor+0x24 2917
				WRITE_SHORT actor+0x26 2855
				WRITE_SHORT actor+0x34 1
			END
			688 WHEN (~%actor_filename%~ STR_EQ ~AMNIS~) BEGIN
				WRITE_SHORT actor+0x20 3113
				WRITE_SHORT actor+0x22 2986
				WRITE_SHORT actor+0x24 3113
				WRITE_SHORT actor+0x26 2986
				WRITE_SHORT actor+0x34 6
			END
			DEFAULT
		END
	END
BUT_ONLY
