////////////////////////
// Black Alaric's Cave
////////////////////////


COPY ~JA#BGT_AdvPack/Black_Alarics_Cave/JA#PIRA1.CRE~ ~override~
	SAY NAME1 @35
	SAY NAME2 @36

COPY ~JA#BGT_AdvPack/Black_Alarics_Cave/JA#SKEL1.CRE~ ~override~
	SAY NAME1 @34
	SAY NAME2 @34


COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Black_Alarics_Cave/ja#pira1.d~


COPY_EXISTING ~%Lighthouse_BlackAlaricsCave%.ARE~ ~override~
	// Replace each Flesh Golem with a Pirate skeleton
	GET_OFFSET_ARRAY actors ARE_V10_ACTORS
	PHP_EACH actors AS i => actor BEGIN
		READ_ASCII actor+0x80 actor_filename
		PATCH_IF (~%actor_filename%~ STR_EQ ~GOLEMF~) BEGIN
			WRITE_ASCII actor+0x00 ~Piratenskelett~ #32
			WRITE_LONG  actor+0x30  0xEB10 // SKELETON_WARRIOR
			WRITE_ASCII actor+0x80 ~JA#SKEL1~ #8
		END
	END
	// Add The Red Beric
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 882
		fj_loc_y          = 212
		fj_dest_x         = 882
		fj_dest_y         = 212
		fj_orientation    = 4
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Pirat~
		fj_cre_resref     = ~JA#PIRA1~
	END
	// Rest encounters
	LPF fj_are_structure
		INT_VAR
		fj_cre_strref0	  = 10148 // strref fine for BGT, BGEE and EET
		STR_VAR
		fj_structure_type = interrupts
		fj_cre_resref0    = ~JA#SKEL1~
		fj_max_num		  = 10
		fj_day_prob		  = 20
		fj_night_prob 	  = 20
	END
BUT_ONLY
