////////////////////////
//Black Alaric's Cave
////////////////////////

COPY ~JA#BGT_AdvPack/Black_Alarics_Cave/JA#PIRA1.CRE~ ~override~
	SAY NAME1 @35
	SAY NAME2 @36
	SAY INITIAL_MEETING #5659
	SAY DAMAGE #5840
	SAY DYING #5841

COPY ~JA#BGT_AdvPack/Black_Alarics_Cave/JA#SKEL1.CRE~ ~override~
	SAY NAME1 @34
	SAY NAME2 @34

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Black_Alarics_Cave/JA#PIRA1.D~

COPY_EXISTING ~%Lighthouse_BlackAlaricsCave%.ARE~ ~override~
    READ_LONG 0x54 ofs_actors
    READ_SHORT 0x58 num_actors
	SET actors_end = ofs_actors + (num_actors * 0x110)

	// Replace Flesh Golems with Pirate skeletons
	// LPF ALTER_AREA_ACTOR
	// 	INT_VAR animation = 0xEB10
	// 	STR_VAR actor_name = ~Flesh Golem~
	// 			cre_file = ~JA#SKEL1~
	// END
	FOR (actor = ofs_actors; actor < actors_end; actor+=0x110) BEGIN
		READ_ASCII actor+0x80 actors_filename (32) NULL
		PATCH_IF (~%actors_filename%~ STR_EQ ~GOLEMF~) BEGIN
			WRITE_ASCII	actor+0x00 ~Piratenskelett~ #32
			WRITE_LONG	actor+0x30 0xEB10 // SKELETON_WARRIOR
			WRITE_ASCII	actor+0x80 ~JA#SKEL1~ #8
		END
	END

	// Add The Red Beric
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 882
		fj_loc_y          = 212
		fj_dest_x         = 882
		fj_dest_y         = 212
		fj_orientation    = 4
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Pirat~
		fj_cre_resref     = ~JA#PIRA1~
		fj_dlg_resref	  = ~JA#PIRA1~
	END

	// Rest encounters
	LPF fj_are_structure
		INT_VAR
		fj_cre_strref0	  = RESOLVE_STR_REF (#10148)
		STR_VAR
		fj_structure_type = interrupts
		fj_name           = ~~
		fj_cre_resref0    = JA#SKEL1
		fj_max_num		  = 10
		fj_day_prob		  = 20
		fj_night_prob 	  = 20
	END
