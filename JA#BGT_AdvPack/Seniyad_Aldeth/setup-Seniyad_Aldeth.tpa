////////////////////
//Seniyad & Aldeth
////////////////////

// FYI: In EE with jaheira in group you already have a peaceful solution.

COPY ~JA#BGT_AdvPack/Seniyad_Aldeth/JA#ALDET.BAM~ ~override~
COPY ~JA#BGT_AdvPack/Seniyad_Aldeth/JA#ALDET.ITM~ ~override~
	SAY NAME1 @2101
	SAY NAME2 @2101
	SAY DESC @2102


// If Aldeth main component is installed
ACTION_IF NOT FILE_EXISTS_IN_GAME ~JA#ALDET.BCS~ BEGIN
	COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Seniyad_Aldeth/JA#ALDET.BAF~
END ELSE BEGIN
	EXTEND_TOP ~JA#ALDET.BCS~ ~JA#BGT_AdvPack/Seniyad_Aldeth/JA#ALDET.BAF~ EVALUATE_BUFFER
END


// BGT
COPY_EXISTING ~DRUID3.BCS~ ~override~
			  ~SENIYA.DLG~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~EscapeAreaDestroy(90)~ ~EscapeAreaObject("druid_escape")~
	END
BUT_ONLY


ACTION_IF GAME_IS ~bgee eet~ BEGIN
	<<<<<<<< ...inlined/aldeth_ee.d
	ADD_STATE_TRIGGER ALDETH 31 ~Global("SeniyadXP","GLOBAL",0)~
	>>>>>>>>
	COMPILE EVALUATE_BUFFER ~...inlined/aldeth_ee.d~
END

LAF GET_DLG_STRREF
	INT_VAR idx_state = (GAME_IS ~bgt~ ? 0 : 4)
	STR_VAR dlg_file = ~ALDETH.DLG~
	RET aldeth_state04_journal = journal_strref
END
LAF GET_DLG_WEIGHT
	INT_VAR idx_state = 7
	STR_VAR dlg_file = ~ALDETH.DLG~
	RET aldeth_state07_weight = weight
END

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Seniyad_Aldeth/ja#aldeth_reward.d~ USING ~JA#BGT_AdvPack/languages/%s/ja#aldeth.tra~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Seniyad_Aldeth/ja#seniya.d~ USING ~JA#BGT_AdvPack/languages/%s/ja#aldeth.tra~

ADD_JOURNAL EXISTING @20 USING ~JA#BGT_AdvPack/languages/%s/JA#ALDETH.tra~


// Also defined in Merchants' League investigations
COPY_EXISTING ~ALDETH.CRE~ ~override~
	LPF ADD_CRE_SCRIPT STR_VAR script_new = ~JA#ALDET~ END
BUT_ONLY


// BGT
COPY_EXISTING ~%CloakwoodLodge%.ARE~ ~override~
	//add region trigger
	LPF fj_are_structure
		INT_VAR
		fj_type             = 0
		fj_box_left         = 2929
		fj_box_top          = 2590
		fj_box_right        = 3319
		fj_box_bottom       = 2784
		fj_cursor_idx       = 0
		fj_vertex_0         = 3319 + (2687 << 16)
		fj_vertex_1         = 2995 + (2784 << 16)
		fj_vertex_2         = 2929 + (2672 << 16)
		fj_vertex_3         = 3276 + (2590 << 16)
		STR_VAR
		fj_structure_type   = region
		fj_name             = ~druid_escape~
	END
UNLESS ~druid_escape~



/* [DEPRECATED] bg1npc is installed: Viconia's interjection kills the new reply option to settle the discpute with Seniyad peacefully. Add the reply option to Viconia's new state, as well: */

ACTION_IF ((MOD_IS_INSTALLED ~BG1NPC/BG1NPC.TP2~ "10") //bg1npc interjections and quests is installed
			AND (FILE_EXISTS ~bg1npc/tra/english/x#viint.tra~)) BEGIN // only checking that the bg1npc folder is still in there

	PRINT ~BG1 NPC Quest und Banter Component detected~

	OUTER_SET vic_state = STATE_WHICH_SAYS 5 IN ~bg1npc/tra/%s/x#viint.tra~ FROM ~%VICONIA_JOINED%.dlg~
	OUTER_SET jaheira_state = STATE_WHICH_SAYS 95 IN ~bg1npc/tra/%s/x#jaqu.tra~ FROM ~%JAHEIRA_JOINED%.dlg~
	OUTER_SET seniya3_state = STATE_WHICH_SAYS 3 IN ~JA#BGT_AdvPack/languages/%s/ja#aldeth.tra~ FROM ~SENIYA.dlg~
	OUTER_SET seniya17_state = STATE_WHICH_SAYS 17 IN ~JA#BGT_AdvPack/languages/%s/ja#aldeth.tra~ FROM ~SENIYA.dlg~

	ACTION_IF ((vic_state >= 0) AND (jaheira_state >= 0) AND (seniya3_state >= 0) AND (seniya17_state >= 0)) BEGIN
		<<<<<<<< ...inlined/seniya_bg1npc.d
		/* If we know the state number in viconij.dlg we append to it */
		EXTEND_TOP ~%VICONIA_JOINED%~ %vic_state% #1
		IF ~~ THEN REPLY @0 EXTERN SENIYA %seniya3_state%
		END

		/* more bg1npc compatibility: loop to Jaheira's quest, by Lumorus */
		INTERJECT SENIYA %seniya17_state% JA#SENIYA_06_BG1NPC
		== %JAHEIRA_JOINED% IF ~InParty("jaheira") InMyArea("jaheira") !StateCheck("jaheira",CD_STATE_NOTVALID)~ THEN @68
		== SENIYA IF ~InParty("jaheira") InMyArea("jaheira") !StateCheck("jaheira",CD_STATE_NOTVALID)~ THEN @111
		== %JAHEIRA_JOINED% IF ~InParty("jaheira") InMyArea("jaheira") !StateCheck("jaheira",CD_STATE_NOTVALID)~ THEN @112
		== SENIYA IF ~InParty("jaheira") InMyArea("jaheira") !StateCheck("jaheira",CD_STATE_NOTVALID)~ THEN @113
		END %JAHEIRA_JOINED% %jaheira_state%
		>>>>>>>>
		COMPILE EVALUATE_BUFFER ~...inlined/seniya_bg1npc.d~ USING ~JA#BGT_AdvPack/languages/%s/JA#ALDETH.tra~
	END

END // ACTION_IF bg1npc interjections is installed
