////////////////////////////////////
//Merchants' League investigations
////////////////////////////////////

/* für BTG:EE könnte hier übertragen werden (genaues Einfügen muss noch überprüft werden):
-Extradialog, um Senyiad zu beschwichtigen
-Begegnung mit Aldeth über Deagan in Baldurs Tor (Drei alte Krüge)
-Möglichkeit, Aldeth auf seinen Verrat anzusprechen.
Änderungen in der Gilde + Verrat sind in BG:EE bereits restauriert.
*/

COMPILE ~JA#BGT_AdvPack/Merchants_League/JA#BRANDI.D~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Merchants_League/JA#DEAGA.D~ USING ~JA#BGT_AdvPack/languages/%s/JA#DEAGAN.tra~
COMPILE ~JA#BGT_AdvPack/Merchants_League/JA#DOPP.D~

ACTION_IF GAME_IS ~bgt~ BEGIN
	LAF GET_DLG_JOURNAL_STRREF
		INT_VAR idx_state = 0
		STR_VAR dlg_file = ~IRLENT.DLG~
		RET aldeth_state00_journal = strref
	END

	<<<<<<<< ...inlined/irlent_bgt.d
	ALTER_TRANS IRLENT
	BEGIN 0 END
	BEGIN 1 END
	BEGIN
	  "UNSOLVED_JOURNAL" ~%aldeth_state00_journal%~
	END
	>>>>>>>>
	COMPILE EVALUATE_BUFFER ~...inlined/irlent_bgt.d~
END

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Merchants_League/JA#BART10.D~ USING ~JA#BGT_AdvPack/languages/%s/JA#ALDETH.tra~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Merchants_League/JA#MERCHG.D~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Merchants_League/JA#MERLEA.D~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Merchants_League/JA#MLCOK.D~ USING ~JA#BGT_AdvPack/languages/%s/JA#ALDETH.tra~

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Merchants_League/JA#IRLENT.D~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Merchants_League/JA#ZORL.D~


ACTION_IF GAME_IS ~bgt~ BEGIN
	LAF GET_DLG_JOURNAL_STRREF
		INT_VAR idx_state = 13
		STR_VAR dlg_file = ~ALDETH.DLG~
		RET aldeth_state13_journal = strref
	END

	<<<<<<<< ...inlined/aldeth_bgt.d
	ALTER_TRANS ALDETH
	BEGIN JA#ALDETH_00 END
	BEGIN 0 END
	BEGIN
	  "UNSOLVED_JOURNAL" ~%aldeth_state13_journal%~
	END
	>>>>>>>>
	COMPILE EVALUATE_BUFFER ~...inlined/aldeth_bgt.d~ ~JA#BGT_AdvPack/Merchants_League/JA#ALDETH.D~
END ELSE BEGIN
	COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Merchants_League/JA#ALDETH.D~
END

ADD_JOURNAL @12 @17 USING ~JA#BGT_AdvPack/languages/%s/ja#deagan.tra~
ADD_JOURNAL EXISTING @1025 @1026 @1027 @1028 @1029 @1030


COPY_EXISTING_REGEXP ~\(ALDETH\|FLAMAL\)\.DLG~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~ActionOverride("Brandi",EscapeArea())~ ~\0 ActionOverride("JA#DEAGA",EscapeArea())~
	END
BUT_ONLY


COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Merchants_League/JA#CUALD.BAF~
COMPILE ~JA#BGT_AdvPack/Merchants_League/JA#CUDEA.BAF~
COMPILE ~JA#BGT_AdvPack/Merchants_League/JA#CUDEB.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Merchants_League/JA#DEAGA.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Merchants_League/JA#DOPP.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Merchants_League/JA#ALDET.BAF~

EXTEND_TOP ~%NBaldursGate_ThreeOldKegs_L1%.BCS~ ~JA#BGT_AdvPack/Merchants_League/AR7219.BAF~
EXTEND_TOP ~%SWBaldursGate_MerchantLeague_L1%.BCS~ ~JA#BGT_AdvPack/Merchants_League/AR7227.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%SWBaldursGate_MerchantLeague_L3%.BCS~ ~JA#BGT_AdvPack/Merchants_League/AR7229.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%NBaldursGate%.BCS~ ~JA#BGT_AdvPack/Merchants_League/AR7300.BAF~ EVALUATE_BUFFER

EXTEND_BOTTOM ~IRLENTRE.BCS~ ~JA#BGT_AdvPack/Merchants_League/IRLENTRE.BAF~ EVALUATE_BUFFER
EXTEND_BOTTOM ~ZORL.BCS~ ~JA#BGT_AdvPack/Merchants_League/ZORL.BAF~ EVALUATE_BUFFER
	REPLACE ~999999~ @41


COPY_EXISTING ~%SWBaldursGate%.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~SetGlobal("SpawnDabron","GLOBAL",1)~ ~NoAction()~
		REPLACE_TEXTUALLY CASE_SENSITIVE ~CreateCreature("ALDETH",\[3507\.2477\],[0S])~ ~Continue()~
	END
BUT_ONLY


// Restore party part only in BGT (already restored in EE)
ACTION_IF GAME_IS ~bgt~ BEGIN
	INCLUDE ~JA#BGT_AdvPack/Merchants_League/party/setup-party.tpa~
END


COPY ~JA#BGT_AdvPack/Merchants_League/JA#MLBAR.STO~ ~override~

COPY ~JA#BGT_AdvPack/Merchants_League/JA#DEAGA.CRE~ ~override~
	SAY NAME1 #3224 //~Deagan~ (string ref is true for BGT and BG:EE)
	SAY NAME2 #3224 //~Deagan~ (string ref is true for BGT and BG:EE)

COPY ~JA#BGT_AdvPack/Merchants_League/JA#DOPP0.CRE~ ~override~
	 ~JA#BGT_AdvPack/Merchants_League/JA#DOPP1.CRE~ ~override~
	 ~JA#BGT_AdvPack/Merchants_League/JA#DOPP2.CRE~ ~override~
	 ~JA#BGT_AdvPack/Merchants_League/JA#DOPP3.CRE~ ~override~
	 ~JA#BGT_AdvPack/Merchants_League/JA#DOPP4.CRE~ ~override~
	 ~JA#BGT_AdvPack/Merchants_League/JA#DOPP5.CRE~ ~override~
	 ~JA#BGT_AdvPack/Merchants_League/JA#DOPP6.CRE~ ~override~
	 ~JA#BGT_AdvPack/Merchants_League/JA#DOPP7.CRE~ ~override~
	 ~JA#BGT_AdvPack/Merchants_League/JA#DOPP8.CRE~ ~override~
	 ~JA#BGT_AdvPack/Merchants_League/JA#DOPP9.CRE~ ~override~
	SAY NAME1 @2120
	SAY NAME2 @2120
	WRITE_ASCII SCRIPT_OVERRIDE ~None~ #8
	WRITE_ASCII SCRIPT_CLASS ~None~ #8
	WRITE_ASCII SCRIPT_RACE ~JA#DOPP~ #8
	WRITE_ASCII SCRIPT_GENERAL ~None~ #8
	WRITE_ASCII SCRIPT_DEFAULT ~WTASIGHT~ #8
	WRITE_ASCII DEATHVAR ~DopZorl~ #32

COPY ~JA#BGT_AdvPack/Merchants_League/JA#ZRLKY.BAM~ ~override~
COPY ~JA#BGT_AdvPack/Merchants_League/JA#ZRLKY.ITM~ ~override~
	SAY NAME1 @2103
	SAY NAME2 @2103
	SAY DESC @2104

COPY ~JA#BGT_AdvPack/Merchants_League/JA#DZORL.ITM~ ~override~
	SAY NAME1 @2105
	SAY NAME2 @2105
	SAY DESC @2106

COPY ~JA#BGT_AdvPack/Merchants_League/JA#DIRLE.ITM~ ~override~
	SAY NAME1 @2107
	SAY NAME2 @2107
	SAY DESC @2108

COPY_EXISTING ~IRLENT.CRE~ ~override~
	WRITE_BYTE 0x271 255 // General: MONSTER
	WRITE_BYTE 0x272 103 // Race: BEAR
	WRITE_BYTE 0x273 112 // Class: DOPPLEGANGER_GREATER
	WRITE_BYTE 0x275 4   // Gender: NIETHER
	REPLACE_CRE_ITEM ~CHAN04~ #0 #0 #0 ~NONE~ ~ARMOR~ EQUIP

COPY_EXISTING ~ZORL.CRE~ ~override~
	SAY NAME1 @2109
	SAY NAME2 @2109
	WRITE_BYTE 0x271 255 // General: MONSTER
	ADD_CRE_ITEM ~JA#ZRLKY~ #0 #0 #0 ~IDENTIFIED~ ~INV1~

COPY_EXISTING ~ALDETH.CRE~ ~override~
	LPF ADD_CRE_SCRIPT STR_VAR script_new = ~JA#ALDET~ END


COPY_EXISTING ~AM0704.WAV~ ~override~

COPY_EXISTING ~%SWBaldursGate_MerchantLeague_L1%.ARE~ ~override~
	LPF ALTER_AREA_ACTOR
		INT_VAR
		x_coord		= 914
		y_coord		= 528
		dest_x		= 914
		dest_y		= 528
		orient		= 0
		STR_VAR
		actor_name 	= ~Irlentree~
	END
	//add new actors
	PATCH_DEFINE_ARRAY x_coord BEGIN 1103 1176 779 1048 688 632 711 219 402 1125 END
	PATCH_DEFINE_ARRAY y_coord BEGIN 587  577  888 757  598 576 301 487 606 500 END
	PATCH_DEFINE_ARRAY orient  BEGIN 11   2    6   8    5   13  0   14  6   3 END
	PATCH_PHP_EACH x_coord AS index => x BEGIN
		LPF fj_are_structure
			INT_VAR
			fj_loc_x          = $x_coord("%index%")
			fj_loc_y          = $y_coord("%index%")
			fj_dest_x         = $x_coord("%index%")
			fj_dest_y         = $y_coord("%index%")
			fj_orientation    = $orient("%index%")
			STR_VAR
			fj_structure_type = actor
			fj_name           = Gast
			fj_cre_resref     = EVAL ~JA#DOPP%index%~
			fj_dlg_resref 	  = EVAL ~JA#DOPP%index%~
		END
	END
	// Add Ambient 1
	LPF fj_are_structure
		INT_VAR
		fj_loc_x           = 50
		fj_loc_y           = 50
		fj_radius          = 500
		fj_volume_variance = 40
		fj_sound_num       = 1
		fj_flags 		   = 0b0000000111
		STR_VAR
		fj_structure_type = ambient
		fj_name           = AM0704
		fj_wav_resref0    = AM0704
	END
	READ_LONG 0xbc "JA#OFF_SNG"
	WRITE_LONG "JA#OFF_SNG" ~0~
	WRITE_LONG "JA#OFF_SNG" + 0x4 ~0~
	WRITE_ASCII "JA#OFF_SNG" + 0x28 ~NONE~ #8
	WRITE_ASCII "JA#OFF_SNG" + 0x3c ~NONE~ #8
BUT_ONLY

COPY_EXISTING ~%SWBaldursGate_MerchantLeague_L2%.ARE~ ~override~
	GET_OFFSET_ARRAY actors ARE_V10_ACTORS
	PHP_EACH actors AS i => actor BEGIN
		READ_ASCII actor+0x80 actor_filename
		PATCH_MATCH ~%actor_filename%~ WITH
			~FECOOK~ BEGIN
				WRITE_ASCII actor+0x48 ~JA#MLCOK~ // Dialog
			END
			~BRANDI~ BEGIN
				WRITE_SHORT actor+0x20 637
				WRITE_SHORT actor+0x22 734
				WRITE_SHORT actor+0x24 637
				WRITE_SHORT actor+0x26 734
				WRITE_SHORT actor+0x34 14
			END
			~ZORL~ BEGIN
				WRITE_SHORT actor+0x20 1352
				WRITE_SHORT actor+0x22 671
				WRITE_SHORT actor+0x24 1352
				WRITE_SHORT actor+0x26 671
				WRITE_SHORT actor+0x34 13
			END
			DEFAULT
		END
	END
	//container scripts, scroll and key
	LPF ALTER_AREA_CONTAINER
		STR_VAR
		container_name = ~Container 2~
		lock_difficulty = 80
		container_key = ~JA#ZRLKY~
	END
	//add new actors
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 807
		fj_loc_y          = 736
		fj_dest_x         = 807
		fj_dest_y         = 736
		fj_orientation    = 14
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Deagan~
		fj_cre_resref     = ~JA#DEAGA~
		fj_dlg_resref	  = ~JA#DEAGA~
	END
	// Add Ambient 1
	LPF fj_are_structure
		INT_VAR
		fj_loc_x           = 50
		fj_loc_y           = 50
		fj_radius          = 500
		fj_volume_variance = 20
		fj_sound_num       = 1
		fj_flags 		   = 0b0000000111
		STR_VAR
		fj_structure_type = ambient
		fj_name           = AM0704
		fj_wav_resref0    = AM0704
	END
	READ_LONG 0xbc "JA#OFF_SNG"
	WRITE_LONG "JA#OFF_SNG" ~0~
	WRITE_LONG "JA#OFF_SNG" + 0x4 ~0~
	WRITE_ASCII "JA#OFF_SNG" + 0x28 ~NONE~ #8
	WRITE_ASCII "JA#OFF_SNG" + 0x3c ~NONE~ #8
BUT_ONLY

COPY_EXISTING ~%SWBaldursGate_MerchantLeague_L3%.ARE~ ~override~
	// Add Ambient 1
	LPF fj_are_structure
		INT_VAR
		fj_loc_x           = 50
		fj_loc_y           = 50
		fj_radius          = 500
		fj_volume_variance = 20
		fj_sound_num       = 1
		fj_flags 		   = 0b0000000111
		STR_VAR
		fj_structure_type = ambient
		fj_name           = AM0704
		fj_wav_resref0    = AM0704
	END
	READ_LONG 0xbc "JA#OFF_SNG"
	WRITE_LONG "JA#OFF_SNG" ~0~
	WRITE_LONG "JA#OFF_SNG" + 0x4 ~0~
	WRITE_ASCII "JA#OFF_SNG" + 0x28 ~NONE~ #8
	WRITE_ASCII "JA#OFF_SNG" + 0x3c ~NONE~ #8
BUT_ONLY

COPY_EXISTING ~%UlgothsBeard_House2%.ARE~ ~override~
	LPF REPLACE_AREA_ITEM
		STR_VAR old_item = ~SW1H03~
				new_item = ~SW1H02~
	END
BUT_ONLY
