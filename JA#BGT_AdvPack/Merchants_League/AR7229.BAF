IF
	Global("ZorlDopple","GLOBAL",1)
	Global("JA#CUTSCENE_ACTIVE","%SWBaldursGate_MerchantLeague_L3%",0)
THEN
	RESPONSE #100
		SetGlobal("JA#CUTSCENE_ACTIVE","%SWBaldursGate_MerchantLeague_L3%",1)
		SetGlobal("JA#MERLEA_MYST","GLOBAL",1)
		SetGlobal("HelpBrandilar","GLOBAL",2)
		SetGlobal("JA#CUTSCENE_ACTIVE","%SWBaldursGate_MerchantLeague_L3%",2)
END

IF
	GlobalGT("Chapter","GLOBAL",%tutu_chapter_5%)
	Global("DukeThanks","GLOBAL",1)
	Exists("Aldeth")
	!Dead("Aldeth")
THEN
	RESPONSE #100
		ActionOverride("Aldeth",DestroySelf())
END

/* Zorls and Irlentree's corpse will be on the table when doppelgangers are revealed */
IF
	Global("HelpBrandilar","GLOBAL",2)
	Global("JA#Dopp_Corpse","LOCALS",0)
THEN
	RESPONSE #100
		ActionOverride("Container 1",CreateItem("JA#DIRLE",0,0,0)) // Irlentrees Leiche
		ActionOverride("Container 1",CreateItem("JA#DZORL",0,0,0)) // Zorls Leiche
		SetGlobal("JA#Dopp_Corpse","LOCALS",1)
END

/* In case the player didn't find the bodies yet */
IF
	GlobalGT("Chapter","GLOBAL",%tutu_chapter_6%)
	Global("JA#Dopp_Corpse","LOCALS",1)
THEN
	RESPONSE #100
		SetGlobal("JA#Dopp_Corpse","LOCALS",2)
		ActionOverride("Container 1",DestroyItem("JA#DIRLE"))
		ActionOverride("Container 1",DestroyItem("JA#DZORL"))
END
