// Anything to process Aldeth's party


COPY ~JA#BGT_AdvPack/Merchants_League/party/JA#DPIRL.ITM~ ~override~
COPY ~JA#BGT_AdvPack/Merchants_League/party/JA#DPIRL.CRE~ ~override~
	SAY NAME1 @2100
	SAY NAME2 @2100
	WRITE_ASCII DEATHVAR ~Irlentre~ #32

COPY ~JA#BGT_AdvPack/Merchants_League/party/JA#DPZRL.ITM~ ~override~
COPY ~JA#BGT_AdvPack/Merchants_League/party/JA#DPZRL.CRE~ ~override~
	SAY NAME1 @2099
	SAY NAME2 @2099
	WRITE_ASCII DEATHVAR ~Zorl~ #32


COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Merchants_League/party/JA#CUIRL.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Merchants_League/party/JA#DPZRL.BAF~


EXTEND_TOP ~ZORL.BCS~ ~JA#BGT_AdvPack/Merchants_League/party/ZORL.BAF~ EVALUATE_BUFFER

EXTEND_TOP ~%SWBaldursGate_MerchantLeague_L1_BCS%.BCS~ ~JA#BGT_AdvPack/Merchants_League/party/AR7227.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%SWBaldursGate_MerchantLeague_L2_BCS%.BCS~ ~JA#BGT_AdvPack/Merchants_League/party/AR7228.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%SWBaldursGate_MerchantLeague_L3_BCS%.BCS~ ~JA#BGT_AdvPack/Merchants_League/party/AR7229.BAF~ EVALUATE_BUFFER


COPY_EXISTING ~%SWBaldursGate_MerchantLeague_L2_BCS%.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~NumDeadGT("DopZorl",5)~ ~NumDeadGT("DopZorl",9) Dead("Zorl") Dead("Irlentre")~
	END
BUT_ONLY

COPY_EXISTING ~IRLENTRE.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EVALUATE_REGEXP ~\(CreateCreature("DOPZORL",\[[0-9]+\.[0-9]+\],\(1?[0-9]\|[NS]?[NS]?[WE]?[WE]?\))[%WNL%%MNL%%LNL%%TAB% ]*\)+~ ~UseItem("JA#DPIRL",Myself)~
		REPLACE_TEXTUALLY EXACT_MATCH ~ForceSpell(Myself,DOPPLEGANGER_CHANGE2)~ ~~
	END
BUT_ONLY

COPY_EXISTING ~ZORL.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~ForceSpell(Myself,DOPPLEGANGER_CHANGE2)~ ~UseItem("JA#DPZRL",Myself)~
	END
BUT_ONLY


COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Merchants_League/party/ja#aldeth.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Merchants_League/party/ja#zorl.d~ USING ~JA#BGT_AdvPack/languages/%s/ja#aldeth.tra~


COPY_EXISTING ~IRLENT.CRE~ ~override~
	ADD_CRE_ITEM ~JA#DPIRL~ #0 #0 #0 ~IDENTIFIED~ ~LRING~
BUT_ONLY

COPY_EXISTING ~ZORL.CRE~ ~override~
	ADD_CRE_ITEM ~JA#DPZRL~ #0 #0 #0 ~IDENTIFIED~ ~LRING~
BUT_ONLY


COPY_EXISTING ~%SWBaldursGate_MerchantLeague_L3%.ARE~ ~override~
	LPF DELETE_AREA_ACTOR STR_VAR cre_to_delete = ~DOPPLE~ END
BUT_ONLY
