///////////////
// Seven Suns
///////////////


COPY ~JA#BGT_AdvPack/Seven_Suns/JA#SSGU1.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Seven_Suns/JA#SSGU4.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Seven_Suns/JA#JHASS.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Seven_Suns/JA#SSMR1.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Seven_Suns/JA#SSMR2.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Seven_Suns/JA#SSGU3.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Seven_Suns/JA#SSGU2.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Seven_Suns/JA#SSGU5.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Seven_Suns/JA#SSMR3.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Seven_Suns/JA#SSMR4.CRE~ ~override~
	PATCH_IF GAME_IS ~bgee~ BEGIN
		SAY INITIAL_MEETING ~~ [TANNER20]
	END

COPY_EXISTING ~DOPPGR.CRE~ ~override/JA#DPKIZ.CRE~
	SAY NAME1 @2048
	SAY NAME2 @2048
	WRITE_SHORT 0x24 (THIS+25) // Current HP
	WRITE_SHORT 0x26 (THIS+25) // Maximum HP
	WRITE_ASCII DEATHVAR ~RASHAD~ #32

COPY ~JA#BGT_AdvPack/Seven_Suns/JA#DPKIZ.ITM~ ~override~


COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Seven_Suns/JA#DPKIZ.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Seven_Suns/JA#SSDOP.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Seven_Suns/JA#SSGUA.BAF~ // used by JA#SSGU1.CRE and JA#SSGU4.CRE
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Seven_Suns/JA#SSMER.BAF~ // used in DOPPSM.CRE
	PATCH_IF GAME_IS ~bgee~ BEGIN
		REPLACE_TEXTUALLY ~46151~ ~62279~
	END

EXTEND_TOP ~%SWBaldursGate_SevenSuns_L1_BCS%.BCS~ ~JA#BGT_AdvPack/Seven_Suns/AR7601.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%SWBaldursGate_SevenSuns_L2_BCS%.BCS~ ~JA#BGT_AdvPack/Seven_Suns/AR7602.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%SWBaldursGate_BCS%.BCS~ ~JA#BGT_AdvPack/Seven_Suns/AR8000.BAF~ EVALUATE_BUFFER


COPY_EXISTING ~%SWBaldursGate_BCS%.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EVALUATE_REGEXP
		~Global("laola","GLOBAL",1)[%WNL%%MNL%%LNL%%TAB% ]*Global("LaolaSpawn","GLOBAL",0)[%WNL%%MNL%%LNL%%TAB% ]*Global("ScarMission","GLOBAL",1)~
		~Global("LaolaSpawn","GLOBAL",0)~
		REPLACE_TEXTUALLY ~CreateCreature("LAOLA",\[[0-9]+\.[0-9]+\],\(1?[0-9]\|[NS]?[NS]?[WE]?[WE]?\))~ ~CreateCreature("LAOLA",[3909.1387],0)~
	END
BUT_ONLY


COPY_EXISTING ~RASHAD.DLG~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~ApplySpell(Myself,DOPPLEGANGER_CHANGE_DEFAULT)~ ~UseItem("JA#DPKIZ",Myself)~
	END
BUT_ONLY


LAF GET_DLG_STRREF
	INT_VAR idx_state = 1
	STR_VAR dlg_file = ~DOPPSE.DLG~
	RET doppse_state01_journal = journal_strref
END
LAF GET_DLG_STRREF
	INT_VAR idx_state = 3
	STR_VAR dlg_file = ~DOPPSE.DLG~
	RET doppse_state03_journal = journal_strref
END
LAF GET_DLG_STRREF
	INT_VAR idx_state = 3
	STR_VAR dlg_file = ~DOPPSM.DLG~
	RET doppsm_state03_journal = journal_strref
END

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Seven_Suns/ja#doppse.d~ // Doppelganger + merchants
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Seven_Suns/ja#ssung.d~ // Guards

ACTION_IF GAME_IS ~bgt~ BEGIN
	<<<<<<<< ...inlined/doppsm_bgt.d
	ALTER_TRANS DOPPSM
	BEGIN 3 7 8 END
	BEGIN END
	BEGIN
	  "UNSOLVED_JOURNAL" ~#%doppse_state01_journal%~
	END
	>>>>>>>>
	COMPILE EVALUATE_BUFFER ~...inlined/doppsm_bgt.d~

	// Done in JA#BGT_AdvPack/Seven_Suns/ja#scar_bgt.d
	// COPY_EXISTING ~SCAR.DLG~ ~override~
	// 	DECOMPILE_AND_PATCH BEGIN
	// 		REPLACE_EVALUATE ~Global\(GT\)?("KilledDopple","GLOBAL",\([0-9]+\))~ BEGIN
	// 			SET num = (~%MATCH1%~ STR_EQ ~GT~) ? MATCH2 : MATCH2 - 1
	// 		END ~NumDeadGT("SSDOPPLE",%num%)~
	//    END
	// BUT_ONLY

	COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Seven_Suns/ja#scar_bgt.d~ USING ~JA#BGT_AdvPack/languages/%s/ja#scar.tra~
END

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Seven_Suns/ja#laola.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Seven_Suns/ja#jhasso.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Seven_Suns/ja#ivanne.d~

ACTION_IF GAME_IS ~bgt~ BEGIN
	WITH_TRA ~JA#BGT_AdvPack/languages/%s/ja#jhasso.tra~ BEGIN
		OUTER_SET JA#JHASSO_20 = STATE_WHICH_SAYS @50 FROM ~JHASSO.DLG~
		OUTER_SET JA#JHASSO_21 = STATE_WHICH_SAYS @52 FROM ~JHASSO.DLG~

		<<<<<<<< ...inlined/jhasso_bgt.d
		ADD_TRANS_ACTION JHASSO
		BEGIN %JA#JHASSO_20% %JA#JHASSO_21% END
		BEGIN END
		~EraseJournalEntry(@1012) EraseJournalEntry(@1013) EraseJournalEntry(@1014)~

		ALTER_TRANS JHASSO
		BEGIN %JA#JHASSO_20% %JA#JHASSO_21% END
		BEGIN 0 END
		BEGIN
		  "SOLVED_JOURNAL" ~@51~
		END
		>>>>>>>>
		COMPILE EVALUATE_BUFFER ~...inlined/jhasso_bgt.d~
	END
END
ACTION_IF GAME_IS ~bgee eet~ BEGIN
	<<<<<<<< ...inlined/harbor_ee.d
	ADD_TRANS_ACTION HARBOR
	BEGIN 9 END
	BEGIN END
	~EraseJournalEntry(@1012) EraseJournalEntry(@1013) EraseJournalEntry(@1014)~
	>>>>>>>>
	COMPILE EVALUATE_BUFFER ~...inlined/harbor_ee.d~

	ADD_JOURNAL EXISTING @1012 @1013 @1014
END


COPY_EXISTING ~DOPPSM.CRE~ ~override~
	LPF REMOVE_CRE_SCRIPT STR_VAR script_old = ~INITDLG~ END
	LPF INSERT_CRE_SCRIPT INT_VAR offset = SCRIPT_OVERRIDE STR_VAR script_new = ~JA#SSMER~ END
	WRITE_BYTE 0x271 1 // General: HUMANOID
	WRITE_BYTE 0x272 1 // Race: HUMAN
	WRITE_BYTE 0x273 155 // Class: INNOCENT
BUT_ONLY

COPY_EXISTING ~RASHAD.CRE~ ~override~
	LPF REPLACE_CRE_SCRIPT STR_VAR script_old = ~GDCHANGE~ script_new = ~JA#DPKIZ~ END
	ADD_CRE_ITEM ~JA#DPKIZ~ #0 #0 #0 ~IDENTIFIED~ ~LRING~ // Turn into JA#DPKIZ.CRE (Kizska)
BUT_ONLY


COPY_EXISTING ~%SWBaldursGate_SevenSuns_L1%.ARE~ ~override~
	// add new actor
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 472
		fj_loc_y          = 272
		fj_dest_x         = 472
		fj_dest_y         = 272
		fj_orientation    = 1
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Guard~
		fj_cre_resref     = ~JA#SSGU1~
	END
	LPF MUTE_AREA_SONG END
BUT_ONLY

COPY_EXISTING ~%SWBaldursGate_SevenSuns_L2%.ARE~ ~override~
	GET_OFFSET_ARRAY actors ARE_V10_ACTORS
	PHP_EACH actors AS i => actor BEGIN
		READ_SHORT actor+0x20 actor_pos_x
		READ_ASCII actor+0x80 actor_filename
		PATCH_MATCH ~%actor_pos_x%~ WITH
			436 WHEN (~%actor_filename%~ STR_EQ ~DOPMER~) BEGIN
				WRITE_SHORT actor+0x20 360
				WRITE_SHORT actor+0x22 468
				WRITE_SHORT actor+0x24 360
				WRITE_SHORT actor+0x26 468
				WRITE_SHORT actor+0x34 11
			END
			523 WHEN (~%actor_filename%~ STR_EQ ~DOPMER~) BEGIN
				WRITE_SHORT actor+0x20 281
				WRITE_SHORT actor+0x22 255
				WRITE_SHORT actor+0x24 281
				WRITE_SHORT actor+0x26 255
				WRITE_SHORT actor+0x34 1
			END
			DEFAULT
		END
	END
	// add new actors
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 454
		fj_loc_y          = 356
		fj_dest_x         = 454
		fj_dest_y         = 356
		fj_orientation    = 4
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~JhassoDopp~
		fj_cre_resref     = ~JA#JHASS~
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 424
		fj_loc_y          = 311
		fj_dest_x         = 424
		fj_dest_y         = 311
		fj_orientation    = 15
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~MercDopp~
		fj_cre_resref     = ~JA#SSMR2~
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 365
		fj_loc_y          = 342
		fj_dest_x         = 365
		fj_dest_y         = 342
		fj_orientation    = 13
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~MercDopp~
		fj_cre_resref     = ~JA#SSMR1~
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 628
		fj_loc_y          = 262
		fj_dest_x         = 628
		fj_dest_y         = 262
		fj_orientation    = 6
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Guard~
		fj_cre_resref     = ~JA#SSGU4~
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 519
		fj_loc_y          = 230
		fj_dest_x         = 519
		fj_dest_y         = 230
		fj_orientation    = 8
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~MercDopp~
		fj_cre_resref     = ~JA#SSMR3~
	END
	LPF MUTE_AREA_SONG END
BUT_ONLY

COPY_EXISTING ~%SWBaldursGate_SevenSuns_Cellar%.ARE~ ~override~
	LPF ALTER_AREA_ACTOR
		INT_VAR
		x_coord     = 440
		y_coord     = 435
		dest_x      = 440
		dest_y      = 435
		orient      = 7
		STR_VAR
		actor_name  = ~Jhasso~
	END
	LPF DELETE_AREA_ACTOR STR_VAR cre_to_delete = ~DOPPSS~ END
	// add new actors
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 263
		fj_loc_y          = 417
		fj_dest_x         = 263
		fj_dest_y         = 417
		fj_orientation    = 15
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Guard1~
		fj_cre_resref     = ~JA#SSGU2~
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 200
		fj_loc_y          = 366
		fj_dest_x         = 200
		fj_dest_y         = 366
		fj_orientation    = 0
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Guard2~
		fj_cre_resref     = ~JA#SSGU3~
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 368
		fj_loc_y          = 349
		fj_dest_x         = 368
		fj_dest_y         = 349
		fj_orientation    = 2
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Guard3~
		fj_cre_resref     = ~JA#SSGU3~
	END
	LPF MUTE_AREA_SONG END
BUT_ONLY
