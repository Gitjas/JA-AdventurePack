///////////////////////
// Sorcerous Sundries
///////////////////////


COPY ~JA#BGT_AdvPack/Sorcerous_Sundries/JA#TAY01.CRE~ ~override~
	SAY NAME1 @6
	SAY NAME2 @7

COPY ~JA#BGT_AdvPack/Sorcerous_Sundries/JA#SSC01.CRE~ ~override~
	SAY NAME1 @8
	SAY NAME2 @9

COPY ~JA#BGT_AdvPack/Sorcerous_Sundries/JA#SSELF.CRE~ ~override~
	SAY NAME1 @10
	SAY NAME2 @10


COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Sorcerous_Sundries/JA#HLBZ1.BAF~


// BGEE/EET: Connect specific response text to state 7 (Advantage: edit any COPY_TRANS HALBAZ of previous mods)
COPY_EXISTING ~HALBAZ.DLG~ ~override~
	GET_OFFSET_ARRAY responses 0x14 4 0x10 4 0 0 0x20
	PHP_EACH responses AS i => response BEGIN
		READ_LONG (response + 0x04) strref
		PATCH_IF ((strref MODULO 200000) = 192) BEGIN
			WRITE_LONG (response + 0x1c) 7 // Point to next state
		END
	END
BUT_ONLY


COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Sorcerous_Sundries/ja#halbaz.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Sorcerous_Sundries/ja#tay01.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Sorcerous_Sundries/ja#ssc01.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Sorcerous_Sundries/ja#sself.d~


COPY_EXISTING ~HALBAZ.CRE~ ~override~
	WRITE_ASCII SCRIPT_DEFAULT ~JA#HLBZ1~ #8
BUT_ONLY


COPY ~JA#BGT_AdvPack/Sorcerous_Sundries/JA#AMSS1.WAV~ ~override~

COPY_EXISTING ~%EBaldursGate_SorcerousSundries_L1%.ARE~ ~override~
	// add new actors
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 238
		fj_loc_y          = 697
		fj_dest_x         = 238
		fj_dest_y         = 697
		fj_orientation    = 5
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Roter Magier~
		fj_cre_resref     = ~JA#TAY01~
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 827
		fj_loc_y          = 274
		fj_dest_x         = 827
		fj_dest_y         = 274
		fj_orientation    = 14
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Velara~
		fj_cre_resref     = ~JA#SSC01~
	END
	// add ambient
	LPF fj_are_structure
		INT_VAR
		fj_loc_x           = 50
		fj_loc_y           = 50
		fj_radius          = 500
		fj_volume_variance = 100
		fj_sound_num       = 1
		fj_flags           = 0b0000000111
		STR_VAR
		fj_structure_type = ambient
		fj_name           = ~Main Ambient~
		fj_wav_resref0    = ~JA#AMSS1~
	END
	LPF MUTE_AREA_SONG END
BUT_ONLY

COPY_EXISTING ~%EBaldursGate_SorcerousSundries_L2%.ARE~ ~override~
	// add new actor
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 709
		fj_loc_y          = 519
		fj_dest_x         = 709
		fj_dest_y         = 519
		fj_orientation    = 11
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Naiduja~
		fj_cre_resref     = ~JA#SSELF~
	END
	// add ambient
	LPF fj_are_structure
		INT_VAR
		fj_loc_x           = 50
		fj_loc_y           = 50
		fj_radius          = 500
		fj_volume_variance = 80
		fj_sound_num       = 1
		fj_flags           = 0b0000000111
		STR_VAR
		fj_structure_type = ambient
		fj_name           = ~Main Ambient~
		fj_wav_resref0    = ~JA#AMSS1~
	END
	LPF MUTE_AREA_SONG END
BUT_ONLY
