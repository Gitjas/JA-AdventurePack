//////////////////////////////////////////////////////
// RESTORED: Marl (tarvern brawl in Feldepost's Inn)
//////////////////////////////////////////////////////


COPY ~JA#BGT_AdvPack/Marl/JA#GUAB1.CRE~ ~override~
	 ~JA#BGT_AdvPack/Marl/JA#GUAB2.CRE~ ~override~
	 ~JA#BGT_AdvPack/Marl/JA#GUAB3.CRE~ ~override~
	SAY NAME1 @2085
	SAY NAME2 @2085


COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Marl/JA#DUNKI.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Marl/JA#GBECU.BAF~


EXTEND_BOTTOM ~MARL.BCS~ ~JA#BGT_AdvPack/Marl/MARL.BAF~ EVALUATE_BUFFER

EXTEND_TOP ~%Beregost_FeldepostsInn_L1_BCS%.BCS~ ~JA#BGT_AdvPack/Marl/AR6751.BAF~ EVALUATE_BUFFER


LAF GET_DLG_SCRIPT
	INT_VAR idx_state = 1
	STR_VAR dlg_file = ~KELDDA.DLG~
	RET state01_weight = idx_state_trigger
		state01_trigger = state_trigger
		state01_response0_action = response_action
END
OUTER_PATCH_SAVE ~state01_response0_action~ ~%state01_response0_action%~ BEGIN
	REPLACE_TEXTUALLY ~GiveGoldForce([0-9]+)~ ~GiveGoldForce(2000)~
	REPLACE_EVALUATE ~AddexperienceParty(\([0-9]+\))~ BEGIN
		SET result = MATCH1 / 2
	END ~AddexperienceParty(%result%)~
END

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Marl/ja#keldda.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Marl/ja#marl.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Marl/ja#dunkin.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Marl/ja#guab1.d~

ADD_JOURNAL EXISTING @0 USING ~JA#BGT_AdvPack/languages/%s/ja#keldda.tra~


COPY_EXISTING ~MARL.CRE~ ~override~
	WRITE_BYTE 0x273 155 // Class: innocent (reputation loss when killed)
	REMOVE_CRE_ITEM ~B1-2~
BUT_ONLY

COPY_EXISTING ~DUNKIN.CRE~ ~override~
	LPF INSERT_CRE_SCRIPT INT_VAR offset = SCRIPT_OVERRIDE STR_VAR script_new = ~JA#DUNKI~ END
BUT_ONLY
