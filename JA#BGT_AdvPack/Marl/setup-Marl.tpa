///////////////////////////////
//RESTORED: Marl & the Guard
///////////////////////////////


COPY_EXISTING ~MARL.CRE~ ~override~
	// WRITE_BYTE 0x273 155	//innocent - reputation
	REMOVE_CRE_ITEM ~B1-2~

COPY_EXISTING ~DUNKIN.CRE~ ~override~
	LPF INSERT_CRE_SCRIPT STR_VAR script_new = ~JA#DUNKI~ END

COPY ~JA#BGT_AdvPack/Marl/JA#GUAB1.CRE~ ~override~
	~JA#BGT_AdvPack/Marl/JA#GUAB2.CRE~ ~override~
	~JA#BGT_AdvPack/Marl/JA#GUAB3.CRE~ ~override~
	SAY NAME1 @2085
	SAY NAME2 @2085


// Grab trigger and action text from first response of state 1
COPY_EXISTING ~KELDDA.DLG~ ~override~
	SET state = 1
    READ_LONG 0x0c ofs_states
    READ_LONG 0x14 ofs_responses
    READ_LONG 0x18 ofs_state_triggers
    READ_LONG 0x28 ofs_actions

	SET state = ofs_states + (state * 0x10)
	READ_LONG (state + 0x4) idx_first_response
	READ_LONG (state + 0xc) idx_state1_trigger

	SET trigger = ofs_state_triggers + (idx_state1_trigger * 0x8)
	READ_LONG (trigger + 0x0) ofs_trigger
	READ_LONG (trigger + 0x4) len_trigger
	READ_ASCII ofs_trigger state1_trigger (len_trigger) NULL

	SET first_response = ofs_responses + (idx_first_response * 0x20)
	// READ_STRREF (first_response + 0x08) state1_response0_journal
	READ_LONG (first_response + 0x10) idx_action

	SET action = ofs_actions + (idx_action * 0x8)
	READ_LONG (action + 0x0) ofs_action
	READ_LONG (action + 0x4) len_action
	READ_ASCII ofs_action state1_response0_action (len_action) NULL
BUT_ONLY

OUTER_PATCH_SAVE ~state1_response0_action~ ~%state1_response0_action%~ BEGIN
	REPLACE_TEXTUALLY ~GiveGoldForce([0-9]+)~ ~GiveGoldForce(2000)~
	REPLACE_EVALUATE ~AddExperienceParty(\([0-9]+\))~ BEGIN
		SET result = MATCH1 / 2
	END ~AddExperienceParty(%result%)~
END

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Marl/JA#KELDDA.D~
ADD_JOURNAL EXISTING @0 USING ~JA#BGT_AdvPack/languages/%s/ja#keldda.tra~

COMPILE ~JA#BGT_AdvPack/Marl/JA#MARL.D~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Marl/JA#DUNKIN.D~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Marl/JA#GUAB1.D~


COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Marl/JA#DUNKI.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Marl/JA#GBECU.BAF~
EXTEND_BOTTOM ~MARL.BCS~ ~JA#BGT_AdvPack/Marl/MARL.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%Beregost_FeldepostsInn_L1%.BCS~ ~JA#BGT_AdvPack/Marl/AR6751.BAF~ EVALUATE_BUFFER
