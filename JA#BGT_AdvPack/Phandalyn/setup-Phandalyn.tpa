////////////////////////////////////////
//FIXED: Moved Phandalyn to the Elfsong
////////////////////////////////////////

// Remove SHOUT script - or the whole inn will turn hostile!
COPY_EXISTING ~PHANDA.CRE~ ~override~
	//loop through cre scripts - remove SHOUT.BCS, add PHANDALY.BCS one time if it does not exist
	FOR (script = 0x248 phandaly = 0; script <= 0x268; script+=0x08) BEGIN
		READ_ASCII script script_filename
		PATCH_MATCH ~%script_filename%~ WITH
			~SHOUT~ BEGIN
				WRITE_ASCII script ~None~ #8
			END
			~None~ ~PHANDALY~ WHEN !phandaly BEGIN
				SET phandaly = 1
				WRITE_ASCII script ~PHANDALY~ #8
			END
			DEFAULT
		END
	END
BUT_ONLY


// Cycle through the existing actors, first (in case area was altered by another mod)
COPY_EXISTING ~%SWBaldursGate_Tavern_L1%.are~ ~override~
	//remove Phandalyn
	GET_OFFSET_ARRAY actors ARE_V10_ACTORS
	FOR (i = (SHORT_AT 0x58)-1; i >= 0; --i) BEGIN
		READ_ASCII $actors("%i%") actor_name (32) NULL
		PATCH_IF (~%actor_name%~ STR_EQ ~Phandalyn~) BEGIN
			LPF fj_are_structure
				INT_VAR fj_delete_mode = i
				STR_VAR fj_structure_type = ~actor~
			END
		END
	END
BUT_ONLY

// Correct Phandalyn's script: should talk before attacking evil party members
COPY_EXISTING ~PHANDALY.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~Attack(\[.*MASK_EVIL\])~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~\(%textToReplace%\)~ ~Dialogue([PC])~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

EXTEND_TOP ~PHANDALY.BCS~ ~JA#BGT_AdvPack/Phandalyn/JA#PHANDALY.BAF~

COMPILE ~JA#BGT_AdvPack/Phandalyn/JA#Phanda.d~
COMPILE ~JA#BGT_AdvPack/Phandalyn/JA#Bellam.d~
	PATCH_IF GAME_IS ~bgt~ BEGIN
		REPLACE_TEXTUALLY ~#26870~ ~@1~
	END
	PATCH_IF GAME_IS ~eet~ BEGIN
		REPLACE_TEXTUALLY ~#26870~ ~#226870~
	END

COPY_EXISTING ~%EBaldursGate_ElfsongTavern_L1%.ARE~ ~override~
	//add new actor
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 980
		fj_loc_y          = 577
		fj_dest_x         = 980
		fj_dest_y         = 577
		fj_orientation    = 1
		fj_schedule		  = 0b000000011111111111100000
		STR_VAR
		fj_structure_type = actor
		fj_name           = Phandalyn
		fj_cre_resref     = PHANDA
		fj_dlg_resref	  = PHANDA
	END
BUT_ONLY
