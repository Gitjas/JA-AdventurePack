////////////////////////////////////////
//FIXED: Moved Phandalyn to the Elfsong
////////////////////////////////////////

/* remove SHOUT script - or the whole inn will turn hostile! */
COPY_EXISTING ~PHANDA.CRE~ ~override~
	WRITE_ASCIIE 0x248 ~PHANDALY~ #8       // remove SHOUT OVERRIDE script
BUT_ONLY




/* this is not fail-proof in case other mods changed the area already
COPY_EXISTING ~AR8009.ARE~ ~override~
PATCH_IF (SOURCE_SIZE > 0x11b) BEGIN 
READ_SHORT 0x54 "JA#OFF_ACT"
WRITE_LONG "JA#OFF_ACT" + (0x110 * 15) + 0x38        0x00000000	//Remove Phandalyn
END
BUT_ONLY
*/


/* Better: cycle through the existing actors, first (in case area was altered by another mod) */

COPY_EXISTING ~%SWBaldursGate_Tavern_L1%.are~ ~override~
	READ_LONG  0x54 actor_off
	READ_SHORT 0x58 actor_num
	FOR (index = 0 ; index < actor_num ; ++index) BEGIN
		READ_ASCII (actor_off + 0x80 + (index * 0x110)) cre_file
		PATCH_IF ("%cre_file%" STRING_COMPARE_CASE "phanda" = 0) BEGIN // phandalyn's creature file
		WRITE_LONG (actor_off + 0x38 + (index * 0x110)) 0
		END
	END
BUT_ONLY

/* correct Phandalyn's script: should attack evil party member, not other evil (quest) NPCs! */
COPY_EXISTING ~PHANDALY.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~Attack(\[0\.0\.0\.0\.0\.0\.MASK_EVIL\])~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~\(%textToReplace%\)~ ~Attack(LastSeenBy(Myself))~
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

EXTEND_TOP ~PHANDALY.BCS~ ~JA#BGT_AdvPack/Phandalyn/PHANDALY.BAF~

//misc.d
COMPILE ~JA#BGT_AdvPack/Phandalyn/PHANDALY.D~
COMPILE ~JA#BGT_AdvPack/Phandalyn/Bellamy.D~