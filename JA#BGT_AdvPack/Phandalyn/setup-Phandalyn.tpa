////////////////////////////////////////
//FIXED: Moved Phandalyn to the Jopalins Tavern
////////////////////////////////////////

/* remove SHOUT script - or the whole inn will turn hostile! */
COPY_EXISTING ~PHANDA.CRE~ ~override~
	WRITE_ASCIIE 0x248 ~~ #8       // remove SHOUT OVERRIDE script
BUT_ONLY

/* Better: cycle through the existing actors, first (in case area was altered by another mod) */
COPY_EXISTING ~%SWBaldursGate_Tavern_L1%.ARE~ ~override~
	//delete not needed actors
	FOR (cnt=BYTE_AT 0x58 -1; cnt>=0; --cnt) BEGIN
		READ_ASCII SHORT_AT 0x54 +(0x110 * (cnt))+0x80 actors_filename (32) NULL
		PATCH_IF (~%actors_filename%~ STR_EQ  ~PHANDA~) BEGIN
			LPF fj_are_structure
				INT_VAR fj_delete_mode = EVAL %cnt%
				STR_VAR fj_structure_type = actor
			END
		END
	END
BUT_ONLY

COPY_EXISTING ~%BaldursGateDocks_JopalinsTavern%.ARE~ ~override~
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 738
		fj_loc_y          = 275
		fj_dest_x         = 738
		fj_dest_y         = 275
		fj_schedule		  = 0b000001111111111111100000 // during the day only
		fj_orientation    = 4     
		STR_VAR
		fj_structure_type = actor
		fj_name           = Phandalyn
		fj_cre_resref     = PHANDA
		fj_dlg_resref	  = PHANDA
	END

/* correct Phandalyn's script: should attack evil party member, not other evil (quest) NPCs! */
COPY_EXISTING ~PHANDALY.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~Attack(\[0\.0\.0\.0\.0\.0\.MASK_EVIL\])~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches
		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~\(%textToReplace%\)~ ~Continue()~ // attack is already handle by WDASIGHT.BCS
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY

COPY_EXISTING ~PHANDA.DLG~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~EscapeArea()~ ~NoAction()~
		REPLACE_TEXTUALLY ~Attack(\[0\.0\.0\.0\.0\.0\.MASK_EVIL\])~ ~ChangeClass(Myself,PALADIN) Enemy()~
	END
BUT_ONLY

EXTEND_TOP ~PHANDALY.BCS~ ~%MOD_FOLDER%/Phandalyn/JA#Phanda.BAF~

//misc.d
COMPILE ~%MOD_FOLDER%/Phandalyn/JA#Phanda.d~
USING ~%MOD_FOLDER%/languages/%s/JA#Phanda.tra~
COMPILE ~%MOD_FOLDER%/Phandalyn/JA#Bellamy.D~