//////////////////////////////////////
// Phandalyn waits in Elfsong Tavern
//////////////////////////////////////


EXTEND_TOP ~%EBaldursGate_ElfsongTavern_L1_BCS%.BCS~ ~JA#BGT_AdvPack/Phandalyn/AR7705.BAF~ EVALUATE_BUFFER


// Correct Phandalyn's script: should talk before attacking evil party members
COPY_EXISTING ~PHANDALY.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		SPRINT textToReplace ~Attack(\[0\.0\.0\.0\.0\.0\.MASK_EVIL\])~
		COUNT_REGEXP_INSTANCES ~%textToReplace%~ num_matches

		PATCH_IF (num_matches > 0) BEGIN
			REPLACE_TEXTUALLY ~%textToReplace%~ ~Continue()~ // attack is already handle by WDASIGHT.BCS
			PATCH_PRINT ~Patching: %num_matches% matches found in %SOURCE_FILESPEC% for REPLACE_TEXTUALLY: %textToReplace%~
		END ELSE BEGIN
			PATCH_WARN ~WARNING: could not find %textToReplace% in %SOURCE_FILESPEC%~
		END
	END
BUT_ONLY


COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Phandalyn/ja#phanda.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Phandalyn/ja#bellam.d~
	PATCH_MATCH _ WITH
		ANY GAME_IS ~bgt~ BEGIN
			REPLACE_TEXTUALLY EXACT_MATCH ~999999~ ~@1034~
		END
		ANY GAME_IS ~bgee~ BEGIN
			REPLACE_TEXTUALLY EXACT_MATCH ~999999~ ~#26870~
		END
		ANY GAME_IS ~eet~ BEGIN
			REPLACE_TEXTUALLY EXACT_MATCH ~999999~ ~#226870~
		END
		DEFAULT
	END


// Remove SHOUT script - or the whole inn will turn hostile!
COPY_EXISTING ~PHANDA.CRE~ ~override~
	LPF REMOVE_CRE_SCRIPT STR_VAR script_old = ~SHOUT~ END
BUT_ONLY


COPY_EXISTING ~%SWBaldursGate_Tavern_L1%.ARE~ ~override~
	LPF DELETE_AREA_ACTOR STR_VAR actor_to_delete = ~Phandalyn~ END
BUT_ONLY
