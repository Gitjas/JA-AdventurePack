////////////////////////////
//Improved Shop of Silence
////////////////////////////


LAF GET_DLG_STRREF
	INT_VAR idx_state = 4
	STR_VAR dlg_file = ~SILENC.DLG~
	RET silenc_state04_journal = journal_strref
END

// BGT version of SILENC.DLG is a lot behind the BGEE/EET version
//  => BGT Version wird an die Version von EET angeglichen
ACTION_IF GAME_IS ~bgt~ BEGIN
	COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Shop_of_Silence/JA#SILENC_bgt.D~ ~JA#BGT_AdvPack/Shop_of_Silence/JA#SILENC.D~
	  USING ~JA#BGT_AdvPack/languages/%s/JA#SILENC.tra~
	<<<<<<<< ...inlined/ja#silenc_weight.d
		SET_WEIGHT SILENC 8 #999
	>>>>>>>>
	COMPILE EVALUATE_BUFFER ~...inlined/ja#silenc_weight.d~
END
ACTION_IF GAME_IS ~eet bgee~ BEGIN
	COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Shop_of_Silence/JA#SILENC_ee.D~ ~JA#BGT_AdvPack/Shop_of_Silence/JA#SILENC.D~
	  USING ~JA#BGT_AdvPack/languages/%s/JA#SILENC.tra~
END

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Shop_of_Silence/JA#MONGO.D~
COMPILE ~JA#BGT_AdvPack/Shop_of_Silence/JA#STH01.D~
COMPILE ~JA#BGT_AdvPack/Shop_of_Silence/JA#STH02.D~
COMPILE ~JA#BGT_AdvPack/Shop_of_Silence/JA#SILC1.BAF~

COPY ~JA#BGT_AdvPack/Shop_of_Silence/JA#STH01.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Shop_of_Silence/JA#STH02.CRE~ ~override~

ACTION_IF FILE_EXISTS_IN_GAME ~MONGO.CRE~ BEGIN
	COPY_EXISTING ~MONGO.CRE~ ~override/JA#MONGO.CRE~
END ELSE BEGIN
	COPY ~JA#BGT_AdvPack/Shop_of_Silence/JA#MONGO.CRE~ ~override~
		SAY NAME1 @4
		SAY NAME2 @4
END
COPY_EXISTING ~JA#MONGO.CRE~ ~override~
	WRITE_BYTE  0x238 17 // More strength to match his dialog
	WRITE_BYTE  0x270 128
	WRITE_ASCII 0x2cc ~JA#MONGO~ #8


COPY ~JA#BGT_AdvPack/Shop_of_Silence/JA#MASK1.STO~ ~override~
	SAY 0xc @5

COPY_EXISTING ~%EBaldursGate_Inn_L2%.ARE~ ~override~
	//add new actors
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 556
		fj_loc_y          = 410
		fj_dest_x         = 556
		fj_dest_y         = 410
		fj_orientation    = 9
		STR_VAR
		fj_structure_type = actor
		fj_name           = Schattendieb
		fj_cre_resref     = JA#STH02
		fj_dlg_resref	  = JA#STH02
	END
BUT_ONLY

COPY_EXISTING ~%EBaldursGate_SilencesShop%.ARE~ ~override~
	LPF ALTER_AREA_ACTOR
		STR_VAR actor_name = ~Silence~
				script_specifics = ~JA#SILC1~
	END
	//add new actors
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 317
		fj_loc_y          = 367
		fj_dest_x         = 317
		fj_dest_y         = 367
		fj_orientation    = 2
		STR_VAR
		fj_structure_type = actor
		fj_name           = Schattendieb
		fj_cre_resref     = JA#STH01
		fj_dlg_resref	  = JA#STH01
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 581
		fj_loc_y          = 164
		fj_dest_x         = 581
		fj_dest_y         = 164
		fj_orientation    = 1
		STR_VAR
		fj_structure_type = actor
		fj_name           = Mongo
		fj_cre_resref     = JA#MONGO
		fj_dlg_resref	  = JA#MONGO
	END
	// Add Ambient 1
	LPF fj_are_structure
		INT_VAR
		fj_loc_x           = 90
		fj_loc_y           = 50
		fj_radius          = 500
		fj_volume_variance = 90
		fj_sound_num       = 1
		fj_flags 		   = 0b0000000111
		STR_VAR
		fj_structure_type = ambient
		fj_name           = ~Main Ambient~
		fj_wav_resref0    = GAHOUSE3
	END
BUT_ONLY
