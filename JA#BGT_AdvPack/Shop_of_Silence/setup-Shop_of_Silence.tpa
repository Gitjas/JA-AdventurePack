///////////////////////////////////////
//VERBESSERTES GESCHAEFT DER STILLE
///////////////////////////////////////

//Im Blade & Stars: Der Besitzer hei√üt Aundegul Shawn, spricht wenn Geld kommt

// safty check for intalled mod
ACTION_IF MOD_IS_INSTALLED ~GARRICK-TT.TP2~ ~0~ BEGIN
	OUTER_SET check_for_garrick = 1
	OUTER_SET check_for_garrick1 = 2
	ACTION_IF GAME_IS ~bgt~ BEGIN
		<<<<<<<< ...inlined/JA#Silenc_garrick.d
		
		ALTER_TRANS SILENC // file name
		BEGIN 1 END // state number (can be more than one)
		BEGIN %check_for_garrick1% END // transition number (can be more than one)
		BEGIN // list of changes, see below for flags
		  "REPLY" ~#%garrick1%~
		END
		>>>>>>>>
	END ELSE ACTION_IF GAME_IS ~eet bgee~ BEGIN
		<<<<<<<< ...inlined/JA#Silenc_garrick.d

		ALTER_TRANS SILENC // file name
		BEGIN 2 END // state number (can be more than one)
		BEGIN %check_for_garrick1% END // transition number (can be more than one)
		BEGIN // list of changes, see below for flags
		  "REPLY" ~#%garrick1%~
		END
		>>>>>>>>
	END
END ELSE BEGIN
	OUTER_SET check_for_garrick = 0
	OUTER_SET check_for_garrick1 = 1
	<<<<<<<< ...inlined/JA#Silenc_garrick.d
	>>>>>>>>
END

ACTION_IF GAME_IS ~bgt~ BEGIN
	COPY_EXISTING ~SILENC.dlg~ ~override~		
		LPF ~READ_RESPONSE_STRREF~
			INT_VAR
			state         = "1"  // state index
			stateResponse = 1     // relative response index (e.g. 0 for first response associated with the state, 1 for second response, and so on...)
			RET
			strref    // returned string reference, or -1 if not available
		END
	SET garrick1 = strref

	COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/Shop_of_Silence/JA#SILENC.D~
	COMPILE EVALUATE_BUFFER ~...inlined/JA#Silenc_garrick.d~	

END
ACTION_IF GAME_IS ~eet bgee~ BEGIN
	COPY_EXISTING ~SILENC.dlg~ ~override~		
		LPF ~READ_RESPONSE_STRREF~
			INT_VAR
			state         = "2"  // state index
			stateResponse = 1     // relative response index (e.g. 0 for first response associated with the state, 1 for second response, and so on...)
			RET
			strref    // returned string reference, or -1 if not available
		END
	SET garrick1 = strref

	COPY_EXISTING ~SILENC.DLG~ ~override~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY EXACT_MATCH ~!Global("Chapter","GLOBAL",7)~ ~GlobalLT("Chapter","GLOBAL",%tutu_chapter_7%)~
		END	
	COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/Shop_of_Silence/JA#SILENC_eet.D~ USING ~%MOD_FOLDER%/languages/%s/JA#SILENC.tra~
	COMPILE EVALUATE_BUFFER ~...inlined/JA#Silenc_garrick.d~	
END

COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/Shop_of_Silence/JA#MONGO.D~
COMPILE ~%MOD_FOLDER%/Shop_of_Silence/JA#STH01.D~
COMPILE ~%MOD_FOLDER%/Shop_of_Silence/JA#STH02.D~
COMPILE ~%MOD_FOLDER%/Shop_of_Silence/JA#SILC1.BAF~
ACTION_IF !FILE_EXISTS_IN_GAME ~JA#RUM01.DLG~ BEGIN // rumors needed to reveal the temple of Mask
	ACTION_IF GAME_IS ~bgt~ BEGIN
		OUTER_SPRINT storename ~TAV0705~
	END
	ACTION_IF GAME_IS ~eet bgee~ BEGIN
		OUTER_SPRINT storename ~inn0705~
	END
	COPY_EXISTING ~%storename%.STO~ ~override~
		WRITE_ASCII 0x44 ~JA#RUM01~ #8
	COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/Miniquest/JA#RUM01.D~
END

COPY ~%MOD_FOLDER%/Shop_of_Silence/JA#STH01.CRE~ ~override~
COPY ~%MOD_FOLDER%/Shop_of_Silence/JA#STH02.CRE~ ~override~
COPY ~%MOD_FOLDER%/Shop_of_Silence/JA#MONGO.CRE~ ~override~
	SAY NAME1 @4
	SAY NAME2 @4

COPY ~%MOD_FOLDER%/Shop_of_Silence/JA#MASK1.STO~ ~override~
	SAY 0xc @5

COPY_EXISTING ~%EBaldursGate_Inn_L2%.ARE~ ~override~
	//add new actors
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 556
		fj_loc_y          = 410
		fj_dest_x         = 556
		fj_dest_y         = 410
		fj_orientation    = 9      
		STR_VAR
		fj_structure_type = actor
		fj_name           = Shadow thief
		fj_cre_resref     = JA#STH02
		fj_dlg_resref	  = JA#STH02
	END
BUT_ONLY

COPY_EXISTING ~%EBaldursGate_SilencesShop%.ARE~ ~override~
	GET_OFFSET_ARRAY actors_array ARE_V10_ACTORS 
	PHP_EACH actors_array AS actors_num => actors_offset BEGIN
		READ_ASCII actors_offset actors_name (32) NULL
		READ_SHORT actors_offset+0x20 actors_pos_x
		READ_ASCII actors_offset+0x80 actors_filename (32) NULL
		PATCH_IF (~%actors_filename%~ STR_EQ  ~SILENC~) BEGIN
			WRITE_ASCII actors_offset+0x78        ~JA#SILC1~ #8	
		END
	END
	//add new actors
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 317
		fj_loc_y          = 367
		fj_dest_x         = 317
		fj_dest_y         = 367
		fj_orientation    = 2      
		STR_VAR
		fj_structure_type = actor
		fj_name           = Shadow thief
		fj_cre_resref     = JA#STH01
		fj_dlg_resref	  = JA#STH01
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 581
		fj_loc_y          = 164
		fj_dest_x         = 581
		fj_dest_y         = 164
		fj_orientation    = 1      
		STR_VAR
		fj_structure_type = actor
		fj_name           = Mongo
		fj_cre_resref     = JA#MONGO
		fj_dlg_resref	  = JA#MONGO
	END
	// Add Ambient 1
	LPF fj_are_structure
		INT_VAR
		fj_loc_x           = 90
		fj_loc_y           = 50 
		fj_radius          = 500
		fj_volume_variance = 90 
		fj_sound_num       = 1
		fj_flags 		   = 0b0000000111
		STR_VAR
		fj_structure_type = ambient
		fj_name           = ~Main Ambient~
		fj_wav_resref0    = GAHOUSE3
	END
BUT_ONLY

//Mongo and JA#Mongo should not appear at the same time
//Mongo becomes Honk
ACTION_IF GAME_IS ~eet bgee~ BEGIN
	COPY_EXISTING ~Mongo.cre~ ~override/JA#Honk.cre~
		SAY NAME1 @44
		SAY NAME2 @44
		WRITE_EVALUATED_ASCII 0x280 ~Honk~ #32 // script name / DV
	<<<<<<<< ...inlined/JA#Silenc_mongo.d
	
	ALTER_TRANS SILENC // file name
	BEGIN 8 END // state number (can be more than one)
	BEGIN 0 END // transition number (can be more than one)
	BEGIN // list of changes, see below for flags
		"ACTION" ~DialogueInterrupt(FALSE)
			Wait(3)
			CreateCreature("JA#Honk",[661.372],NW)
			Enemy()~
	END
	>>>>>>>>
	COMPILE EVALUATE_BUFFER ~...inlined/JA#Silenc_mongo.d~
END
