REPLACE_TRIGGER_TEXT SILENC
~!Global("Chapter","GLOBAL",%tutu_chapter_7%)~
~GlobalLT("Chapter","GLOBAL",%tutu_chapter_7%)~

REPLACE_ACTION_TEXT SILENC
~CreateCreature("mongo",[^)]+)~
~ActionOverride("Mongo",Enemy())~



ALTER_TRANS SILENC
BEGIN 1 END
BEGIN 0 END
BEGIN
  "REPLY" ~@8~
END

EXTEND_BOTTOM SILENC 1
IF ~~ THEN REPLY @0 DO ~SetGlobal("SeenSanctuary","GLOBAL",1)~ EXIT
END


ADD_STATE_TRIGGER SILENC 4
~Global("JA#SILENCE_CH8","LOCALS",0)~

ADD_STATE_TRIGGER SILENC 5
~Global("JA#SILENCE_CH8","LOCALS",0)~

ADD_TRANS_ACTION SILENC
BEGIN 4 5 END
BEGIN 0 END
~SetGlobal("JA#SILENCE_CH8","LOCALS",1)~

ALTER_TRANS SILENC
BEGIN 4 5 END
BEGIN 0 END
BEGIN
  "REPLY" ~@10~
  "JOURNAL" ~@1032~
END

EXTEND_BOTTOM SILENC 5
IF ~Global("JA#SILENCE_MASK","LOCALS",1) !InParty("TIAX")~ THEN REPLY @2 DO ~StartStore("JA#MASK1",LastTalkedToBy(Myself))~ EXIT
IF ~Global("JA#SILENCE_MASK","LOCALS",1) InParty("TIAX")~ THEN REPLY @2 GOTO JA#SILENC_3
END

EXTEND_BOTTOM SILENC 4 5
IF ~~ THEN REPLY @0 DO ~SetGlobal("JA#SILENCE_CH8","LOCALS",1)~ JOURNAL @1032 EXIT
END


ALTER_TRANS SILENC
BEGIN 9 END
BEGIN 0 END
BEGIN
  "REPLY" ~@10~
END

EXTEND_BOTTOM SILENC 9
IF ~Global("JA#SILENCE_MASK","LOCALS",1) !InParty("TIAX")~ THEN REPLY @2 DO ~StartStore("JA#MASK1",LastTalkedToBy(Myself))~ EXIT
IF ~Global("JA#SILENCE_MASK","LOCALS",1) InParty("TIAX")~ THEN REPLY @2 GOTO JA#SILENC_3
IF ~~ THEN REPLY @0 EXIT
END


APPEND SILENC

IF WEIGHT #3 ~Global("SeenSanctuary","GLOBAL",1) Global("JA#KNOW_MASKTEMPLE","GLOBAL",1) Global("JA#SILENCE_MASK","LOCALS",0)~ THEN BEGIN JA#SILENC_1
SAY @3
IF ~~ THEN REPLY @4 DO ~StartStore("stosilen",LastTalkedToBy(Myself))~ EXIT
IF ~!InParty("TIAX")~ THEN REPLY @5 DO ~SetGlobal("JA#SILENCE_MASK","LOCALS",1)~ GOTO JA#SILENC_2
IF ~InParty("TIAX")~ THEN REPLY @5 DO ~SetGlobal("JA#SILENCE_MASK","LOCALS",1)~ GOTO JA#SILENC_3
IF ~~ THEN REPLY @0 EXIT
END

IF ~~ THEN BEGIN JA#SILENC_2
SAY @6
IF ~~ THEN DO ~StartStore("JA#MASK1",LastTalkedToBy(Myself))~ EXIT
END

IF ~~ THEN BEGIN JA#SILENC_3
SAY @7
IF ~~ THEN EXIT
END

END // APPEND SILENC
