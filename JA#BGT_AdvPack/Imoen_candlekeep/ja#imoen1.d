ADD_STATE_TRIGGER IMOEN 0
~AreaCheck("%Candlekeep%")~

ADD_STATE_TRIGGER IMOEN 9
~AreaCheck("%Candlekeep%")~


ALTER_TRANS IMOEN
BEGIN 9 END
BEGIN 0 END
BEGIN "EPILOGUE" ~GOTO JA#IMOEN11~ END


APPEND IMOEN

IF ~~ THEN BEGIN JA#IMOEN11
SAY @0 = @0 = @1
IF ~~ THEN DO ~SetGlobal("JA#IMOEN_WINTH","GLOBAL",1) MoveToPoint([2066.1868]) DestroySelf()~ EXIT
END

IF ~Global("JA#IMOEN_WINTH","GLOBAL",2)
AreaCheck("%Candlekeep_Inn_L1%")~ THEN BEGIN JA#IMOEN14
SAY @2
IF ~~ THEN EXTERN INNKE2 JA#WINTH01
END

IF ~~ THEN BEGIN JA#IMOEN15
SAY @3
IF ~~ THEN EXTERN INNKE2 JA#WINTH02
END

IF ~~ THEN BEGIN JA#IMOEN16
SAY @4
IF ~~ THEN DO ~SetGlobal("ImoenWinth","%Candlekeep_Inn_L1%",2) MoveToPoint([396.326]) SetGlobal("JA#IMOEN_WINTH","GLOBAL",3) EscapeAreaDestroy(90)~ EXIT
END

IF ~AreaCheck("%Candlekeep_Inn_L2%") Global("JA#IMOEN_WINTH","GLOBAL",4)~ THEN BEGIN JA#IMOEN17
SAY @5 = @6
IF ~~ THEN REPLY @7 GOTO JA#IMOEN19
IF ~~ THEN REPLY @8 GOTO JA#IMOEN20
END

IF ~~ THEN BEGIN JA#IMOEN19
SAY @9
IF ~~ THEN REPLY @10 GOTO JA#IMOEN21
IF ~~ THEN REPLY @11 GOTO JA#IMOEN20
END

IF ~~ THEN BEGIN JA#IMOEN20
SAY @12
IF ~~ THEN REPLY @13 DO ~GiveGoldForce(17)~ GOTO JA#IMOEN22
END

IF ~~ THEN BEGIN JA#IMOEN21
SAY @14
IF ~~ THEN REPLY @13 GOTO JA#IMOEN22
END

IF ~~ THEN BEGIN JA#IMOEN22
SAY @15
IF ~~ THEN DO ~SetGlobal("JA#IMOEN_WINTH","GLOBAL",5) EscapeAreaDestroy(3)~ EXIT
END

END //End Append



APPEND INNKE2

IF ~~ THEN BEGIN JA#WINTH01
SAY @16
IF ~~ THEN EXTERN IMOEN JA#IMOEN15
END

IF ~~ THEN BEGIN JA#WINTH02
SAY @17
IF ~~ THEN GOTO JA#WINTH03
END

IF ~~ THEN BEGIN JA#WINTH03
SAY @18
IF ~~ THEN EXTERN IMOEN JA#IMOEN16
END

END //End Append
