/////////////////////////////////////
//RESTORED: Aldeth's hunter comrade
/////////////////////////////////////

COPY ~%MOD_FOLDER%/Aldeth_hunter_comrade/JA#PALIN.CRE~ ~override~
	SAY NAME1 @2054
	SAY NAME2 @2054

/* bg1npc is installed: Viconia's interjection kills the new reply option to settle the discpute with Seniyad peacefully. Add the reply option to Viconia's new state, as well: */

OUTER_SET vic_state = "-4"
OUTER_SET jaheira_state = "-4"

ACTION_IF ((MOD_IS_INSTALLED ~BG1NPC/BG1NPC.TP2~ "10") //bg1npc interjections and quests is installed
		    AND (FILE_EXISTS ~bg1npc/tra/english/x#viint.tra~)) BEGIN // only checking that the bg1npc folder is still in there

PRINT ~BG1 NPC Quest und Banter Component detected~

/* look for Vici's new dialogue state. Since we don't know which language version the player installed bg1npc in, we try all of them.
We are looking for @5 in bg1npc/tra/[LANGUAGE]/x#viint.tra
And also: to make the transition to Jaheira's Shadow Druid quest, i.e. to lead to Seniyad's reply to Jaheira's greeting: @95 in bg1npc/tra/[LANGUAGE]/x#jaqu.tra
*/

/* this will grab the correct state from the language bg1npc is installed in - this is great! */
OUTER_SET jaheira_state = STATE_WHICH_SAYS 95 IN ~bg1npc/tra/%s/x#jaqu.tra~ FROM ~%JAHEIRA_JOINED%.dlg~
OUTER_SET vic_state = STATE_WHICH_SAYS 5 IN ~bg1npc/tra/%s/x#viint.tra~ FROM ~%VICONIA_JOINED%.dlg~

END //ACTION_IF bg1npc interjections is installed

/* Baldurdash: Aldeth's special store. The additional dialogue state for Baldurdash will be deactivated to increase compatibility. The store can be started from the original dialogue state instead.
This has to be done before compiling the ja#aldeth.d */

COPY_EXISTING ~aldeth.dlg~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~\(Global("VP_MyBank","GLOBAL",1)\)~
		~False()~
	END
BUT_ONLY

/* If we know the state number in viconij.dlg we append to it */

<<<<<<<< ...inlined/aldeth_bg1npc.d

EXTEND_BOTTOM ~%VICONIA_JOINED%~ %vic_state%
  IF ~~ THEN REPLY @0 EXTERN SENIYA JA#SENIYA_00
END

/* more bg1npc compatibility: loop to Jaheira's quest, by Lumorus */

INTERJECT SENIYA JA#SENIYA_07 JA#SENIYA_06_BG1NPC
== %JAHEIRA_JOINED% IF ~InParty("jaheira") InMyArea("jaheira") !StateCheck("jaheira",CD_STATE_NOTVALID)~ THEN @68 
== SENIYA IF ~InParty("jaheira") InMyArea("jaheira") !StateCheck("jaheira",CD_STATE_NOTVALID)~ THEN @111
== %JAHEIRA_JOINED% IF ~InParty("jaheira") InMyArea("jaheira") !StateCheck("jaheira",CD_STATE_NOTVALID)~ THEN @112
== SENIYA IF ~InParty("jaheira") InMyArea("jaheira") !StateCheck("jaheira",CD_STATE_NOTVALID)~ THEN @113
END %JAHEIRA_JOINED% %jaheira_state%
>>>>>>>>

ACTION_IF ((vic_state >= 0) AND (jaheira_state >= 0)) BEGIN

	COMPILE EVALUATE_BUFFER 
		~...inlined/aldeth_bg1npc.d~ 
		~%MOD_FOLDER%/Aldeth_hunter_comrade/JA#ALDETH.D~
		USING ~%MOD_FOLDER%/languages/%s/JA#ALDETH.tra~

END ELSE BEGIN //Viconia interjection from bg1npc is not installed or couldn't be detected
  
	COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/Aldeth_hunter_comrade/JA#ALDETH.D~
END

COMPILE ~%MOD_FOLDER%/Aldeth_hunter_comrade/JA#PALIN.D~

COPY_EXISTING ~ALDETH.dlg~ ~override~
  LPF READ_JOURNAL_STRREF
    INT_VAR 
	state = 0
//	stateResponse = 0  //is already set to "0"
    RET strref
  END
  SET aldeth1 = strref

COMPILE ~%MOD_FOLDER%/Aldeth_hunter_comrade/JA#ALDETH.D~ EVALUATE_BUFFER

COMPILE ~%MOD_FOLDER%/Aldeth_hunter_comrade/JA#PALIN.BAF~

COPY_EXISTING ~%CloakwoodLodge%.ARE~ ~override~
	//add new actors
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 2540
		fj_loc_y          = 2084
		fj_dest_x         = 2540
		fj_dest_y         = 2084
		fj_orientation    = 14     
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Hunter~
		fj_cre_resref     = JA#PALIN
		fj_dlg_resref	  = JA#PALIN
	END

