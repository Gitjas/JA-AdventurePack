/////////////////////////////////////////////////////////////
//LAW-SYSTEM FRIENDLY ARM INN, ELFSONG TAVERN, BL. MERMAID
/////////////////////////////////////////////////////////////


COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/LawSystem/JA#FRIENDLYARM.D~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/LawSystem/JA#LAW.D~

EXTEND_TOP ~BENTLY.BCS~ ~JA#BGT_AdvPack/LawSystem/JA#FRGUB.BAF~
EXTEND_TOP ~%FriendlyArmInn_BCS%.BCS~ ~JA#BGT_AdvPack/LawSystem/AR6800.BAF~ EVALUATE_BUFFER

COMPILE ~JA#BGT_AdvPack/LawSystem/JA#FRINI.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/LawSystem/JA#FRCUT.BAF~
COMPILE ~JA#BGT_AdvPack/LawSystem/JA#FRGUA.BAF~
COMPILE ~JA#BGT_AdvPack/LawSystem/JA#FRGUB.BAF~
COMPILE ~JA#BGT_AdvPack/LawSystem/JA#GUAR1.BAF~
COMPILE ~JA#BGT_AdvPack/LawSystem/JA#GUAR2.BAF~

COMPILE ~JA#BGT_AdvPack/LawSystem/JA#LAW00.BAF~
	REPLACE ~999999~ @39
COMPILE ~JA#BGT_AdvPack/LawSystem/JA#LAW01.BAF~
COMPILE ~JA#BGT_AdvPack/LawSystem/JA#LAW02.BAF~
COMPILE ~JA#BGT_AdvPack/LawSystem/JA#LAW03.BAF~
COMPILE ~JA#BGT_AdvPack/LawSystem/JA#LAW04.BAF~

//From Dukal Palace and Husam Story
COMPILE ~JA#BGT_AdvPack/LawSystem/JA#LAW05.BAF~
COMPILE ~JA#BGT_AdvPack/LawSystem/JA#LAW06.BAF~


COPY ~JA#BGT_AdvPack/LawSystem/JA#LAW00.CRE~ ~override~
	~JA#BGT_AdvPack/LawSystem/JA#LAW01.CRE~ ~override~
	~JA#BGT_AdvPack/LawSystem/JA#LAW04.CRE~ ~override~
	~JA#BGT_AdvPack/LawSystem/JA#LAW05.CRE~ ~override~
	~JA#BGT_AdvPack/LawSystem/JA#LAW06.CRE~ ~override~
	SAY NAME1 @2035
	SAY NAME2 @2035

COPY ~JA#BGT_AdvPack/LawSystem/JA#LAW02.CRE~ ~override~
	~JA#BGT_AdvPack/LawSystem/JA#LAW03.CRE~ ~override~
	SAY NAME1 @2036
	SAY NAME2 @2036

COPY ~JA#BGT_AdvPack/LawSystem/JA#FACAP.CRE~ ~override~
	SAY NAME1 @2037
	SAY NAME2 @2037

COPY_EXISTING ~FAFIGHT.CRE~ ~override~
			  ~FRIEND.CRE~  ~override~
	WRITE_ASCII SCRIPT_RACE  ~JA#FRGUB~ #8
COPY_EXISTING ~GELLAN.CRE~ ~override~
	WRITE_ASCII SCRIPT_CLASS ~JA#FRGUB~ #8

/* same patching as in bg1ub */
COPY_EXISTING ~BENTLY.CRE~ ~override~	// Bently Mirrorshade, FAI
	WRITE_ASCII 0x248 ~BENTLY~ #8		// Assigns BENTLY.BCS as the OVERRIDE script
	WRITE_ASCII 0x250 ~SHOUT~  #8		// Assigns SHOUT.BCS as the Class script
BUT_ONLY


COPY_EXISTING ~%EBaldursGate_ElfsongTavern_L2%.ARE~ ~override~
	//change container scripts (dont spawn Guard Enforcer)
	GET_OFFSET_ARRAY containers ARE_V10_CONTAINERS
	PHP_EACH containers AS i => container BEGIN
		READ_ASCII container+0x48 container_script (8) NULL
		PATCH_IF !(~%container_script%~ STRING_COMPARE_REGEXP ~ACT[01][0-9]~) BEGIN
			WRITE_ASCII container+0x48 ~JA#LAW00~ #8
		END
	END

COPY_EXISTING ~%FriendlyArmInn%.ARE~ ~override~
	GET_OFFSET_ARRAY actors ARE_V10_ACTORS
	PHP_EACH actors AS i => actor BEGIN
		READ_ASCII actor actor_name (32) NULL
		READ_SHORT actor+0x20 actor_pos_x
		PATCH_IF (~%actor_name%~ STR_EQ ~Guard~) AND (~%actor_pos_x%~ = 1406) BEGIN
			WRITE_ASCII actor+0x60 "JA#FRINI" #8 // Class script
		END
	END
	//change container script (dont spawn Flaming Fist Enforcer)
	LPF ALTER_AREA_REGION
		STR_VAR
		region_name = ~REPTRAP1~
		region_script = ~None~
	END
	LPF ALTER_AREA_REGION
		STR_VAR
		region_name = ~REPTRAP2~
		region_script = ~None~
	END
	LPF ALTER_AREA_REGION
		STR_VAR
		region_name = ~REPTRAP3~
		region_script = ~None~
	END
	LPF fj_are_structure
		INT_VAR
		fj_type         = 0    //trap
		fj_box_left     = 1414
		fj_box_top      = 2963
		fj_box_right    = 1670
		fj_box_bottom   = 3113
		fj_trap_detect	= 100
		fj_trap_remove	= 100
		fj_trap_active	= 1
		fj_trap_status	= 0
		fj_loc_x		= 1558
		fj_loc_y		= 2924
		fj_vertex_0     = 1414 + (3053 << 16)
		fj_vertex_1     = 1649 + (3113 << 16)
		fj_vertex_2     = 1670 + (3018 << 16)
		fj_vertex_3     = 1430 + (2963 << 16)
		STR_VAR
		fj_structure_type   = region
		fj_name             = JA#FRGUA
		fj_reg_script		= JA#FRGUA
	END

COPY_EXISTING ~%FriendlyArmInn_L1%.ARE~ ~override~
	//change container script (dont spawn Flaming Fist Enforcer)
	LPF ALTER_AREA_REGION
		STR_VAR
		region_name = ~REPTRAP~
		region_script = ~None~
	END

COPY_EXISTING ~%FriendlyArmInn_L2%.ARE~ ~override~
              ~%FriendlyArmInn_L3%.ARE~ ~override~
	//change container script (dont spawn Guard Enforcer)
	GET_OFFSET_ARRAY containers ARE_V10_CONTAINERS
	PHP_EACH containers AS i => container BEGIN
		READ_ASCII container+0x48 container_script (8) NULL
		PATCH_MATCH ~%container_script%~ WITH
			~ACT0[39]~ BEGIN
				WRITE_ASCII container+0x48 ~JA#LAW01~ #8
			END
			~ACT0[28]~ BEGIN
				WRITE_ASCII container+0x48 ~JA#LAW02~ #8
			END
			~ACT0[17]~ BEGIN
				WRITE_ASCII container+0x48 ~JA#LAW03~ #8
			END
			DEFAULT
		END
	END

COPY_EXISTING ~%NEBaldursGate_BlushingMermaid_L1%.ARE~ ~override~
	//change container script (dont spawn Flaming Fist Enforcer)
	LPF ALTER_AREA_REGION
		STR_VAR
		region_name = ~REPTRAP~
		region_script = ~None~
	END
	//change container scripts (dont spawn Guard Enforcer)
	GET_OFFSET_ARRAY containers ARE_V10_CONTAINERS
	PHP_EACH containers AS i => container BEGIN
		READ_ASCII container+0x48 container_script (8) NULL
		PATCH_IF !(~%container_script%~ STRING_COMPARE_REGEXP ~ACT0[28]~) BEGIN
			WRITE_ASCII container+0x48 ~JA#LAW04~ #8
		END
	END


COPY_EXISTING ~%NBaldursGate_DucalPalace_L1%.ARE~ ~override~
	//change container scripts (dont spawn Flaming Fist Enforcer)
	GET_OFFSET_ARRAY containers ARE_V10_CONTAINERS
	PHP_EACH containers AS i => container BEGIN
		WRITE_ASCII container+0x48 ~JA#LAW05~ #8
	END

// COPY_EXISTING ~%NBaldursGate_DucalPalace_L2%.ARE~ ~override~ ?

COPY_EXISTING ~%NBaldursGate_DucalPalace_L3%.ARE~ ~override~
	//change container scripts (dont spawn Flaming Fist Enforcer)
	GET_OFFSET_ARRAY containers ARE_V10_CONTAINERS
	PHP_EACH containers AS i => container BEGIN
		WRITE_ASCII container+0x48 ~JA#LAW06~ #8
	END
