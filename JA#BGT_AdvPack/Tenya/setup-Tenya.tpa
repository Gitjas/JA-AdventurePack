///////////////////////////////////////////////////
//ERWEITERTE TENYA-HANDLUNG & TEMPEL DER UMBERLEE
///////////////////////////////////////////////////


COMPILE ~JA#BGT_AdvPack/TENYA/JA#WATW1.BAF~

COPY ~JA#BGT_AdvPack/Tenya/JA#WATW1.ITM~ ~override~

COPY ~JA#BGT_AdvPack/Tenya/JA#WATW1.CRE~ ~override~
	SAY NAME1 @2111
	SAY NAME2 @2111

ACTION_IF GAME_IS ~bgt~ BEGIN
	ACTION_IF NOT FILE_EXISTS ~t-wei01.wav~ BEGIN		//0xEF10 WATER_WEIRD
		COPY ~JA#BGT_AdvPack/Tenya/WaterWeird~ ~override~
		APPEND ~animate.ids~ ~0xEF10 WATER_WEIRD~ UNLESS ~^0x[Ee][Ff]10~
	END
END

// COPY_EXISTING ~WATERELE.ITM~ ~override~
//   WRITE_BYTE 0x72 1 // melee instead of range

COPY ~JA#BGT_AdvPack/Tenya/JA#SUTY1.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Tenya/JA#SUTY2.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Tenya/JA#SUTY3.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Tenya/JA#SUTY4.CRE~ ~override~

COPY ~JA#BGT_AdvPack/Tenya/JA#TENYA.BAM~ ~override~
COPY ~JA#BGT_AdvPack/Tenya/JA#CTENY.BAM~ ~override~
COPY ~JA#BGT_AdvPack/Tenya/JA#TENYA.ITM~ ~override~
	SAY NAME1 @2112
	SAY NAME2 @2113
	SAY UNIDENTIFIED_DESC @2115
	SAY DESC @2114


LAF GET_DLG_STATE_RESPONSE
	INT_VAR idx_state = 18
	STR_VAR dlg_file = ~SONNER.DLG~
	RET state18_response0_action = response_action
END
OUTER_PATCH_SAVE ~fishermenEscapeArea~ ~%state18_response0_action%~ BEGIN
	REPLACE_TEXTUALLY ~\(.\|[%WNL%%MNL%%LNL%]\)*GiveItem("BLUN03",\[PC\])~ ~~
END
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Tenya/ja#sonner.d~

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Tenya/ja#pumber2.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Tenya/ja#pumberl.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Tenya/ja#jalant.d~
COMPILE ~JA#BGT_AdvPack/Tenya/ja#tlos1.d~

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Tenya/ja#tenya.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Tenya/ja#tenya2.d~
ACTION_IF GAME_IS ~bgt~ BEGIN
	<<<<<<<< ...inlined/ja#tenya2_bgt.d
	ADD_TRANS_TRIGGER TENYA2 11 ~False()~

	EXTEND_BOTTOM TENYA2 11
	COPY_TRANS TENYA 28
	END

	ALTER_TRANS TENYA
	BEGIN 2 END
	BEGIN 0 END
	BEGIN
	  "ACTION" ~Enemy()~
	END
	>>>>>>>>
	COMPILE EVALUATE_BUFFER ~...inlined/ja#tenya2_bgt.d~
END

ADD_JOURNAL @22 @1022 USING ~JA#BGT_AdvPack/languages/%s/ja#tenya2.tra~


ACTION_IF GAME_IS ~bgt~ BEGIN
	LAF GET_DLG_STATE_RESPONSE
		INT_VAR idx_state = 8
				idx_response = 4
		STR_VAR dlg_file = ~TENYA.DLG~
		RET EraseJournalEntries = response_action
	END
END
ACTION_IF GAME_IS ~eet bgee~ BEGIN
	LAF GET_DLG_STATE_RESPONSE
		INT_VAR idx_state = 3
				idx_response = 0
		STR_VAR dlg_file = ~TELMAN.DLG~
		RET EraseJournalEntries = response_action
	END
END

<<<<<<<< ...inlined/failedFisherQuest.baf
IF
	Dead("Tenya")
	Global("HostileFishermen","GLOBAL",1)
	Global("JA#FISHERQUEST","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("JA#FISHERQUEST","MYAREA",1)
		%EraseJournalEntries%
		Continue()
END
>>>>>>>>
EXTEND_TOP_REGEXP ~\(%WyrmsCrossing_BCS%\|%FishingVillage_BCS%\)\.BCS~ ~...inlined/failedFisherQuest.baf~ EVALUATE_BUFFER

COPY_EXISTING ~FISHER.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~DestroySelf()~ ~DestroyItem("MISC53") Wait(1) Kill(Myself)~
	END
BUT_ONLY

EXTEND_TOP ~PUMBER.BCS~ ~JA#BGT_AdvPack/Tenya/JA#PUMBE.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%BaldursGateDocks_WaterQueensHouse%.BCS~ ~JA#BGT_AdvPack/Tenya/AR7609.BAF~ EVALUATE_BUFFER


COMPILE ~JA#BGT_AdvPack/TENYA/JA#TENYA.BAF~


COPY ~JA#BGT_AdvPack/Tenya/JA#TLOS1.CRE~ ~override~
	SAY NAME1 @2116
	SAY NAME2 @2116

COPY_EXISTING ~SONNER.CRE~ ~override~
	LPF ADD_CRE_SCRIPT STR_VAR script_new = ~SHOUTDLG~ END

COPY_EXISTING ~PUMBERL.CRE~ ~override~
	LPF ADD_CRE_SCRIPT INT_VAR offset_start = SCRIPT_CLASS STR_VAR script_new = ~PUMBER~ END

COPY_EXISTING ~PUMBER2.CRE~ ~override~
	SAY NAME1 @2117
	SAY NAME2 @2117
	LPF REPLACE_CRE_SCRIPT STR_VAR script_old = ~WDASIGHT~ script_new = ~WTASIGHT~ END // no RandomWalk()

COPY_EXISTING ~TENYA2.CRE~ ~override~
	LPF REMOVE_CRE_SCRIPT STR_VAR script_old = ~TENYA~ END
	LPF REMOVE_CRE_SCRIPT STR_VAR script_old = ~INITDLG~ END
	LPF ADD_CRE_SCRIPT STR_VAR script_new = ~JA#TENYA~ END


COPY ~JA#BGT_AdvPack/Tenya/JA#C02.ARE~ ~override~
	PATCH_IF GAME_IS ~bgee eet~ BEGIN
		WRITE_ASCIIE 0x8 ~%NWBaldursGate_House4_L1%~ #8
		LPF ALTER_AREA_REGION
			STR_VAR region_name			= ~Door8200~
					destination_area	= EVAL ~%SEBaldursGate%~
		END
	END

COPY ~JA#BGT_AdvPack/Tenya/JA#C03.ARE~ ~override~
	PATCH_IF GAME_IS ~bgee eet~ BEGIN
		WRITE_ASCIIE 0x8 ~%NWBaldursGate_House4_L2%~ #8
	END

COPY_EXISTING ~%SEBaldursGate%.ARE~ ~override~
	//add region triggers
	LPF fj_are_structure
		INT_VAR
		fj_type         = 2    //travel
		fj_box_left     = 1030
		fj_box_top      = 806
		fj_box_right    = 1260
		fj_box_bottom   = 990
		fj_cursor_idx	= 30
		fj_loc_x		= 1068
		fj_loc_y		= 850
		fj_vertex_0     = 1133 + (806 << 16)
		fj_vertex_1     = 1030 + (881 << 16)
		fj_vertex_2     = 1114 + (990 << 16)
		fj_vertex_3     = 1260 + (886 << 16)
		STR_VAR
		fj_structure_type   = region
		fj_destination_area	= JA#C02
		fj_destination_name	= FR8200
		fj_name             = DOORJA#C02
	END
	//add entrance
	LPF  ~fj_are_structure~
		INT_VAR
		fj_loc_x       = 978   //x
		fj_loc_y       = 781   //y
		fj_orientation = 7
		STR_VAR
		fj_structure_type = entrance
		fj_name           = FRJA#C02
	END

COPY_EXISTING ~%BaldursGateDocks_WaterQueensHouse%.ARE~ ~override~
	GET_OFFSET_ARRAY actors_array ARE_V10_ACTORS
	PHP_EACH actors_array AS actors_num => actors_offset BEGIN
		READ_SHORT actors_offset+0x20 actors_pos_x
		READ_ASCII actors_offset+0x80 actors_filename
		//Flaming Fist Dialog and Script changes
		PATCH_IF ((~%actors_pos_x%~ = 1029) AND (~%actors_filename%~ STR_EQ  ~PUMBER2~)) BEGIN
			WRITE_SHORT actors_offset + 0x20        1143
			WRITE_SHORT actors_offset + 0x22        610
			WRITE_SHORT actors_offset + 0x24        1143
			WRITE_SHORT actors_offset + 0x26        610
			WRITE_SHORT actors_offset + 0x34        5
		END
		PATCH_IF ((~%actors_pos_x%~ = 936) AND (~%actors_filename%~ STR_EQ  ~PUMBER2~)) BEGIN
			WRITE_SHORT actors_offset + 0x20        395
			WRITE_SHORT actors_offset + 0x22        825
			WRITE_SHORT actors_offset + 0x24        395
			WRITE_SHORT actors_offset + 0x26        825
			WRITE_SHORT actors_offset + 0x34        11
		END
	END
BUT_ONLY
