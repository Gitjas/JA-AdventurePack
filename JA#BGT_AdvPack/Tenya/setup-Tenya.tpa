////////////////////////////
// Tenya and the fishermen
////////////////////////////


ACTION_IF GAME_IS ~bgt~ BEGIN
	LAF GET_DLG_SCRIPT
		INT_VAR idx_state = 8
				idx_response = 4
		STR_VAR dlg_file = ~TENYA.DLG~
		RET EraseJournalEntry_action = response_action
	END
END
ACTION_IF GAME_IS ~bgee eet~ BEGIN
	LAF GET_DLG_SCRIPT
		INT_VAR idx_state = 3
				idx_response = 0
		STR_VAR dlg_file = ~TELMAN.DLG~
		RET EraseJournalEntry_action = response_action
	END
END

<<<<<<<< ...inlined/failedFisherQuest.baf
IF
	Dead("Tenya")
	Global("HostileFishermen","GLOBAL",1)
	Global("JA#FISHERQUEST","MYAREA",0)
THEN
	RESPONSE #100
		SetGlobal("JA#FISHERQUEST","MYAREA",1)
		%EraseJournalEntry_action%
		Continue()
END
>>>>>>>>
EXTEND_TOP ~%FishingVillage_BCS%.BCS~ ~...inlined/failedFisherQuest.baf~ EVALUATE_BUFFER
EXTEND_TOP ~%WyrmsCrossing_BCS%.BCS~ ~...inlined/failedFisherQuest.baf~ EVALUATE_BUFFER


COPY_EXISTING ~FISHER.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~DestroySelf()~ ~DestroyItem("MISC53") Wait(1) Kill(Myself)~
	END
BUT_ONLY


LAF GET_DLG_SCRIPT
	INT_VAR idx_state = 18
	STR_VAR dlg_file = ~SONNER.DLG~
	RET state18_response0_action = response_action
END
OUTER_PATCH_SAVE ~fishermenEscapeArea~ ~%state18_response0_action%~ BEGIN
	REPLACE_TEXTUALLY ~\([%WNL%%MNL%%LNL%]\|.\)*GiveItem("BLUN03",\[PC\])~ ~NoAction()~
END

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Tenya/ja#sonner.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Tenya/ja#tenya.d~


COPY_EXISTING ~SONNER.CRE~ ~override~
	LPF ADD_CRE_SCRIPT STR_VAR script_new = ~SHOUTDLG~ END
BUT_ONLY
