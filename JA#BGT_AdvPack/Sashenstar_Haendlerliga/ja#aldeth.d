// BGT
ADD_TRANS_TRIGGER ALDETH 14
~OR(2) ActionOverride("Irlentre",NumTimesTalkedToGT(0)) ActionOverride("Zorl",NumTimesTalkedToGT(0))~ DO 1
UNLESS ~Global("TalkedToZorlIrenl","GLOBAL",1)~


ADD_STATE_TRIGGER ALDETH 14
~!Global("JA#CUTSCENE_ACTIVE","%SWBaldursGate_MerchantLeague_L3%",1) !Global("ZorlDopple","GLOBAL",1)~
ADD_STATE_TRIGGER ALDETH 21
~!Global("JA#CUTSCENE_ACTIVE","%SWBaldursGate_MerchantLeague_L3%",1) Global("JA#CUDED","%SWBaldursGate_MerchantLeague_L2%",0)~
ADD_STATE_TRIGGER ALDETH 24
~!Global("JA#CUTSCENE_ACTIVE","%SWBaldursGate_MerchantLeague_L3%",1)~
ADD_STATE_TRIGGER ALDETH 26
~!Global("JA#CUTSCENE_ACTIVE","%SWBaldursGate_MerchantLeague_L3%",1)~
ADD_STATE_TRIGGER ALDETH 27
~!Global("JA#CUTSCENE_ACTIVE","%SWBaldursGate_MerchantLeague_L3%",1)~


ALTER_TRANS ALDETH
BEGIN 13 END
BEGIN 1 END
BEGIN
  "EPILOGUE" ~GOTO JA#ALDETH_00~
END

ALTER_TRANS ALDETH
BEGIN 25 END
BEGIN 0 END
BEGIN
  "ACTION" ~SetGlobal("AldethMove","GLOBAL",2) ClearAllActions() StartCutSceneMode() StartCutScene("JA#CUALD")~
END

ADD_TRANS_ACTION ALDETH
BEGIN 27 END
BEGIN 0 END
~EraseJournalEntry(@1025)
EraseJournalEntry(@1026)
EraseJournalEntry(@1027)
EraseJournalEntry(@1028)
EraseJournalEntry(@1029)
EraseJournalEntry(@1030)~


ALTER_TRANS IRLENT
BEGIN 3 END
BEGIN 0 END
BEGIN
  "EPILOGUE" ~EXTERN ALDETH JA#ALDETH_02~
END

ALTER_TRANS IRLENT
BEGIN 4 END
BEGIN 0 END
BEGIN
  "EPILOGUE" ~EXTERN ZORL 7~
END

ALTER_TRANS ZORL
BEGIN 6 END
BEGIN 0 END
BEGIN
  "EPILOGUE" ~EXTERN IRLENT 4~
END

ALTER_TRANS ZORL
BEGIN 7 END
BEGIN 0 END
BEGIN
  "EPILOGUE" ~GOTO 11~
END

ALTER_TRANS ZORL
BEGIN 7 END
BEGIN 0 END
BEGIN
  "ACTION" ~SetGlobal("ZorlDopple","GLOBAL",1)~
END


EXTEND_BOTTOM ALDETH 14
IF ~PartyHasItem("SCRL2P")~ THEN REPLY @1 DO ~SetGlobal("JA#ALDETH_KNOWDOPP","GLOBAL",1)~ UNSOLVED_JOURNAL @1025 GOTO JA#ALDETH_30
END

EXTEND_BOTTOM ALDETH 14
IF ~!PartyHasItem("SCRL2P")GlobalGT("JA#DOPPELGANGER_SPAWN","GLOBAL",0)Global("JA#SENDZORL","GLOBAL",2)~ THEN REPLY @2 GOTO JA#ALDETH_31
END


APPEND ALDETH

IF ~~ THEN BEGIN JA#ALDETH_00
SAY @21
IF ~~ THEN REPLY @22 GOTO 25
IF ~~ THEN REPLY @23 GOTO 10
END

IF ~Global("JA#CUTSCENE_ACTIVE","%SWBaldursGate_MerchantLeague_L3%",1)Global("JA#ALDETH_KNOWDOPP","GLOBAL",0)~ THEN BEGIN JA#ALDETH_01
SAY @24
IF ~~ THEN EXTERN IRLENT 3
END

IF ~Global("JA#CUTSCENE_ACTIVE","%SWBaldursGate_MerchantLeague_L3%",1)Global("JA#ALDETH_KNOWDOPP","GLOBAL",1)~ THEN BEGIN JA#ALDETH_Y1
SAY @25
= @26
IF ~~ THEN EXTERN ZORL JA#ZORL_100
END

IF ~~ THEN BEGIN JA#ALDETH_02
SAY @27
IF ~~ THEN EXTERN ZORL 6
END

IF ~~ THEN BEGIN JA#ALDETH_30
SAY @31
= @32
IF ~~ THEN DO ~SetGlobal("JA#CUIRL","GLOBAL",1)SetGlobal("JA#ALD_MOVE1","%SWBaldursGate_MerchantLeague_L2%",1)EscapeArea()~ EXIT
END

IF ~~ THEN BEGIN JA#ALDETH_31
SAY @33
IF ~~ THEN DO ~SetGlobal("JA#CUIRL","GLOBAL",1)SetGlobal("JA#ALD_MOVE1","%SWBaldursGate_MerchantLeague_L2%",1)EscapeArea()~ EXIT
END

/* new dialogue state - if PC attacked Zorl or Irlentree and forced them to change without the invitation to the 2nd floor */

IF WEIGHT #-1
~Global("ZorlDopple","GLOBAL",1) GlobalLT("AldethDopple","GLOBAL",6)
Global("Chapter","GLOBAL",%tutu_chapter_5%)
Global("HelpAldeth","GLOBAL",1)~ THEN fight_started
SAY @69
IF ~~ THEN EXIT
END

IF WEIGHT #-1
~!Global("Chapter","GLOBAL",%tutu_chapter_7%)
Global("HelpAldeth","GLOBAL",2)
OR(2)
	PartyHasItem("JA#DIRLE")
	PartyHasItem("JA#DZORL")~ THEN bringback_bodies
SAY @70
IF ~PartyHasItem("JA#DIRLE")~ THEN DO ~TakePartyItem("JA#DIRLE") DestroyItem("JA#DIRLE") AddexperienceParty(250)~ EXIT
IF ~PartyHasItem("JA#DZORL")~ THEN DO ~TakePartyItem("JA#DZORL") DestroyItem("JA#DZORL") AddexperienceParty(250)~ EXIT
IF ~PartyHasItem("JA#DIRLE")
PartyHasItem("JA#DZORL")~ THEN DO ~TakePartyItem("JA#DZORL") DestroyItem("JA#DZORL") TakePartyItem("JA#DIRLE") DestroyItem("JA#DIRLE") AddexperienceParty(500)~ EXIT
END

END // APPEND ALDETH


APPEND ZORL

IF ~~ THEN BEGIN JA#ZORL_100
SAY @42
IF ~~ THEN EXTERN IRLENT 4
END

END // APPEND ZORL


ALTER_TRANS MERLEA
BEGIN 6 END
BEGIN 0 END
BEGIN
  "ACTION" ~SetGlobal("JA#MERLEA_CHARM","LOCALS",1)
IncrementGlobal("JA#MERCLEAGUE_INSP","GLOBAL",1)
AddJournalEntry(@1030,QUEST)~
END


ADD_STATE_TRIGGER MERLEA 0 ~Global("JA#MERLEA_DESK","MYAREA",0) !Global("HelpAldeth","GLOBAL",2)~
ADD_STATE_TRIGGER MERLEA 5 ~Global("JA#MERLEA_MYST","GLOBAL",0)~
ADD_STATE_TRIGGER MERLEA 6 ~Global("JA#MERLEA_CHARM","LOCALS",0)~

ALTER_TRANS MERLEA
BEGIN 1 END
BEGIN 0 END
BEGIN
  "ACTION" ~SetGlobal("JA#MERLEA_DESK","MYAREA",1)~
END

ALTER_TRANS MERLEA
BEGIN 6 END
BEGIN 0 END
BEGIN
  "ACTION" ~SetGlobal("JA#MERLEA_MYST","GLOBAL",1)~
END





ADD_STATE_TRIGGER BART10 0 ~Global("JA#MERLEA_MYST","GLOBAL",0)~

ALTER_TRANS BART10
BEGIN 0 END
BEGIN 0 END
BEGIN
  "ACTION" ~SetGlobal("JA#MERLEA_MYST","GLOBAL",1)~
  "REPLY" ~@59~
  "EPILOGUE" ~GOTO JA#BART10_00~
END


EXTEND_BOTTOM BART10 0
IF ~~ THEN REPLY @43 EXIT
END

APPEND BART10

IF ~~ THEN BEGIN JA#BART10_00
SAY @44
IF ~~ THEN DO ~IncrementGlobal("JA#MERCLEAGUE_INSP","GLOBAL",1)AddJournalEntry(@1030,QUEST)~ EXIT
END

IF ~!Global("HelpAldeth","GLOBAL",2)~ THEN BEGIN JA#BART10_01
SAY @45
IF ~~ THEN EXIT
END

IF ~Global("HelpAldeth","GLOBAL",2)Global("JA#BART10","LOCALS",0)~ THEN BEGIN JA#BART10_02
SAY @46
IF ~~ THEN REPLY @47 DO ~SetGlobal("JA#BART10","LOCALS",1)StartStore("ja#mlbar",LastTalkedToBy(Myself))~ EXIT
IF ~~ THEN REPLY @48 DO ~SetGlobal("JA#BART10","LOCALS",1)~ EXIT
END

IF ~Global("HelpAldeth","GLOBAL",2)Global("JA#BART10","LOCALS",1)~ THEN BEGIN JA#BART10_03
SAY @49
IF ~~ THEN REPLY @50 DO ~StartStore("ja#mlbar",LastTalkedToBy(Myself))~ EXIT
IF ~~ THEN REPLY @48 EXIT
END


END // APPEND BART10



ADD_STATE_TRIGGER MERCHG 0 ~NumTimesTalkedTo(0)~
ADD_STATE_TRIGGER MERCHG 1 ~Global("JA#MERCHG_HELP","%SWBaldursGate_MerchantLeague_L1%",0)~
ADD_STATE_TRIGGER MERCHG 5 ~!Global("HelpAldeth","GLOBAL",2)Global("JA#MERCHG_HELP","%SWBaldursGate_MerchantLeague_L1%",0)~
ADD_STATE_TRIGGER MERCHG 6 ~ReactionLT(LastTalkedToBy,NEUTRAL_LOWER)~



ALTER_TRANS MERCHG
BEGIN 1 END
BEGIN 0 END
BEGIN
  "ACTION" ~SetGlobal("JA#MERCHG_HELP","%SWBaldursGate_MerchantLeague_L1%",1)~
END

ALTER_TRANS MERCHG
BEGIN 3 END
BEGIN 0 END
BEGIN
  "ACTION" ~IncrementGlobal("JA#MERCLEAGUE_INSP","GLOBAL",1)AddJournalEntry(@1030,QUEST)~
END


ALTER_TRANS MERCHG
BEGIN 5 END
BEGIN 0 END
BEGIN
  "ACTION" ~SetGlobal("JA#MERCHG_HELP","%SWBaldursGate_MerchantLeague_L1%",1)IncrementGlobal("JA#MERCLEAGUE_INSP","GLOBAL",1)AddJournalEntry(@1030,QUEST)~
END


BEGIN ~JA#MLCOK~

IF ~!Global("HelpAldeth","GLOBAL",2)NumTimesTalkedTo(0)~ THEN BEGIN JA#MLCOK_1
SAY @51
IF ~~ THEN REPLY @52 GOTO JA#MLCOK_2
IF ~~ THEN REPLY @53 EXIT
END

IF ~~ THEN BEGIN JA#MLCOK_2
SAY @54
IF ~~ THEN REPLY @55 DO ~SetGlobal("JA#MLCOK","LOCALS",1)~ GOTO JA#MLCOK_4
IF ~~ THEN REPLY @56 EXIT
END

IF ~!Global("HelpAldeth","GLOBAL",2)NumTimesTalkedToGT(0)~ THEN BEGIN JA#MLCOK_3
SAY @57
IF ~Global("JA#MLCOK","LOCALS",0)~ THEN REPLY @52 GOTO JA#MLCOK_2
IF ~~ THEN REPLY @53 EXIT
END

IF ~~ THEN BEGIN JA#MLCOK_4
SAY @58
IF ~~ THEN REPLY @59 DO ~IncrementGlobal("JA#MERCLEAGUE_INSP","GLOBAL",1)~ UNSOLVED_JOURNAL @1026 GOTO JA#MLCOK_5
END

IF ~~ THEN BEGIN JA#MLCOK_5
SAY @60
= @61
IF ~~ THEN REPLY @62 GOTO JA#MLCOK_6
IF ~~ THEN REPLY @63 GOTO JA#MLCOK_7
END

IF ~~ THEN BEGIN JA#MLCOK_6
SAY @64
IF ~~ THEN REPLY @65 GOTO JA#MLCOK_7
END

IF ~~ THEN BEGIN JA#MLCOK_7
SAY @66
IF ~~ THEN EXIT
END

IF ~Global("HelpAldeth","GLOBAL",2)~ THEN BEGIN JA#MLCOK_8
SAY @67
IF ~~ THEN EXIT
END

END // JA#MLCOK
