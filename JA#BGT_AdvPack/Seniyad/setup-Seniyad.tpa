////////////////////////
//Seniyad and Aldeth
////////////////////////

// Btw: In Enhanched Editions you already have a peaceful solution with jaheira in group.

ACTION_IF GAME_IS ~bgee eet~ BEGIN
	<<<<<<<< ...inlined/aldeth_ee.d
	ADD_STATE_TRIGGER ALDETH 31 ~Global("SeniyadXP","GLOBAL",0)~
	>>>>>>>>
	COMPILE ~...inlined/aldeth_ee.d~
END

COPY_EXISTING ~ALDETH.dlg~ ~override~
	LPF READ_JOURNAL_STRREF
		INT_VAR state = (GAME_IS ~bgt~ ? 0 : 4)
		RET state04_journal = strref
	END
BUT_ONLY

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Seniyad/JA#ALDETH_REWARD.D~ USING ~JA#BGT_AdvPack/languages/%s/JA#ALDETH.tra~

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Seniyad/JA#SENIYA.D~ USING ~JA#BGT_AdvPack/languages/%s/JA#ALDETH.tra~

ACTION_IF GAME_IS ~bgt~ BEGIN
	COPY_EXISTING ~SENIYA.dlg~ ~override~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY EXACT_MATCH ~EscapeAreaObject("druid_escape")~ ~EscapeAreaDestroy(90)~
		END
END

ADD_JOURNAL EXISTING @20 USING ~JA#BGT_AdvPack/languages/%s/JA#ALDETH.tra~


COPY ~JA#BGT_AdvPack/Seniyad/JA#ALDET.BAM~ ~override~
COPY ~JA#BGT_AdvPack/Seniyad/JA#ALDET.ITM~ ~override~
	SAY NAME1 @2101
	SAY NAME2 @2101
	SAY DESC @2102

// If Aldeth main component is installed
ACTION_IF FILE_EXISTS_IN_GAME ~JA#ALDET.BCS~ BEGIN
	EXTEND_TOP ~JA#ALDET.BCS~ ~JA#BGT_AdvPack/Seniyad/JA#ALDET.BAF~ EVALUATE_BUFFER
END ELSE BEGIN
	COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Seniyad/JA#ALDET.BAF~

	COPY_EXISTING ~ALDETH.CRE~ ~override~
		LPF ADD_CRE_SCRIPT STR_VAR script_new = ~JA#ALDET~ END
END


/* bg1npc is installed: Viconia's interjection kills the new reply option to settle the discpute with Seniyad peacefully. Add the reply option to Viconia's new state, as well: */

ACTION_IF ((MOD_IS_INSTALLED ~BG1NPC/BG1NPC.TP2~ "10") //bg1npc interjections and quests is installed
			AND (FILE_EXISTS ~bg1npc/tra/english/x#viint.tra~)) BEGIN // only checking that the bg1npc folder is still in there

	PRINT ~BG1 NPC Quest und Banter Component detected~

	OUTER_SET vic_state = STATE_WHICH_SAYS 5 IN ~bg1npc/tra/%s/x#viint.tra~ FROM ~%VICONIA_JOINED%.dlg~
	OUTER_SET jaheira_state = STATE_WHICH_SAYS 95 IN ~bg1npc/tra/%s/x#jaqu.tra~ FROM ~%JAHEIRA_JOINED%.dlg~
	OUTER_SET seniya3_state = STATE_WHICH_SAYS 3 IN ~JA#BGT_AdvPack/languages/%s/ja#aldeth.tra~ FROM ~SENIYA.dlg~
	OUTER_SET seniya17_state = STATE_WHICH_SAYS 17 IN ~JA#BGT_AdvPack/languages/%s/ja#aldeth.tra~ FROM ~SENIYA.dlg~

	ACTION_IF ((vic_state >= 0) AND (jaheira_state >= 0) AND (seniya3_state >= 0) AND (seniya17_state >= 0)) BEGIN
		<<<<<<<< ...inlined/seniya_bg1npc.d
		/* If we know the state number in viconij.dlg we append to it */
		EXTEND_TOP ~%VICONIA_JOINED%~ %vic_state% #1
		IF ~~ THEN REPLY @0 EXTERN SENIYA %seniya3_state%
		END

		/* more bg1npc compatibility: loop to Jaheira's quest, by Lumorus */
		INTERJECT SENIYA %seniya17_state% JA#SENIYA_06_BG1NPC
		== %JAHEIRA_JOINED% IF ~InParty("jaheira") InMyArea("jaheira") !StateCheck("jaheira",CD_STATE_NOTVALID)~ THEN @68
		== SENIYA IF ~InParty("jaheira") InMyArea("jaheira") !StateCheck("jaheira",CD_STATE_NOTVALID)~ THEN @111
		== %JAHEIRA_JOINED% IF ~InParty("jaheira") InMyArea("jaheira") !StateCheck("jaheira",CD_STATE_NOTVALID)~ THEN @112
		== SENIYA IF ~InParty("jaheira") InMyArea("jaheira") !StateCheck("jaheira",CD_STATE_NOTVALID)~ THEN @113
		END %JAHEIRA_JOINED% %jaheira_state%
		>>>>>>>>
		COMPILE EVALUATE_BUFFER ~...inlined/seniya_bg1npc.d~ USING ~JA#BGT_AdvPack/languages/%s/JA#ALDETH.tra~
	END

END // ACTION_IF bg1npc interjections is installed
