//////////////////////////////////////////////////////////////////////////
// Komplett Ã¼berarbeitete Herzogspalast-Szene mit restaurierten Inhalten
// Restauriert: Abschluss der Husam-Storyline
//////////////////////////////////////////////////////////////////////////


COPY ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#7211A.BAM~ ~override~

ACTION_IF GAME_IS ~bgt~ BEGIN
	COPY ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#SPWN1.VVC~ ~override~
	COPY ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#SPWN1.BAM~ ~override~
	COPY ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#SPWN1.WAV~ ~override~
END


COPY ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#FLAM1.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#FLAM2.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#FLAM3.CRE~ ~override~

COPY ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#KAEM1.CRE~ ~override~
	SAY NAME1 @2044
	SAY NAME2 @2044

COPY ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#PRISO.CRE~ ~override~
	SAY NAME1 @2045
	SAY NAME2 @2046

COPY ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#NOPA1.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#NOPA2.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#NOPA3.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#NOPA4.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#NOPA5.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#NOPA6.CRE~ ~override~
	SAY NAME1 @2047
	SAY NAME2 @2047

ACTION_IF GAME_IS ~bgee eet~ BEGIN
	COPY_EXISTING ~SHADOW03.CRE~ ~override/JA#STH03.CRE~
	              ~SHADOW04.CRE~ ~override/JA#STH04.CRE~
	              ~SHADOW05.CRE~ ~override/JA#STH05.CRE~
	              ~SHADOW06.CRE~ ~override/JA#STH06.CRE~
	              ~SHADOW02.CRE~ ~override/JA#STH07.CRE~
		LPF REPLACE_CRE_SCRIPT STR_VAR script_old = EVAL ~%SOURCE_RES%~ script_new = ~JA#STHFX~ END
		LPF ADD_CRE_SCRIPT STR_VAR script_new = ~SEEENEMY~ END
		WRITE_ASCIIE DEATHVAR ~%DEST_RES%~ #32
END ELSE BEGIN
	COPY ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#STH03.CRE~ ~override~
	COPY ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#STH04.CRE~ ~override~
	COPY ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#STH05.CRE~ ~override~
	COPY ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#STH06.CRE~ ~override~
	COPY ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#STH07.CRE~ ~override~
END


COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#NILA.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#FLM01.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#FLM02.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#FLM03.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#FLM04.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#FLM05.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#FLM06.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#FLM07.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#FLM08.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#FLM09.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#KAEM1.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#7209T.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#7211T.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#STHFX.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#CANDL.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#HUCU2.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#CUSAR.BAF~


EXTEND_TOP ~%NBaldursGate_DucalPalace_L1_BCS%.BCS~ ~JA#BGT_AdvPack/Dukalpalace-HusamStory/AR7208.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%NBaldursGate_DucalPalace_L2_BCS%.BCS~ ~JA#BGT_AdvPack/Dukalpalace-HusamStory/AR7209.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%NBaldursGate_DucalPalace_Cellar_BCS%.BCS~ ~JA#BGT_AdvPack/Dukalpalace-HusamStory/AR7211.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%NBaldursGate_BCS%.BCS~ ~JA#BGT_AdvPack/Dukalpalace-HusamStory/AR7300.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%CentralSewers%.BCS~ ~JA#BGT_AdvPack/Dukalpalace-HusamStory/AR7325.BAF~ EVALUATE_BUFFER


COPY_EXISTING ~%NBaldursGate_DucalPalace_L1_BCS%.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REFACTOR_TRIGGER EXACT_MATCH ~Global("SarevokBehavior","GLOBAL",0)~ ~\0 !Global("JA#HUSAM_MOVE","GLOBAL",1) PartyHasItem("SCRL3F")~
		REPLACE_TEXTUALLY ~CreateCreature("NOBLPA",\[769\.571\],[0S])~ ~\0 ClearAllActions() StartCutSceneMode() FadeToColor([30.0],0) Wait(1) StartCutScene("JA#CUSAR")~
	END
BUT_ONLY

COPY_EXISTING ~%NEBaldursGate_BCS%.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~CreateCreature("HUSAM2",\[199\.2039\],[0S])~ ~CreateCreature("HUSAM2",[358.1961],2)~
	END
BUT_ONLY

// Belt initiates his dialog after final dialog from JA#Husam
COPY_EXISTING ~BELT.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REFACTOR_TRIGGER EXACT_MATCH ~Global("SarevokBehavior","GLOBAL",1)~ ~\0 OR(2) Global("JA#HUSAM_MOVE","GLOBAL",4) Dead("JA#HUSAM")~
	END
BUT_ONLY

// Let Liia use her script MAGE5.BCS
COPY_EXISTING ~LIIA.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~See([ENEMY])~ ~False()~
	END
BUT_ONLY

ACTION_IF GAME_IS ~bgee eet~ BEGIN
	COPY_EXISTING_REGEXP ~SHADOW0[2-6]\.BCS~ ~override~
		DECOMPILE_AND_PATCH BEGIN
			REFACTOR_TRIGGER EXACT_MATCH ~Global("HusamMove","GLOBAL",7)~ ~\0 !AreaCheck("%NBaldursGate_DucalPalace_L1%") !AreaCheck("%NBaldursGate_DucalPalace_Cellar%")~
		END
	BUT_ONLY
END


ACTION_IF GAME_IS ~bgt~ BEGIN
	// transport cutscene with Belt/Liia
	COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#CUBEL.BAF~
	COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/ja#cubel.d~
	// 2 cutscenes with Husam2
	COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#HUCU1.BAF~
	COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/JA#HUCU3.BAF~
	COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/ja#husam_bgt.d~
END

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/ja#nila.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/ja#palast.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/ja#priso.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/ja#husam.d~
// misc.d
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/ja#mcook4.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Dukalpalace-HusamStory/ja#denkod.d~

ADD_JOURNAL @4 @7 @13 @15 @19 USING ~JA#BGT_AdvPack/languages/%s/ja#husam.tra~


COPY_EXISTING ~CYTHAN.CRE~ ~override~
	LPF DELETE_CRE_ITEM STR_VAR item_to_delete = ~SCRL3F~ END
BUT_ONLY

COPY_EXISTING ~SLYTHE.CRE~ ~override~
	LPF REPLACE_CRE_SCRIPT STR_VAR script_old = ~INITDLG~ script_new = ~SHOUTDLG~ END
BUT_ONLY

COPY_EXISTING ~NILA.CRE~ ~override~
	LPF ADD_CRE_SCRIPT STR_VAR script_new = ~JA#NILA~ END
BUT_ONLY

COPY_EXISTING ~HUSAM2.CRE~ ~override/JA#HUSAM.CRE~
	SAY INITIAL_MEETING #4561
	SAY MORALE #4560
	SAY UNHAPPY_BREAKING_POINT #4560
	SAY BATTLE_CRY1 #4561
	SAY SELECT_COMMON1 #4561
	SAY SELECT_COMMON2 #4565
	SAY SELECT_COMMON3 #0
	LPF REMOVE_CRE_SCRIPT STR_VAR script_old = ~SHOUT~ END
	LPF REPLACE_CRE_SCRIPT STR_VAR script_old = ~HUSAM2~ script_new = ~SEEENEMY~ END
	WRITE_ASCII DEATHVAR ~JA#HUSAM~ #32
	WRITE_ASCII DIALOG ~JA#HUSAM~ #8
BUT_ONLY

COPY_EXISTING ~FLAM7.CRE~ ~override~
	LPF REPLACE_CRE_SCRIPT STR_VAR script_old = ~FLAM7~ script_new = ~JA#FLM03~ END
	LPF REMOVE_CRE_SCRIPT STR_VAR script_old = ~SEEENEMY~ END
	LPF REPLACE_CRE_SCRIPT STR_VAR script_old = ~WDASIGHT~ script_new = ~WTASIGHT~ END
BUT_ONLY

COPY_EXISTING ~FLAM9.CRE~ ~override~
	LPF REPLACE_CRE_SCRIPT STR_VAR script_old = ~INITDLG~ script_new = ~JA#FLM03~ END
	LPF REPLACE_CRE_SCRIPT STR_VAR script_old = ~WDASIGHT~ script_new = ~WTASIGHT~ END
BUT_ONLY

COPY_EXISTING ~FLAM10.CRE~ ~override~
              ~FLAM11.CRE~ ~override~
	LPF REPLACE_CRE_SCRIPT STR_VAR script_old = ~WDASIGHT~ script_new = ~WTASIGHT~ END
BUT_ONLY


COPY_EXISTING ~%NBaldursGate_DucalPalace_L1%.ARE~ ~override~
	GET_OFFSET_ARRAY actors ARE_V10_ACTORS
	PHP_EACH actors AS i => actor BEGIN
		READ_SHORT actor+0x20 actor_pos_x
		READ_ASCII actor+0x80 actor_filename
		// Flaming Fist Dialog and Script changes
		PATCH_MATCH ~%actor_pos_x%~ WITH
			463 WHEN (~%actor_filename%~ STR_EQ ~FLAM10~) BEGIN
				WRITE_ASCII actor+0x48 ~JA#FLAM4~ #8
				WRITE_ASCII actor+0x78 ~JA#FLM04~ #8
			END
			829 WHEN (~%actor_filename%~ STR_EQ ~FLAM10~) BEGIN
				WRITE_ASCII actor+0x48 ~JA#FLAM4~ #8
				WRITE_ASCII actor+0x78 ~JA#FLM05~ #8
			END
			625 WHEN (~%actor_filename%~ STR_EQ ~FLAM10~) BEGIN
				WRITE_ASCII actor+0x48 ~JA#FLAM4~ #8
				WRITE_ASCII actor+0x78 ~JA#FLM06~ #8
			END
			507 WHEN (~%actor_filename%~ STR_EQ ~FLAM10~) BEGIN
				WRITE_ASCII actor+0x78 ~JA#FLM07~ #8
			END
			1019 WHEN (~%actor_filename%~ STR_EQ ~FLAM10~) BEGIN
				WRITE_ASCII actor+0x48 ~JA#FLAM4~ #8
				WRITE_ASCII actor+0x78 ~JA#FLM08~ #8
			END
			396 WHEN (~%actor_filename%~ STR_EQ ~FLAM10~) BEGIN
				WRITE_ASCII actor+0x48 ~JA#FLAM4~ #8
				WRITE_ASCII actor+0x78 ~JA#FLM09~ #8
			END
			DEFAULT
		END
	END
	LPF DELETE_AREA_ACTOR STR_VAR cre_to_delete = ~NOBW5~ END
	LPF DELETE_AREA_ACTOR STR_VAR cre_to_delete = ~NOBW12~ END
	// Add new Noble and Lady Odelia
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 271
		fj_loc_y          = 555
		fj_dest_x         = 271
		fj_dest_y         = 555
		fj_orientation    = 6
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Noble Woman~
		fj_cre_resref     = ~JA#NOPA1~ // with NOBW5.DLG
		fj_bcs_specific   = ~SARRUN~
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 297
		fj_loc_y          = 603
		fj_dest_x         = 297
		fj_dest_y         = 603
		fj_orientation    = 9
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Noble Woman~
		fj_cre_resref     = ~JA#NOPA5~ // with JA#NOBW6.DLG
		fj_bcs_specific   = ~SARRUN~
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 274
		fj_loc_y          = 509
		fj_dest_x         = 274
		fj_dest_y         = 509
		fj_orientation    = 0
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Noble Man~
		fj_cre_resref     = ~JA#NOPA2~ // with NOBL12.DLG
		fj_bcs_specific   = ~SARRUN~
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 286
		fj_loc_y          = 471
		fj_dest_x         = 286
		fj_dest_y         = 471
		fj_orientation    = 6
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Noble Man~
		fj_cre_resref     = ~JA#NOPA3~ // with JA#NOB00.DLG
		fj_bcs_specific   = ~SARRUN~
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 204
		fj_loc_y          = 511
		fj_dest_x         = 204
		fj_dest_y         = 511
		fj_orientation    = 14
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Noble Man~
		fj_cre_resref     = ~JA#NOPA4~ // with JA#NOB13.DLG
		fj_bcs_specific   = ~SARRUN~
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 784
		fj_loc_y          = 872
		fj_dest_x         = 784
		fj_dest_y         = 872
		fj_orientation    = 5
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Lady Odelia~
		fj_cre_resref     = ~JA#NOPA6~ // with JA#NOPA6.DLG
		fj_bcs_specific   = ~SARRUN~
	END
BUT_ONLY

COPY_EXISTING ~%NBaldursGate_DucalPalace_L2%.ARE~ ~override~
	LPF ALTER_AREA_ACTOR
		INT_VAR
		x_coord     = 264
		y_coord     = 523
		dest_x      = 264
		dest_y      = 523
		orient      = 13
		STR_VAR
		actor_name  = ~Nila~
	END
	// container scripts, scroll and key
	LPF ALTER_AREA_CONTAINER
		STR_VAR
		container_name   = ~Container 1~
		container_script = ~NONE~
		lock_difficulty  = 100 // will be unlocked by Nila
	END
	LPF ALTER_AREA_CONTAINER
		STR_VAR
		container_name   = ~Container 2~
		container_script = ~NONE~
	END
	LPF fj_are_structure
		INT_VAR
		fj_con_itm_idx    = 0
		fj_flags          = 0
		STR_VAR
		fj_structure_type = itm
		fj_name           = ~SCRL3F~
	END
	// new trap
	LPF fj_are_structure
		INT_VAR
		fj_type        = 0    // trap
		fj_box_left    = 538
		fj_box_top     = 545
		fj_box_right   = 616
		fj_box_bottom  = 596
		fj_cursor_idx  = 0
		fj_flags       = 0b0000000010
		fj_trap_active = 1
		fj_trap_status = 0
		fj_loc_x       = 776
		fj_loc_y       = 1103
		fj_vertex_0    = 586 + (545 << 16)
		fj_vertex_1    = 616 + (566 << 16)
		fj_vertex_2    = 570 + (596 << 16)
		fj_vertex_3    = 538 + (576 << 16)
		STR_VAR
		fj_structure_type = region
		fj_name           = ~JA#TRAP1~
		fj_reg_script     = ~JA#7209T~
	END
	// add actor
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 512
		fj_loc_y          = 700
		fj_dest_x         = 512
		fj_dest_y         = 700
		fj_orientation    = 9
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Camerarius~
		fj_cre_resref     = ~JA#KAEM1~
	END
BUT_ONLY

COPY_EXISTING ~%NBaldursGate_DucalPalace_L3%.ARE~ ~override~
	LPF DELETE_AREA_ACTOR STR_VAR cre_to_delete = ~FLAM11~ END
	// Add new Flaming Fist
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 909
		fj_loc_y          = 695
		fj_dest_x         = 909
		fj_dest_y         = 695
		fj_orientation    = 14
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Flammende Faust~
		fj_cre_resref     = ~JA#FLAM3~
		fj_bcs_specific   = ~JA#FLM01~
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 836
		fj_loc_y          = 750
		fj_dest_x         = 836
		fj_dest_y         = 750
		fj_orientation    = 14
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Flammende Faust~
		fj_cre_resref     = ~JA#FLAM3~
		fj_bcs_specific   = ~JA#FLM01~
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 566
		fj_loc_y          = 435
		fj_dest_x         = 566
		fj_dest_y         = 435
		fj_orientation    = 6
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Flammende Faust~
		fj_cre_resref     = ~JA#FLAM3~
		fj_bcs_specific   = ~JA#FLM01~
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 506
		fj_loc_y          = 476
		fj_dest_x         = 506
		fj_dest_y         = 476
		fj_orientation    = 6
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Flammende Faust~
		fj_cre_resref     = ~JA#FLAM3~
		fj_bcs_specific   = ~JA#FLM01~
	END
BUT_ONLY

COPY_EXISTING ~%NBaldursGate_DucalPalace_Cellar%.ARE~ ~override~
	GET_OFFSET_ARRAY actors ARE_V10_ACTORS
	PHP_EACH actors AS i => actor BEGIN
		READ_SHORT actor+0x20 actor_pos_x
		READ_ASCII actor+0x80 actor_filename
		PATCH_MATCH ~%actor_pos_x%~ WITH
			710 WHEN (~%actor_filename%~ STR_EQ ~FLAM7~) BEGIN
				WRITE_SHORT actor+0x20 710
				WRITE_SHORT actor+0x22 1055
				WRITE_SHORT actor+0x24 710
				WRITE_SHORT actor+0x26 1055
				WRITE_SHORT actor+0x34 12
			END
			693 WHEN (~%actor_filename%~ STR_EQ ~FLAM7~) BEGIN
				WRITE_SHORT actor+0x20 693
				WRITE_SHORT actor+0x22 1072
				WRITE_SHORT actor+0x24 693
				WRITE_SHORT actor+0x26 1072
				WRITE_SHORT actor+0x34 14
			END
			DEFAULT
		END
	END
	LPF DELETE_AREA_ACTOR STR_VAR cre_to_delete = ~PRISON~ END // exists only in BGT
	// add new actors
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 1122
		fj_loc_y          = 396
		fj_dest_x         = 1122
		fj_dest_y         = 396
		fj_orientation    = 2
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~Gefangener~
		fj_cre_resref     = ~JA#PRISO~
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 297
		fj_loc_y          = 603
		fj_dest_x         = 297
		fj_dest_y         = 603
		fj_orientation    = 9
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~FLAM7~
		fj_cre_resref     = ~FLAM7~
		END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 383
		fj_loc_y          = 579
		fj_dest_x         = 383
		fj_dest_y         = 579
		fj_orientation    = 4
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~FLAM7~
		fj_cre_resref     = ~FLAM7~
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 353
		fj_loc_y          = 530
		fj_dest_x         = 353
		fj_dest_y         = 530
		fj_orientation    = 1
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~FLAM7~
		fj_cre_resref     = ~FLAM7~
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x          = 895
		fj_loc_y          = 828
		fj_dest_x         = 895
		fj_dest_y         = 828
		fj_orientation    = 0
		STR_VAR
		fj_structure_type = actor
		fj_name           = ~JA#FLAM1~
		fj_cre_resref     = ~JA#FLAM1~
	END
	// add region triggers
	LPF fj_are_structure
		INT_VAR
		fj_type         = 2    // travel
		fj_box_left     = 547
		fj_box_top      = 244
		fj_box_right    = 611
		fj_box_bottom   = 346
		fj_cursor_idx   = 34
		fj_flags        = 0b000100000100 // Party required(2) Trigger deactivated(8)
		fj_loc_x        = 612
		fj_loc_y        = 337
		fj_vertex_0     = 555 + (346 << 16)
		fj_vertex_1     = 547 + (283 << 16)
		fj_vertex_2     = 595 + (244 << 16)
		fj_vertex_3     = 611 + (308 << 16)
		STR_VAR
		fj_structure_type   = region
		fj_destination_area = EVAL ~%CentralSewers%~
		fj_destination_name = ~JA#7211~
		fj_name             = ~JA#AR7325~
	END
	LPF fj_are_structure
		INT_VAR
		fj_type         = 1    // info
		fj_box_left     = 448
		fj_box_top      = 354
		fj_box_right    = 458
		fj_box_bottom   = 374
		fj_cursor_idx   = 8
		fj_flags        = 0b000100000000 // Trigger deactivated(8)
		fj_loc_x        = 494
		fj_loc_y        = 414
		fj_vertex_0     = 450 + (374 << 16)
		fj_vertex_1     = 448 + (363 << 16)
		fj_vertex_2     = 458 + (354 << 16)
		fj_vertex_3     = 455 + (374 << 16)
		STR_VAR
		fj_structure_type   = region
		fj_reg_script       = ~JA#CANDL~
		fj_name             = ~JA#CANDL~
	END
	LPF fj_are_structure
		INT_VAR
		fj_type         = 0    // trap
		fj_box_left     = 599
		fj_box_top      = 1007
		fj_box_right    = 691
		fj_box_bottom   = 1079
		fj_cursor_idx   = 0
		fj_flags        = 0b0000001010
		fj_trap_active  = 1
		fj_trap_status  = 0
		fj_trap_detect  = 60
		fj_trap_remove  = 50
		fj_loc_x        = 776
		fj_loc_y        = 1103
		fj_vertex_0     = 674 + (1007 << 16)
		fj_vertex_1     = 599 + (1066 << 16)
		fj_vertex_2     = 616 + (1079 << 16)
		fj_vertex_3     = 691 + (1020 << 16)
		STR_VAR
		fj_structure_type   = region
		fj_name             = ~JA#TRAP1~
		fj_reg_script       = ~GSPEAR~
	END
	LPF fj_are_structure
		INT_VAR
		fj_type         = 0    // trap
		fj_box_left     = 514
		fj_box_top      = 950
		fj_box_right    = 606
		fj_box_bottom   = 1022
		fj_cursor_idx   = 0
		fj_flags        = 0b0000001010
		fj_trap_active  = 1
		fj_trap_status  = 0
		fj_trap_detect  = 70
		fj_trap_remove  = 55
		fj_loc_x        = 471
		fj_loc_y        = 898
		fj_vertex_0     = 589 + (950 << 16)
		fj_vertex_1     = 514 + (1009 << 16)
		fj_vertex_2     = 531 + (1022 << 16)
		fj_vertex_3     = 606 + (963 << 16)
		STR_VAR
		fj_structure_type   = region
		fj_name             = ~JA#TRAP2~
		fj_reg_script       = ~GTLB~
	END
	LPF fj_are_structure
		INT_VAR
		fj_type         = 0    // trap
		fj_box_left     = 484
		fj_box_top      = 356
		fj_box_right    = 592
		fj_box_bottom   = 467
		fj_cursor_idx   = 0
		fj_flags        = 0b0000001000
		fj_trap_active  = 1
		fj_trap_status  = 0
		fj_trap_detect  = 80
		fj_trap_remove  = 60
		fj_loc_x        = 461
		fj_loc_y        = 768
		fj_vertex_0     = 484 + (446 << 16)
		fj_vertex_1     = 521 + (467 << 16)
		fj_vertex_2     = 592 + (376 << 16)
		fj_vertex_3     = 557 + (356 << 16)
		STR_VAR
		fj_structure_type   = region
		fj_name             = ~JA#TRAP3~
		fj_reg_script       = ~GTCK~
	END
	LPF fj_are_structure
		INT_VAR
		fj_type         = 0    // trap
		fj_box_left     = 624
		fj_box_top      = 528
		fj_box_right    = 1199
		fj_box_bottom   = 957
		fj_cursor_idx   = 0
		fj_flags        = 0b00000000010
		fj_trap_active  = 1
		fj_trap_status  = 0
		fj_trap_detect  = 0
		fj_trap_remove  = 0
		fj_loc_x        = 461
		fj_loc_y        = 768
		fj_vertex_0     = 624 + (914 << 16)
		fj_vertex_1     = 1146 + (528 << 16)
		fj_vertex_2     = 1199 + (566 << 16)
		fj_vertex_3     = 683 + (957 << 16)
		STR_VAR
		fj_structure_type   = region
		fj_name             = ~JA#TRAP4~
		fj_reg_script       = ~JA#7211T~
	END
	// add entrance
	LPF fj_are_structure
		INT_VAR
		fj_loc_x       = 627   // x
		fj_loc_y       = 360   // y
		fj_orientation = 14
		STR_VAR
		fj_structure_type = entrance
		fj_name           = ~JA#7325~
	END
	// bam as optical entrance JA#7325
	LPF fj_are_structure
	  INT_VAR
		fj_loc_x = 551
		fj_loc_y = 246
		fj_schedule = 0b111111111111111111111111 // Hours 0.00 - 24.00
		fj_flags = 0b00000000000000000001000101000000 // blend colours, not covered by wall, don't remove in combat
		STR_VAR
		fj_structure_type = animation
		fj_name = ~JA#7211A~
		fj_bam_resref = ~JA#7211A~
	END
BUT_ONLY

COPY_EXISTING ~%CentralSewers%.are~ ~override~
	// add region triggers
	LPF fj_are_structure
		INT_VAR
		fj_type         = 2    // travel
		fj_box_left     = 420
		fj_box_top      = 1741
		fj_box_right    = 596
		fj_box_bottom   = 1908
		fj_cursor_idx   = 34
		fj_flags        = 0b000100000100 // Party required(2) Trigger deactivated(8)
		fj_loc_x        = 572
		fj_loc_y        = 1803
		fj_vertex_0     = 573 + (1775 << 16)
		fj_vertex_1     = 420 + (1741 << 16)
		fj_vertex_2     = 454 + (1908 << 16)
		fj_vertex_3     = 596 + (1907 << 16)
		STR_VAR
		fj_structure_type   = region
		fj_destination_area = EVAL ~%NBaldursGate_DucalPalace_Cellar%~
		fj_destination_name = ~JA#7325~
		fj_name             = ~JA#7211~
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x       = 667    // x
		fj_loc_y       = 1818   // y
		fj_orientation = 13
		STR_VAR
		fj_structure_type = entrance
		fj_name           = ~JA#7211~
	END
BUT_ONLY
