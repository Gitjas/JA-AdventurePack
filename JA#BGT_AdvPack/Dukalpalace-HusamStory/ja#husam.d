
ADD_TRANS_ACTION HUSAM
BEGIN 3 END
BEGIN 0 END
~SetGlobal("JA#LEADERS","LOCALS",1)~

ADD_TRANS_ACTION HUSAM
BEGIN 3 END
BEGIN 1 END
~SetGlobal("JA#PLANS","LOCALS",1)~

EXTEND_BOTTOM HUSAM 3
IF ~~ THEN REPLY @0 DO ~SetGlobal("JA#DOOR","LOCALS",1) SetGlobal("JA#HUSAM_SECRET","GLOBAL",2)~ GOTO 6
END


ALTER_TRANS HUSAM
BEGIN 4 END
BEGIN 0 END
BEGIN
  "EPILOGUE" ~GOTO 7~
END


ALTER_TRANS HUSAM
BEGIN 5 END
BEGIN 0 END
BEGIN
  "EPILOGUE" ~GOTO 7~
END


ADD_TRANS_TRIGGER HUSAM 7
~Global("JA#PLANS","LOCALS",0)~ DO 1

ADD_TRANS_ACTION HUSAM
BEGIN 7 END
BEGIN 1 END
~SetGlobal("JA#PLANS","LOCALS",1)~

ALTER_TRANS HUSAM
BEGIN 7 END
BEGIN 1 END
BEGIN
  "EPILOGUE" ~GOTO 5~
END

EXTEND_BOTTOM HUSAM 7
IF ~Global("JA#LEADERS","LOCALS",0)~ THEN REPLY @1 DO ~SetGlobal("JA#LEADERS","LOCALS",1)~ GOTO 4
IF ~Global("JA#DOOR","LOCALS",0)~ THEN REPLY @0 DO ~SetGlobal("JA#DOOR","LOCALS",1) SetGlobal("JA#HUSAM_SECRET","GLOBAL",2)~ GOTO 6
END

EXTEND_BOTTOM HUSAM 6
IF ~OR(2) Global("JA#PLANS","LOCALS",0) Global("JA#LEADERS","LOCALS",0)~ THEN UNSOLVED_JOURNAL #%husam6% GOTO 7
END


EXTEND_BOTTOM HUSAM 11
IF ~Global("JA#DOOR","LOCALS",0)~ THEN DO ~SetGlobal("JA#DOOR","LOCALS",1) SetGlobal("JA#HUSAM_SECRET","GLOBAL",2)~ GOTO JA#HUSAM_2
END

APPEND HUSAM

IF ~~ THEN BEGIN JA#HUSAM_2
SAY @2
COPY_TRANS HUSAM 11
END

END // APPEND HUSAM

ALTER_TRANS HUSAM
BEGIN JA#HUSAM_2 END
BEGIN END
BEGIN
  "UNSOLVED_JOURNAL" ~#%husam6%~
END




// HUSAM2


ALTER_TRANS HUSAM2
BEGIN 3 END
BEGIN 1 END
BEGIN
  "ACTION" ~ClearAllActions()
StartCutSceneMode()
StartCutScene("JA#HUCU1")~
END

ALTER_TRANS HUSAM2
BEGIN 12 END
BEGIN 0 END
BEGIN
  "ACTION" ~ClearAllActions()
MultiplayerSync()
StartCutSceneMode()
StartCutScene("JA#HUCU3")~
END

ALTER_TRANS HUSAM2
BEGIN 16 END
BEGIN 1 END
BEGIN
  "EPILOGUE" ~GOTO JA#HUSAM_3~
END



APPEND HUSAM2

IF ~~ THEN BEGIN JA#HUSAM_3
SAY @3
IF ~~ THEN DO ~SetGlobal("HusamMove","GLOBAL",7)
EscapeAreaDestroy(90)~ UNSOLVED_JOURNAL @4 EXIT
END

END



BEGIN JA#HUSAM

IF ~Global("HusamMove","GLOBAL",7)
Global("JA#HUSAM_MOVE","GLOBAL",0)~ THEN BEGIN JA#HUSAM2_1
SAY @5
= @6
IF ~~ THEN DO ~SetGlobal("JA#HUSAM_MOVE","GLOBAL",1)MoveToPoint([1773.3193])DestroySelf()~ UNSOLVED_JOURNAL @7 EXIT
END

IF ~Global("JA#HUSAM_MOVE","GLOBAL",1)~ THEN BEGIN JA#HUSAM2_2
SAY @8
IF ~!PartyHasItem("SCRL3F")~ THEN REPLY @9 GOTO JA#HUSAM2_3
IF ~PartyHasItem("SCRL3F")~ THEN REPLY @10 GOTO JA#HUSAM2_4
END

IF ~~ THEN BEGIN JA#HUSAM2_3
SAY @11
= @12
IF ~~ THEN DO ~SetGlobal("JA#HUSAM_MOVE","GLOBAL",2)MoveToPoint([900.838])~ UNSOLVED_JOURNAL @13 EXIT
END

IF ~~ THEN BEGIN JA#HUSAM2_4
SAY @14
= @12
IF ~~ THEN DO ~SetGlobal("JA#HUSAM_MOVE","GLOBAL",2)MoveToPoint([900.838])~ UNSOLVED_JOURNAL @15 EXIT
END

IF ~Global("JA#HUSAM_MOVE","GLOBAL",2)~ THEN BEGIN JA#HUSAM2_5
SAY @16
IF ~~ THEN EXIT
END

IF ~Global("JA#HUSAM_MOVE","GLOBAL",3)
Global("SarevokBehavior","GLOBAL",1)~ THEN BEGIN JA#HUSAM2_6
SAY @17
= @18
IF ~~ THEN DO ~EraseJournalEntry(@15)
EraseJournalEntry(@13)
EraseJournalEntry(@7)
EraseJournalEntry(@4)
SetGlobal("JA#HUSAM_MOVE","GLOBAL",4)
EscapeAreaDestroy(90)~ SOLVED_JOURNAL @19 EXIT
END
