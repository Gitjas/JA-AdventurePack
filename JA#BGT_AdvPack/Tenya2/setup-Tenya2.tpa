/////////////////////////////////////
//Tenya Quest in Temple of Umberlee
/////////////////////////////////////


COPY ~JA#BGT_AdvPack/Tenya2/JA#C02.ARE~ ~override~
	PATCH_IF GAME_IS ~bgee eet~ BEGIN
		WRITE_ASCIIE 0x8 ~%NWBaldursGate_House4_L1%~ #8
		LPF ALTER_AREA_REGION
			STR_VAR region_name			= ~Door8200~
					destination_area	= EVAL ~%SEBaldursGate%~
		END
	END

COPY ~JA#BGT_AdvPack/Tenya2/JA#C03.ARE~ ~override~
	PATCH_IF GAME_IS ~bgee eet~ BEGIN
		WRITE_ASCIIE 0x8 ~%NWBaldursGate_House4_L2%~ #8
	END

COPY ~JA#BGT_AdvPack/Tenya2/JA#SUTY1.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Tenya2/JA#SUTY2.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Tenya2/JA#SUTY3.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Tenya2/JA#SUTY4.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Tenya2/JA#CTENY.BAM~ ~override~
COPY ~JA#BGT_AdvPack/Tenya2/JA#TENYA.BAM~ ~override~
COPY ~JA#BGT_AdvPack/Tenya2/JA#TENYA.ITM~ ~override~
	SAY NAME1 @2112
	SAY NAME2 @2113
	SAY UNIDENTIFIED_DESC @2115
	SAY DESC @2114

COPY ~JA#BGT_AdvPack/Tenya2/JA#TLOS1.CRE~ ~override~
	SAY NAME1 @2116
	SAY NAME2 @2116

ACTION_IF GAME_IS ~bgt~ AND NOT FILE_EXISTS ~t-wei01.wav~ BEGIN
	APPEND ~animate.ids~ ~0xEF10 WATER_WEIRD~ UNLESS ~^0x[Ee][Ff]10~
	COPY ~JA#BGT_AdvPack/Tenya2/WaterWeird~ ~override~
END
COPY ~JA#BGT_AdvPack/Tenya2/JA#WATW1.ITM~ ~override~
COPY ~JA#BGT_AdvPack/Tenya2/JA#WATW1.CRE~ ~override~
	SAY NAME1 @2111
	SAY NAME2 @2111
// COPY_EXISTING ~WATERELE.ITM~ ~override~
//   WRITE_BYTE 0x72 1 // melee instead of range


COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Tenya2/JA#TENYA.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Tenya2/JA#WATW1.BAF~

EXTEND_TOP ~PUMBER.BCS~ ~JA#BGT_AdvPack/Tenya2/JA#PUMBE.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%BaldursGateDocks_WaterQueensHouse_BCS%.BCS~ ~JA#BGT_AdvPack/Tenya2/AR7609.BAF~ EVALUATE_BUFFER


LAF GET_DLG_JOURNAL_STRREF
	INT_VAR idx_state = 28
	STR_VAR dlg_file = ~TENYA.DLG~
	RET tenya_state28_journal = strref
END
LAF GET_DLG_STATE_RESPONSE
	INT_VAR idx_state = 0
			idx_response = 2
	STR_VAR dlg_file = ~TENYA2.DLG~
	RET tenya2_state0_weight = idx_state_trigger
		tenya2_state0_response2_strref = response_strref
END

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Tenya2/ja#pumber2.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Tenya2/ja#pumberl.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Tenya2/ja#jalant.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Tenya2/ja#tenya2.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Tenya2/ja#tlos1.d~

ADD_JOURNAL @22 @1022 USING ~JA#BGT_AdvPack/languages/%s/ja#tenya2.tra~


COPY_EXISTING ~PUMBER2.CRE~ ~override~
	SAY NAME1 @2117
	SAY NAME2 @2117
	LPF REPLACE_CRE_SCRIPT STR_VAR script_old = ~WDASIGHT~ script_new = ~WTASIGHT~ END // no RandomWalk()
BUT_ONLY

COPY_EXISTING ~PUMBERL.CRE~ ~override~
	LPF ADD_CRE_SCRIPT INT_VAR offset_start = SCRIPT_CLASS STR_VAR script_new = ~PUMBER~ END
BUT_ONLY

COPY_EXISTING ~TENYA2.CRE~ ~override~
	LPF REMOVE_CRE_SCRIPT STR_VAR script_old = ~TENYA~ END
	LPF REMOVE_CRE_SCRIPT STR_VAR script_old = ~INITDLG~ END
	LPF ADD_CRE_SCRIPT STR_VAR script_new = ~JA#TENYA~ END
BUT_ONLY


COPY_EXISTING ~%SEBaldursGate%.ARE~ ~override~
	//add region triggers
	LPF fj_are_structure
		INT_VAR
		fj_type         = 2    //travel
		fj_box_left     = 1030
		fj_box_top      = 806
		fj_box_right    = 1260
		fj_box_bottom   = 990
		fj_cursor_idx	= 30
		fj_loc_x		= 1068
		fj_loc_y		= 850
		fj_vertex_0     = 1133 + (806 << 16)
		fj_vertex_1     = 1030 + (881 << 16)
		fj_vertex_2     = 1114 + (990 << 16)
		fj_vertex_3     = 1260 + (886 << 16)
		STR_VAR
		fj_structure_type   = region
		fj_destination_area	= JA#C02
		fj_destination_name	= FR8200
		fj_name             = DOORJA#C02
	END
	//add entrance
	LPF  ~fj_are_structure~
		INT_VAR
		fj_loc_x       = 978   //x
		fj_loc_y       = 781   //y
		fj_orientation = 7
		STR_VAR
		fj_structure_type = entrance
		fj_name           = FRJA#C02
	END
BUT_ONLY

COPY_EXISTING ~%BaldursGateDocks_WaterQueensHouse%.ARE~ ~override~
	GET_OFFSET_ARRAY actors_array ARE_V10_ACTORS
	PHP_EACH actors_array AS actors_num => actors_offset BEGIN
		READ_SHORT actors_offset+0x20 actors_pos_x
		READ_ASCII actors_offset+0x80 actors_filename
		//Flaming Fist Dialog and Script changes
		PATCH_IF ((~%actors_pos_x%~ = 1029) AND (~%actors_filename%~ STR_EQ  ~PUMBER2~)) BEGIN
			WRITE_SHORT actors_offset + 0x20        1143
			WRITE_SHORT actors_offset + 0x22        610
			WRITE_SHORT actors_offset + 0x24        1143
			WRITE_SHORT actors_offset + 0x26        610
			WRITE_SHORT actors_offset + 0x34        5
		END
		PATCH_IF ((~%actors_pos_x%~ = 936) AND (~%actors_filename%~ STR_EQ  ~PUMBER2~)) BEGIN
			WRITE_SHORT actors_offset + 0x20        395
			WRITE_SHORT actors_offset + 0x22        825
			WRITE_SHORT actors_offset + 0x24        395
			WRITE_SHORT actors_offset + 0x26        825
			WRITE_SHORT actors_offset + 0x34        11
		END
	END
BUT_ONLY
