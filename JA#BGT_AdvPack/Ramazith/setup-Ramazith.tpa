/////////////////////////
//Ramaziths Turm
/////////////////////////


// BGT (coord fix)
COPY_EXISTING ~AR7238.BCS~ ~override~ // %NBaldursGate_RamazithsTower_L6%
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH
		~CreateCreature("RAMAZI",[0.0],0)~
		~CreateCreature("RAMAZI",[169.147],13)~
	END
IF_EXISTS BUT_ONLY


<<<<<<<< ...inlined/AR7300_old.baf
IF
	Exists("Ramazith")
	Global("HelpRamazith","GLOBAL",0)
	Global("AbelaTeleport","GLOBAL",1)
THEN
	RESPONSE #100
		ActionOverride("Ramazith",DestroySelf())
END
>>>>>>>>
<<<<<<<< ...inlined/AR7300_new.baf
IF
	Exists("Ramazith")
	OR(2)
		Dead("Abela")
		Global("AbelaTeleport","GLOBAL",1)
THEN
	RESPONSE #100
		ActionOverride("Ramazith",DestroySelf())
		SetGlobal("RamazithMove","GLOBAL",1)
END
>>>>>>>>
// BGT (Ramazith moves into his tower when Abela is gone)
COPY_EXISTING ~AR7300.BCS~ ~override~ // %NBaldursGate%
	REPLACE_BCS_BLOCK
		~...inlined/AR7300_old.baf~
		~...inlined/AR7300_new.baf~
	ON_MISMATCH
		PATCH_WARN ~WARNING: Something went wrong while patching AR7300.BCS! Trying to replace each part separately...~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY EXACT_MATCH
			~[%WNL%%MNL%%LNL%%TAB% ]Global("HelpRamazith","GLOBAL",0)~
			~ True()~ // ~AreaCheck(%NBaldursGate%)~ always true here
			REPLACE_TEXTUALLY EXACT_MATCH
			~[%WNL%%MNL%%LNL%%TAB% ]Global("AbelaTeleport","GLOBAL",1)~
			~ OR(2) Global("AbelaTeleport","GLOBAL",1) Dead("Abela")~
			REPLACE_TEXTUALLY EXACT_MATCH
			~ActionOverride("Ramazith",DestroySelf())~
			~ActionOverride("Ramazith",DestroySelf()) SetGlobal("RamazithMove","GLOBAL",1)~
		END
	END
IF_EXISTS BUT_ONLY

// BGEE/EET (Ramazith moves into his tower when Abela is gone)
COPY_EXISTING ~RAMAZITH.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EVALUATE_REGEXP
		~[%WNL%%MNL%%LNL%%TAB% ]Global("HelpRamazith","GLOBAL",0)~
		~ AreaCheck("%NBaldursGate%")~
	END
BUT_ONLY


// Replace in Ramazith's tower L2 existing creatures with spawns
COPY_EXISTING ~%NBaldursGate_RamazithsTower_L2%.ARE~ ~override~
	FOR (cnt=BYTE_AT 0x58 -1; cnt>=0; --cnt) BEGIN
		READ_ASCII SHORT_AT 0x54 +(0x110 * (cnt))+0x80 actors_filename (32) NULL
		PATCH_IF (~%actors_filename%~ STR_EQ  ~JELLYMU~) BEGIN
			LPF fj_are_structure
				INT_VAR fj_delete_mode = EVAL %cnt%
				STR_VAR fj_structure_type = actor
			END
		END
	END
BUT_ONLY
EXTEND_TOP ~%NBaldursGate_RamazithsTower_L2%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7239.BAF~ EVALUATE_BUFFER

// Replace in Ramazith's tower L3 existing creatures with spawns
ACTION_IF NOT (MOD_IS_INSTALLED ~setup-FotD.tp2~ 0) BEGIN
	COPY_EXISTING ~%NBaldursGate_RamazithsTower_L3%.ARE~ ~override~
		FOR (cnt=BYTE_AT 0x58 -1; cnt>=0; --cnt) BEGIN
			READ_ASCII SHORT_AT 0x54 +(0x110 * (cnt))+0x80 actors_filename (32) NULL
			PATCH_IF (~%actors_filename%~ STR_EQ  ~GHAST~) BEGIN
				LPF fj_are_structure
					INT_VAR fj_delete_mode = EVAL %cnt%
					STR_VAR fj_structure_type = actor
				END
			END
		END
	BUT_ONLY
	EXTEND_TOP ~%NBaldursGate_RamazithsTower_L3%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7240.BAF~ EVALUATE_BUFFER
END

// Replace in Ramazith's tower L4 existing creatures with spawns
COPY_EXISTING ~%NBaldursGate_RamazithsTower_L4%.ARE~ ~override~
	FOR (cnt=BYTE_AT 0x58 -1; cnt>=0; --cnt) BEGIN
		READ_ASCII SHORT_AT 0x54 +(0x110 * (cnt))+0x80 actors_filename (32) NULL
		PATCH_IF (~%actors_filename%~ STR_EQ  ~HOBELITE~) BEGIN
			LPF fj_are_structure
				INT_VAR fj_delete_mode = EVAL %cnt%
				STR_VAR fj_structure_type = actor
			END
		END
	END
BUT_ONLY
EXTEND_TOP ~%NBaldursGate_RamazithsTower_L4%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7241.BAF~ EVALUATE_BUFFER

// Replace in Ramazith's tower L5 existing creatures with spawns
COPY_EXISTING ~%NBaldursGate_RamazithsTower_L5%.ARE~ ~override~
	FOR (cnt=BYTE_AT 0x58 -1; cnt>=0; --cnt) BEGIN
		READ_ASCII SHORT_AT 0x54 +(0x110 * (cnt))+0x80 actors_filename (32) NULL
		PATCH_IF (~%actors_filename%~ STR_EQ  ~KOBCOMM~) BEGIN
			LPF fj_are_structure
				INT_VAR fj_delete_mode = EVAL %cnt%
				STR_VAR fj_structure_type = actor
			END
		END
	END
BUT_ONLY
EXTEND_TOP ~%NBaldursGate_RamazithsTower_L5%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7242.BAF~ EVALUATE_BUFFER

// Hostile Ramazith will start dialogue
EXTEND_BOTTOM ~%NBaldursGate_RamazithsTower_L6%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7238.BAF~ EVALUATE_BUFFER

// Spawn JA#IMP01.CRE(Yuxi) and start cutscene with JA#JOPI(Jopi)
EXTEND_TOP ~%WBaldursGate_RagefastsHouse%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7226.BAF~ EVALUATE_BUFFER

// Spawn and despawn JA#JOPI.CRE(Jopi)
EXTEND_TOP ~%WBaldursGate%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7600.BAF~ EVALUATE_BUFFER


OUTER_SET default_state_during_quest = (GAME_IS ~bgt~) ? 22 : 10
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Ramazith/ja#ramazi.d~

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Ramazith/ja#jopi.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Ramazith/ja#abela.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Ramazith/ja#imp01.d~

ADD_JOURNAL EXISTING @18 USING ~JA#BGT_AdvPack/languages/%LANGUAGE%/ja#ramazi.tra~
ADD_JOURNAL @22 @39 @1023 USING ~JA#BGT_AdvPack/languages/%LANGUAGE%/ja#jopi.tra~


// Make CLCK07.ITM(Nymph Cloak) undroppable - can be obtained other ways
COPY_EXISTING ~ABELA.CRE~ ~override~
	GET_OFFSET_ARRAY items CRE_V10_ITEMS
	PHP_EACH items AS i => item BEGIN
		READ_ASCII item item_filename
		PATCH_IF (~%item_filename%~ STR_EQ ~CLCK07~) BEGIN
			WRITE_LONG item+0x10 (THIS | 1<<3)
		END
	END
BUT_ONLY

COPY_EXISTING ~RAGEFA.CRE~ ~override~
	WRITE_ASCII 0x248 ~SHOUTDLG~ #8 // override ~INITDLG~ (BGT)
BUT_ONLY
COPY_EXISTING ~JOPI.CRE~ ~override/JA#JOPI.CRE~
	WRITE_ASCII 0x268 ~RUNENEMY~ #8 // override ~WDRUNSGT~
	WRITE_ASCII 0x2cc ~JA#JOPI~ #8
COPY ~JA#BGT_AdvPack/Ramazith/JA#GOLC1.CRE~ ~override~
	SAY NAME1 @2040
	SAY NAME2 @2040
COPY ~JA#BGT_AdvPack/Ramazith/JA#IMP01.CRE~ ~override~
	SAY NAME1 @2042
	SAY NAME2 @2042

COMPILE ~JA#BGT_AdvPack/Ramazith/JA#CURA1.BAF~
COMPILE ~JA#BGT_AdvPack/Ramazith/JA#CUJO1.BAF~
COMPILE ~JA#BGT_AdvPack/Ramazith/JA#IMP01.BAF~


ACTION_IF GAME_IS ~bgt~ BEGIN
	COPY ~JA#BGT_AdvPack/Ramazith/AR7226.WED~ ~override/%WBaldursGate_RagefastsHouse%.WED~
	COPY ~JA#BGT_AdvPack/Ramazith/AR7238.WED~ ~override/%NBaldursGate_RamazithsTower_L6%.WED~
	INCLUDE ~%MOD_FOLDER%/lib/alter_searchmap.tpa~
	LAF ALTER_SEARCHMAP
		STR_VAR
		path_to_2da_file = EVAL ~%MOD_FOLDER%/Ramazith/AR7238SR_changes.2da~ 	// full path to the *changes.2da file containing your icons, e.g. ~mymod/bam/AR3700SR_changes_ee.2da~
		areaname = EVAL ~%NBaldursGate_RamazithsTower_L6%~ 						// area name, e.g. ~AR3700~
	END
END
