/////////////////////////
//Ramaziths Turm
/////////////////////////


COPY_EXISTING ~%NBaldursGate%.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		// BGT
		REPLACE_TEXTUALLY EXACT_MATCH
		~Global("AbelaTeleport","GLOBAL",1)~
		~OR(2) Dead("Abela") Global("AbelaTeleport","GLOBAL",1)~
	END
BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~%NBaldursGate_RamazithsTower_L6%.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("RAMAZI",[0.0],0)~ ~CreateCreature("RAMAZI",[169.147],13)~
	END
BUT_ONLY_IF_IT_CHANGES

// TODO: needed?
COPY_EXISTING ~RAMAZITH.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~GlobalLT("RamazithMove","GLOBAL",2)~
		~GlobalLT("RamazithMove","GLOBAL",2) Global("JA#RamazithDeal","GLOBAL",0)~
	END
BUT_ONLY_IF_IT_CHANGES

//delete existing creatures
COPY_EXISTING ~%NBaldursGate_RamazithsTower_L2%.ARE~ ~override~
	FOR (cnt=BYTE_AT 0x58 -1; cnt>=0; --cnt) BEGIN
		READ_ASCII SHORT_AT 0x54 +(0x110 * (cnt))+0x80 actors_filename (32) NULL
		PATCH_IF (~%actors_filename%~ STR_EQ  ~JELLYMU~) BEGIN
			LPF fj_are_structure
				INT_VAR fj_delete_mode = EVAL %cnt%
				STR_VAR fj_structure_type = actor
			END
		END
	END
BUT_ONLY

ACTION_IF NOT (MOD_IS_INSTALLED ~setup-FotD.tp2~ 0) BEGIN
	COPY_EXISTING ~%NBaldursGate_RamazithsTower_L3%.ARE~ ~override~
		FOR (cnt=BYTE_AT 0x58 -1; cnt>=0; --cnt) BEGIN
			READ_ASCII SHORT_AT 0x54 +(0x110 * (cnt))+0x80 actors_filename (32) NULL
			PATCH_IF (~%actors_filename%~ STR_EQ  ~GHAST~) BEGIN
				LPF fj_are_structure
					INT_VAR fj_delete_mode = EVAL %cnt%
					STR_VAR fj_structure_type = actor
				END
			END
		END
	BUT_ONLY
END

COPY_EXISTING ~%NBaldursGate_RamazithsTower_L4%.ARE~ ~override~
	FOR (cnt=BYTE_AT 0x58 -1; cnt>=0; --cnt) BEGIN
		READ_ASCII SHORT_AT 0x54 +(0x110 * (cnt))+0x80 actors_filename (32) NULL
		PATCH_IF (~%actors_filename%~ STR_EQ  ~HOBELITE~) BEGIN
			LPF fj_are_structure
				INT_VAR fj_delete_mode = EVAL %cnt%
				STR_VAR fj_structure_type = actor
			END
		END
	END
BUT_ONLY

COPY_EXISTING ~%NBaldursGate_RamazithsTower_L5%.ARE~ ~override~
	FOR (cnt=BYTE_AT 0x58 -1; cnt>=0; --cnt) BEGIN
		READ_ASCII SHORT_AT 0x54 +(0x110 * (cnt))+0x80 actors_filename (32) NULL
		PATCH_IF (~%actors_filename%~ STR_EQ  ~KOBCOMM~) BEGIN
			LPF fj_are_structure
				INT_VAR fj_delete_mode = EVAL %cnt%
				STR_VAR fj_structure_type = actor
			END
		END
	END
BUT_ONLY

//Add Ramazith and new creatures by script
EXTEND_TOP ~%WBaldursGate_RagefastsHouse%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7226.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%NBaldursGate_RamazithsTower_L1%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7237.BAF~ EVALUATE_BUFFER
EXTEND_BOTTOM ~%NBaldursGate_RamazithsTower_L6%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7238.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%NBaldursGate_RamazithsTower_L2%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7239.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%NBaldursGate_RamazithsTower_L3%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7240.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%NBaldursGate_RamazithsTower_L4%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7241.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%NBaldursGate_RamazithsTower_L5%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7242.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%WBaldursGate%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7600.BAF~ EVALUATE_BUFFER

//Ramazith vanishes, if Abela dead and item denied for Ramazith
EXTEND_TOP ~RAMAZITH.BCS~ ~JA#BGT_AdvPack/Ramazith/RAMAZITH.BAF~

// Grab actions from first response of state 15
COPY_EXISTING ~RAMAZI.DLG~ ~override~
	SET state = 15
    READ_LONG 0x0c ofs_states
    READ_LONG 0x14 ofs_responses
    READ_LONG 0x28 ofs_response_actions

	SET ofs_state = ofs_states + (state * 0x10)
	READ_LONG (ofs_state + 0x4) idx_first_response

	SET ofs_response = ofs_responses + idx_first_response * 0x20
	READ_LONG (ofs_response + 0x10) idx_response_action

	SET ofs_action = ofs_response_actions + idx_response_action * 0x8
	READ_LONG ofs_action actions_ptr
	READ_LONG (ofs_action + 0x8) next_actions_ptr
	READ_ASCII actions_ptr state15_actions (next_actions_ptr - actions_ptr)

OUTER_SET default_dialog_state = (GAME_IS ~bgt~) ? 22 : 10
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Ramazith/JA#RAMAZI.D~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Ramazith/JA#JOPI.D~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Ramazith/JA#ABELA.D~


COPY_EXISTING ~RAGEFA.CRE~ ~override~
	WRITE_ASCII 0x248 ~SHOUTDLG~ #8

COPY_EXISTING ~RAMAZI.CRE~ ~override/JA#RAMAZ.CRE~
	WRITE_ASCII 0x250 ~NONE~ #8
	WRITE_ASCII 0x260 ~NONE~ #8
	WRITE_ASCII 0x2cc ~JA#RAMAZ~ #8
COPY_EXISTING ~JOPI.CRE~ ~override/JA#JOPI.CRE~
	WRITE_ASCII 0x268 ~RUNENEMY~ #8
	WRITE_ASCII 0x2cc ~JA#JOPI~ #8
COPY ~JA#BGT_AdvPack/Ramazith/JA#GOLC1.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Ramazith/JA#IMP01.CRE~ ~override~
	SAY NAME1 @2042
	SAY NAME2 @2042

COMPILE ~JA#BGT_AdvPack/Ramazith/JA#CURA1.BAF~
COMPILE ~JA#BGT_AdvPack/Ramazith/JA#CUJO1.BAF~
COMPILE ~JA#BGT_AdvPack/Ramazith/JA#IMP01.BAF~

ACTION_IF GAME_IS ~bgt~ BEGIN
	COPY ~JA#BGT_AdvPack/Ramazith/AR7226.WED~ ~override/%WBaldursGate_RagefastsHouse%.WED~
	COPY ~JA#BGT_AdvPack/Ramazith/AR7238.WED~ ~override/%NBaldursGate_RamazithsTower_L6%.WED~
	INCLUDE ~%MOD_FOLDER%/lib/alter_searchmap.tpa~
	LAF ALTER_SEARCHMAP
		STR_VAR
		path_to_2da_file = EVAL ~%MOD_FOLDER%/Ramazith/AR7238SR_changes.2da~ 	// full path to the *changes.2da file containing your icons, e.g. ~mymod/bam/AR3700SR_changes_ee.2da~
		areaname = EVAL ~%NBaldursGate_RamazithsTower_L6%~ 							  		 			// area name, e.g. ~AR3700~
	END
END
