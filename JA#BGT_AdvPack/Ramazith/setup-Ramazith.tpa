/////////////////////////
//Ramaziths Turm
/////////////////////////

ACTION_IF GAME_IS ~bgt~ BEGIN
	COPY_EXISTING ~%NBaldursGate_RamazithsTower_L1%.BCS~ ~override~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("RAMAZI",[328.234],0)~ ~NoAction()~
			REPLACE_TEXTUALLY EXACT_MATCH ~ActionOverride("Ramazith",SetNumTimesTalkedTo(1))~ ~Continue()~
		END
	BUT_ONLY_IF_IT_CHANGES
	
	COPY_EXISTING ~%NBaldursGate_RamazithsTower_L6%.BCS~ ~override~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("RAMAZI",[0.0],0)~ ~CreateCreature("RAMAZI",[172.173],13)~
		END
	BUT_ONLY_IF_IT_CHANGES

	
	COPY_EXISTING ~RAMAZITH.BCS~ ~override~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY EXACT_MATCH ~GlobalLT("RamazithMove","GLOBAL",2)~ 
										~GlobalLT("RamazithMove","GLOBAL",2) Global("JA#RamazithDeal","GLOBAL",0)~
		END
	BUT_ONLY_IF_IT_CHANGES
	
	//deactivate actors in Ramazith tower
	COPY_EXISTING ~%NBaldursGate_RamazithsTower_L2%.ARE~ ~override~
		GET_OFFSET_ARRAY actors_array ARE_V10_ACTORS 
		PHP_EACH actors_array AS actors_num => actors_offset BEGIN
			READ_ASCII actors_offset+0x80 actors_filename (32) NULL
			PATCH_IF (~%actors_filename%~ STR_EQ  ~JELLYMU~) BEGIN
				WRITE_LONG actors_offset+0x38 0x0
			END
		END
	BUT_ONLY

	ACTION_IF NOT (MOD_IS_INSTALLED ~setup-FotD.tp2~ 0) BEGIN
		COPY_EXISTING ~%NBaldursGate_RamazithsTower_L3%.ARE~ ~override~
			GET_OFFSET_ARRAY actors_array ARE_V10_ACTORS 
			PHP_EACH actors_array AS actors_num => actors_offset BEGIN
				READ_ASCII actors_offset+0x80 actors_filename (32) NULL
				PATCH_IF (~%actors_filename%~ STR_EQ  ~GHAST~) BEGIN
					WRITE_LONG actors_offset+0x38 0x0
				END
			END
		BUT_ONLY
	END

	COPY_EXISTING ~%NBaldursGate_RamazithsTower_L4%.ARE~ ~override~
		GET_OFFSET_ARRAY actors_array ARE_V10_ACTORS 
		PHP_EACH actors_array AS actors_num => actors_offset BEGIN
			READ_ASCII actors_offset+0x80 actors_filename (32) NULL
			PATCH_IF (~%actors_filename%~ STR_EQ  ~HOBELITE~) BEGIN
				WRITE_LONG actors_offset+0x38 0x0
			END
		END
	BUT_ONLY
	
	COPY_EXISTING ~%NBaldursGate_RamazithsTower_L5%.ARE~ ~override~
		GET_OFFSET_ARRAY actors_array ARE_V10_ACTORS 
		PHP_EACH actors_array AS actors_num => actors_offset BEGIN
			READ_ASCII actors_offset+0x80 actors_filename (32) NULL
			PATCH_IF (~%actors_filename%~ STR_EQ  ~KOBCOMM~) BEGIN
				WRITE_LONG actors_offset+0x38 0x0
			END
		END
	BUT_ONLY

	
	//get stringref for journal entry
	COPY_EXISTING ~RAMAZI.dlg~ ~override~
		LPF ~READ_JOURNAL_STRREF~
			INT_VAR
			state         = "9"  // state index
			stateResponse = 0     // relative response index (e.g. 0 for first response associated with the state, 1 for second response, and so on...)
			RET
			strref    // returned string reference, or -1 if not available
		END
		SET ramazi1 = strref
		LPF ~READ_JOURNAL_STRREF~
			INT_VAR
			state         = "18"  // state index
			stateResponse = 0     // relative response index (e.g. 0 for first response associated with the state, 1 for second response, and so on...)
			RET
			strref    // returned string reference, or -1 if not available
		END
		SET ramazi3 = strref
	COPY_EXISTING ~RAGEFA.dlg~ ~override~
		LPF ~READ_JOURNAL_STRREF~
			INT_VAR
			state         = "3"  // state index
			stateResponse = 0     // relative response index (e.g. 0 for first response associated with the state, 1 for second response, and so on...)
			RET
			strref    // returned string reference, or -1 if not available
		END
		SET ramazi2 = strref
	COPY_EXISTING ~HANNAH.dlg~ ~override~
		LPF ~READ_JOURNAL_STRREF~
			INT_VAR
			state         = "3"  // state index
			stateResponse = 0     // relative response index (e.g. 0 for first response associated with the state, 1 for second response, and so on...)
			RET
			strref    // returned string reference, or -1 if not available
		END
		SET ramazi4 = strref
	
	COMPILE ~JA#BGT_AdvPack/Ramazith/JA#RAMAZI.D~
		REPLACE ~999999~ ~%ramazi1%~
		REPLACE ~999998~ ~%ramazi2%~
		REPLACE ~999997~ ~%ramazi3%~
		REPLACE ~999996~ ~%ramazi4%~
	COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Ramazith/JA#JOPI.D~
	COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Ramazith/JA#ABELA.D~
	
	
	COPY_EXISTING ~RAGEFA.CRE~ ~override~
		WRITE_ASCII 0x248 ~SHOUTDLG~ #8

	COPY_EXISTING ~RAMAZI.CRE~ ~override/JA#RAMAZ.CRE~
		WRITE_ASCII 0x250 ~NONE~ #8
		WRITE_ASCII 0x260 ~NONE~ #8
		WRITE_ASCII 0x2cc ~JA#RAMAZ~ #8
	COPY_EXISTING ~JOPI.CRE~ ~override/JA#JOPI.CRE~
		WRITE_ASCII 0x268 ~RUNENEMY~ #8
		WRITE_ASCII 0x2cc ~JA#JOPI~ #8
	COPY ~JA#BGT_AdvPack/Ramazith/JA#GOLC1.CRE~ ~override~
	COPY ~JA#BGT_AdvPack/Ramazith/JA#IMP01.CRE~ ~override~
		SAY NAME1 @2042
		SAY NAME2 @2042


	EXTEND_TOP ~%WBaldursGate_RagefastsHouse%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7226.BAF~ EVALUATE_BUFFER 
	EXTEND_TOP ~%NBaldursGate_RamazithsTower_L1%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7237.BAF~ EVALUATE_BUFFER 
	EXTEND_BOTTOM ~%NBaldursGate_RamazithsTower_L6%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7238.BAF~ EVALUATE_BUFFER 
	EXTEND_TOP ~%NBaldursGate_RamazithsTower_L2%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7239.BAF~ EVALUATE_BUFFER 
	EXTEND_TOP ~%NBaldursGate_RamazithsTower_L3%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7240.BAF~ EVALUATE_BUFFER 
	EXTEND_TOP ~%NBaldursGate_RamazithsTower_L4%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7241.BAF~ EVALUATE_BUFFER 
	EXTEND_TOP ~%NBaldursGate_RamazithsTower_L5%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7242.BAF~ EVALUATE_BUFFER 
	EXTEND_TOP ~%WBaldursGate%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7600.BAF~ EVALUATE_BUFFER 
	
	COMPILE ~JA#BGT_AdvPack/Ramazith/JA#CURA1.BAF~
	COMPILE ~JA#BGT_AdvPack/Ramazith/JA#CUJO1.BAF~
	COMPILE ~JA#BGT_AdvPack/Ramazith/JA#IMP01.BAF~

	COPY ~JA#BGT_AdvPack/Ramazith/AR7226.WED~ ~override/%WBaldursGate_RagefastsHouse%.WED~
	COPY ~JA#BGT_AdvPack/Ramazith/AR7238.WED~ ~override/%NBaldursGate_RamazithsTower_L6%.WED~
	INCLUDE ~%MOD_FOLDER%/lib/alter_searchmap.tpa~
	LAF ALTER_SEARCHMAP
		STR_VAR
		path_to_2da_file = EVAL ~%MOD_FOLDER%/Ramazith/AR7238SR_changes.2da~ 	// full path to the *changes.2da file containing your icons, e.g. ~mymod/bam/AR3700SR_changes_ee.2da~
		areaname = EVAL ~%NBaldursGate_RamazithsTower_L6%~ 							  		 			// area name, e.g. ~AR3700~
	END
END

//New Monsters in Ramazith tower
ACTION_IF GAME_IS ~bgee eet~ BEGIN
	ACTION_IF NOT (MOD_IS_INSTALLED ~setup-FotD.tp2~ 0) BEGIN
		COPY_EXISTING ~%NBaldursGate_RamazithsTower_L3%.ARE~ ~override~
			//delete not needed actors
			FOR (cnt = BYTE_AT 0x58 -1; cnt>=0; --cnt) BEGIN
				READ_ASCII SHORT_AT 0x54 +(0x110 * (cnt))+0x80 actors_filename (32) NULL
				PATCH_IF (~%actors_filename%~ STR_EQ  ~GHAST~) BEGIN
					LPF fj_are_structure
						INT_VAR fj_delete_mode = EVAL %cnt%
						STR_VAR fj_structure_type = actor
					END
				END
			END
			
			//add new actors
			LPF fj_are_structure
				INT_VAR
				fj_loc_x          = 78
				fj_loc_y          = 235
				fj_dest_x         = 78
				fj_dest_y         = 235
				fj_animation      = 32515
				fj_orientation    = 15     
				STR_VAR
				fj_structure_type = actor
				fj_name           = Mephit01
				fj_cre_resref     = ELEMEP08
			END
			LPF fj_are_structure
				INT_VAR
				fj_loc_x          = 172
				fj_loc_y          = 140
				fj_dest_x         = 172
				fj_dest_y         = 140
				fj_animation      = 32515
				fj_orientation    = 6     
				STR_VAR
				fj_structure_type = actor
				fj_name           = Mephit02
				fj_cre_resref     = MEPICE01
			END
			LPF fj_are_structure
				INT_VAR
				fj_loc_x          = 424
				fj_loc_y          = 286
				fj_dest_x         = 424
				fj_dest_y         = 286
				fj_animation      = 32515
				fj_orientation    = 1     
				STR_VAR
				fj_structure_type = actor
				fj_name           = Mephit03
				fj_cre_resref     = MEPSAL01
			END
		BUT_ONLY
	END
	

	COPY_EXISTING ~%NBaldursGate_RamazithsTower_L4%.ARE~ ~override~
		//delete not needed actors
		FOR (cnt=BYTE_AT 0x58 -1; cnt>=0; --cnt) BEGIN
			READ_ASCII SHORT_AT 0x54 +(0x110 * (cnt))+0x80 actors_filename (32) NULL
			PATCH_IF (~%actors_filename%~ STR_EQ  ~HOBELITE~) BEGIN
				LPF fj_are_structure
					INT_VAR fj_delete_mode = EVAL %cnt%
					STR_VAR fj_structure_type = actor
				END
			END
		END
		//add new actors
		LPF fj_are_structure
			INT_VAR
			fj_loc_x          = 419
			fj_loc_y          = 275
			fj_dest_x         = 419
			fj_dest_y         = 275
			fj_animation      = 31235
			fj_orientation    = 0     
			STR_VAR
			fj_structure_type = actor
			fj_name           = Spider01
			fj_cre_resref     = SPIDSW
		END
		LPF fj_are_structure
			INT_VAR
			fj_loc_x          = 162
			fj_loc_y          = 355
			fj_dest_x         = 162
			fj_dest_y         = 355
			fj_animation      = 31235
			fj_orientation    = 11    
			STR_VAR
			fj_structure_type = actor
			fj_name           = Spider02
			fj_cre_resref     = SPIDSW
		END
		LPF fj_are_structure
			INT_VAR
			fj_loc_x          = 99
			fj_loc_y          = 213
			fj_dest_x         = 99
			fj_dest_y         = 213
			fj_animation      = 31232
			fj_orientation    = 6     
			STR_VAR
			fj_structure_type = actor
			fj_name           = Spider03
			fj_cre_resref     = SPIDGI
		END
		LPF fj_are_structure
			INT_VAR
			fj_loc_x          = 249
			fj_loc_y          = 143
			fj_dest_x         = 249
			fj_dest_y         = 143
			fj_animation      = 31233
			fj_orientation    = 14     
			STR_VAR
			fj_structure_type = actor
			fj_name           = Spider04
			fj_cre_resref     = SPIDHU
		END
	BUT_ONLY
	
	COPY_EXISTING ~%NBaldursGate_RamazithsTower_L5%.ARE~ ~override~
		//delete not needed actors
		FOR (cnt=BYTE_AT 0x58 -1; cnt>=0; --cnt) BEGIN
			READ_ASCII SHORT_AT 0x54 +(0x110 * (cnt))+0x80 actors_filename (32) NULL
			PATCH_IF (~%actors_filename%~ STR_EQ  ~KOBCOMM~) BEGIN
				LPF fj_are_structure
					INT_VAR fj_delete_mode = EVAL %cnt%
					STR_VAR fj_structure_type = actor
				END
			END
		END
		//add new actors
		LPF fj_are_structure
			INT_VAR
			fj_loc_x          = 425
			fj_loc_y          = 264
			fj_dest_x         = 425
			fj_dest_y         = 264
			fj_animation      = 32519
			fj_orientation    = 15     
			STR_VAR
			fj_structure_type = actor
			fj_name           = Golem01
			fj_cre_resref     = JA#GOLC1
		END
		LPF fj_are_structure
			INT_VAR
			fj_loc_x          = 91
			fj_loc_y          = 236
			fj_dest_x         = 91
			fj_dest_y         = 236
			fj_animation      = 32519
			fj_orientation    = 15     
			STR_VAR
			fj_structure_type = actor
			fj_name           = Golem02
			fj_cre_resref     = JA#GOLC1
		END
	BUT_ONLY
END


