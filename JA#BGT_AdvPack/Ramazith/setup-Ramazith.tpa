/////////////////////////
//Ramaziths Turm
/////////////////////////


// BGT (coord fix)
COPY_EXISTING ~AR7238.BCS~ ~override~ // %NBaldursGate_RamazithsTower_L6%
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH
		~CreateCreature("RAMAZI",[0.0],0)~
		~CreateCreature("RAMAZI",[169.147],13)~
	END
IF_EXISTS
BUT_ONLY_IF_IT_CHANGES


<<<<<<<< ...inlined/AR7300_old.baf
IF
	Exists("Ramazith")
	Global("HelpRamazith","GLOBAL",0)
	Global("AbelaTeleport","GLOBAL",1)
THEN
	RESPONSE #100
		ActionOverride("Ramazith",DestroySelf())
END
>>>>>>>>
<<<<<<<< ...inlined/AR7300_new.baf
IF
	Exists("Ramazith")
	OR(2)
		Global("HelpRamazith","GLOBAL",0)
		Global("HelpRamazith","GLOBAL",4)
	OR(2)
		Dead("Abela")
		Global("AbelaTeleport","GLOBAL",1)
THEN
	RESPONSE #100
		ActionOverride("Ramazith",DestroySelf())
		SetGlobal("RamazithMove","GLOBAL",1)
END
>>>>>>>>
// BGT (Ramazith moves into his tower when Abela is gone)
COPY_EXISTING ~AR7300.BCS~ ~override~ // %NBaldursGate%
	REPLACE_BCS_BLOCK
		~...inlined/AR7300_old.baf~
		~...inlined/AR7300_new.baf~
	ON_MISMATCH
		PATCH_WARN ~WARNING: Something went wrong while patching AR7300.BCS! Trying to replace each part separately...~
		DECOMPILE_AND_PATCH BEGIN
			REPLACE_TEXTUALLY EXACT_MATCH
			~Global("HelpRamazith","GLOBAL",0)~
			~OR(2) Global("HelpRamazith","GLOBAL",0) Global("HelpRamazith","GLOBAL",4)~
			REPLACE_TEXTUALLY EXACT_MATCH
			~Global("AbelaTeleport","GLOBAL",1)~
			~OR(2) Global("AbelaTeleport","GLOBAL",1) Dead("Abela")~
			REPLACE_TEXTUALLY EXACT_MATCH
			~ActionOverride("Ramazith",DestroySelf())~
			~ActionOverride("Ramazith",DestroySelf()) SetGlobal("RamazithMove","GLOBAL",1)~
		END
	END
IF_EXISTS
BUT_ONLY_IF_IT_CHANGES

// BGEE/EET (Ramazith moves into his tower when Abela is gone)
COPY_EXISTING ~RAMAZITH.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EVALUATE_REGEXP
		~IF[%WNL%%MNL%%LNL%%TAB% ]+Global("HelpRamazith","GLOBAL",0)~
		~IF OR(2) Global("HelpRamazith","GLOBAL",0) Global("HelpRamazith","GLOBAL",4)~
	END
BUT_ONLY_IF_IT_CHANGES


// Replace in Ramazith's tower L2 existing creatures with spawns
COPY_EXISTING ~%NBaldursGate_RamazithsTower_L2%.ARE~ ~override~
	FOR (cnt=BYTE_AT 0x58 -1; cnt>=0; --cnt) BEGIN
		READ_ASCII SHORT_AT 0x54 +(0x110 * (cnt))+0x80 actors_filename (32) NULL
		PATCH_IF (~%actors_filename%~ STR_EQ  ~JELLYMU~) BEGIN
			LPF fj_are_structure
				INT_VAR fj_delete_mode = EVAL %cnt%
				STR_VAR fj_structure_type = actor
			END
		END
	END
BUT_ONLY
EXTEND_TOP ~%NBaldursGate_RamazithsTower_L2%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7239.BAF~ EVALUATE_BUFFER

// Replace in Ramazith's tower L3 existing creatures with spawns
ACTION_IF NOT (MOD_IS_INSTALLED ~setup-FotD.tp2~ 0) BEGIN
	COPY_EXISTING ~%NBaldursGate_RamazithsTower_L3%.ARE~ ~override~
		FOR (cnt=BYTE_AT 0x58 -1; cnt>=0; --cnt) BEGIN
			READ_ASCII SHORT_AT 0x54 +(0x110 * (cnt))+0x80 actors_filename (32) NULL
			PATCH_IF (~%actors_filename%~ STR_EQ  ~GHAST~) BEGIN
				LPF fj_are_structure
					INT_VAR fj_delete_mode = EVAL %cnt%
					STR_VAR fj_structure_type = actor
				END
			END
		END
	BUT_ONLY
	EXTEND_TOP ~%NBaldursGate_RamazithsTower_L3%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7240.BAF~ EVALUATE_BUFFER
END

// Replace in Ramazith's tower L4 existing creatures with spawns
COPY_EXISTING ~%NBaldursGate_RamazithsTower_L4%.ARE~ ~override~
	FOR (cnt=BYTE_AT 0x58 -1; cnt>=0; --cnt) BEGIN
		READ_ASCII SHORT_AT 0x54 +(0x110 * (cnt))+0x80 actors_filename (32) NULL
		PATCH_IF (~%actors_filename%~ STR_EQ  ~HOBELITE~) BEGIN
			LPF fj_are_structure
				INT_VAR fj_delete_mode = EVAL %cnt%
				STR_VAR fj_structure_type = actor
			END
		END
	END
BUT_ONLY
EXTEND_TOP ~%NBaldursGate_RamazithsTower_L4%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7241.BAF~ EVALUATE_BUFFER

// Replace in Ramazith's tower L5 existing creatures with spawns
COPY_EXISTING ~%NBaldursGate_RamazithsTower_L5%.ARE~ ~override~
	FOR (cnt=BYTE_AT 0x58 -1; cnt>=0; --cnt) BEGIN
		READ_ASCII SHORT_AT 0x54 +(0x110 * (cnt))+0x80 actors_filename (32) NULL
		PATCH_IF (~%actors_filename%~ STR_EQ  ~KOBCOMM~) BEGIN
			LPF fj_are_structure
				INT_VAR fj_delete_mode = EVAL %cnt%
				STR_VAR fj_structure_type = actor
			END
		END
	END
BUT_ONLY
EXTEND_TOP ~%NBaldursGate_RamazithsTower_L5%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7242.BAF~ EVALUATE_BUFFER

// Hostile Ramazith will start dialogue
EXTEND_BOTTOM ~%NBaldursGate_RamazithsTower_L6%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7238.BAF~ EVALUATE_BUFFER

// Spawn JA#IMP01.CRE(Yuxi) and start cutscene with JA#JOPI(Jopi)
EXTEND_TOP ~%WBaldursGate_RagefastsHouse%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7226.BAF~ EVALUATE_BUFFER

// Spawn and despawn JA#JOPI.CRE(Jopi)
EXTEND_TOP ~%WBaldursGate%.BCS~ ~JA#BGT_AdvPack/Ramazith/AR7600.BAF~ EVALUATE_BUFFER


// Grab actions from first response of state 15
COPY_EXISTING ~RAMAZI.DLG~ ~override~
	SET state = 15
    READ_LONG 0x0c ofs_states
    READ_LONG 0x14 ofs_responses
    READ_LONG 0x28 ofs_response_actions

	SET ofs_state = ofs_states + (state * 0x10)
	READ_LONG (ofs_state + 0x4) idx_first_response

	SET ofs_response = ofs_responses + idx_first_response * 0x20
	READ_LONG (ofs_response + 0x10) idx_response_action

	SET ofs_action = ofs_response_actions + idx_response_action * 0x8
	READ_LONG ofs_action actions_ptr
	READ_LONG (ofs_action + 0x8) next_actions_ptr
	READ_ASCII actions_ptr state15_actions (next_actions_ptr - actions_ptr)

OUTER_SET default_state_during_quest = (GAME_IS ~bgt~) ? 22 : 10
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Ramazith/ja#ramazi.d~

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Ramazith/ja#jopi.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Ramazith/ja#abela.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Ramazith/ja#imp01.d~


COPY_EXISTING ~RAGEFA.CRE~ ~override~
	WRITE_ASCII 0x248 ~SHOUTDLG~ #8

COPY_EXISTING ~JOPI.CRE~ ~override/JA#JOPI.CRE~
	WRITE_ASCII 0x268 ~RUNENEMY~ #8
	WRITE_ASCII 0x2cc ~JA#JOPI~ #8
COPY ~JA#BGT_AdvPack/Ramazith/JA#GOLC1.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Ramazith/JA#IMP01.CRE~ ~override~
	SAY NAME1 @2042
	SAY NAME2 @2042

COMPILE ~JA#BGT_AdvPack/Ramazith/JA#CURA1.BAF~
COMPILE ~JA#BGT_AdvPack/Ramazith/JA#CUJO1.BAF~
COMPILE ~JA#BGT_AdvPack/Ramazith/JA#IMP01.BAF~

ACTION_IF GAME_IS ~bgt~ BEGIN
	COPY ~JA#BGT_AdvPack/Ramazith/AR7226.WED~ ~override/%WBaldursGate_RagefastsHouse%.WED~
	COPY ~JA#BGT_AdvPack/Ramazith/AR7238.WED~ ~override/%NBaldursGate_RamazithsTower_L6%.WED~
	INCLUDE ~%MOD_FOLDER%/lib/alter_searchmap.tpa~
	LAF ALTER_SEARCHMAP
		STR_VAR
		path_to_2da_file = EVAL ~%MOD_FOLDER%/Ramazith/AR7238SR_changes.2da~ 	// full path to the *changes.2da file containing your icons, e.g. ~mymod/bam/AR3700SR_changes_ee.2da~
		areaname = EVAL ~%NBaldursGate_RamazithsTower_L6%~ 						// area name, e.g. ~AR3700~
	END
END
