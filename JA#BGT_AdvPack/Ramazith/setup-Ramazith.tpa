/////////////////////////
//Ramaziths Turm
/////////////////////////

COPY_EXISTING ~%NBaldursGate_RamazithsTower_L1%.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("RAMAZI",[328.234],0)~ ~NoAction()~
		REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("RAMAZI",[328.234],S)~ ~NoAction()~
		REPLACE_TEXTUALLY EXACT_MATCH ~ActionOverride("Ramazith",SetNumTimesTalkedTo(1))~ ~Continue()~
	END
BUT_ONLY_IF_IT_CHANGES
	
COPY_EXISTING ~%NBaldursGate_RamazithsTower_L6%.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("RAMAZI",[0.0],0)~ ~CreateCreature("RAMAZI",[172.173],13)~
		REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreature("RAMAZI",[0.0],S)~ ~CreateCreature("RAMAZI",[172.173],13)~
	END
BUT_ONLY_IF_IT_CHANGES

	
COPY_EXISTING ~RAMAZITH.BCS~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY EXACT_MATCH ~GlobalLT("RamazithMove","GLOBAL",2)~ 
		~GlobalLT("RamazithMove","GLOBAL",2) Global("JA#RamazithDeal","GLOBAL",0)~
	END
BUT_ONLY_IF_IT_CHANGES
	
//delete existing creatures	
ACTION_FOR_EACH tower_level IN ~2~ ~3~ ~4~ ~5~ BEGIN
	COPY_EXISTING ~%NBaldursGate_RamazithsTower_L%tower_level%%.ARE~ ~override~
		FOR (cnt=BYTE_AT 0x58 -1; cnt>=0; --cnt) BEGIN
			READ_ASCII SHORT_AT 0x54 +(0x110 * (cnt))+0x80 actors_filename (32) NULL
			PATCH_FOR_EACH cre_name IN ~JELLYMU~ ~GHAST~ ~HOBELITE~ ~KOBCOMM~ BEGIN
				PATCH_IF (~%actors_filename%~ STR_EQ  ~%cre_name%~) BEGIN
					LPF fj_are_structure
						INT_VAR fj_delete_mode = EVAL %cnt%
						STR_VAR fj_structure_type = actor
					END
				END
			END
		END
	BUT_ONLY
END

//Add Ramazith and new creatures by script
EXTEND_TOP ~%WBaldursGate_RagefastsHouse%.BCS~ ~%MOD_FOLDER%/Ramazith/AR7226.BAF~ EVALUATE_BUFFER 
EXTEND_TOP ~%NBaldursGate_RamazithsTower_L1%.BCS~ ~%MOD_FOLDER%/Ramazith/AR7237.BAF~ EVALUATE_BUFFER 
EXTEND_BOTTOM ~%NBaldursGate_RamazithsTower_L6%.BCS~ ~%MOD_FOLDER%/Ramazith/AR7238.BAF~ EVALUATE_BUFFER 
EXTEND_TOP ~%NBaldursGate_RamazithsTower_L2%.BCS~ ~%MOD_FOLDER%/Ramazith/AR7239.BAF~ EVALUATE_BUFFER 
EXTEND_TOP ~%NBaldursGate_RamazithsTower_L3%.BCS~ ~%MOD_FOLDER%/Ramazith/AR7240.BAF~ EVALUATE_BUFFER 
EXTEND_TOP ~%NBaldursGate_RamazithsTower_L4%.BCS~ ~%MOD_FOLDER%/Ramazith/AR7241.BAF~ EVALUATE_BUFFER 
EXTEND_TOP ~%NBaldursGate_RamazithsTower_L5%.BCS~ ~%MOD_FOLDER%/Ramazith/AR7242.BAF~ EVALUATE_BUFFER 
EXTEND_TOP ~%WBaldursGate%.BCS~ ~%MOD_FOLDER%/Ramazith/AR7600.BAF~ EVALUATE_BUFFER
EXTEND_TOP ~%NBaldursGate%.BCS~ ~%MOD_FOLDER%/Ramazith/AR7300.BAF~ EVALUATE_BUFFER

	COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/Ramazith/JA#RAMAZI.D~
	COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/Ramazith/JA#JOPI.D~
	COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/Ramazith/JA#ABELA.D~
	
	
	COPY_EXISTING ~RAGEFA.CRE~ ~override~
		WRITE_ASCII 0x248 ~SHOUTDLG~ #8

	COPY_EXISTING ~RAMAZI.CRE~ ~override/JA#RAMAZ.CRE~
		WRITE_ASCII 0x250 ~NONE~ #8
		WRITE_ASCII 0x260 ~NONE~ #8
		WRITE_ASCII 0x2cc ~JA#RAMAZ~ #8
	COPY_EXISTING ~JOPI.CRE~ ~override/JA#JOPI.CRE~
		WRITE_ASCII 0x268 ~RUNENEMY~ #8
		WRITE_ASCII 0x2cc ~JA#JOPI~ #8
	COPY ~%MOD_FOLDER%/Ramazith/JA#GOLC1.CRE~ ~override~
	COPY ~%MOD_FOLDER%/Ramazith/JA#IMP01.CRE~ ~override~
		SAY NAME1 @2042
		SAY NAME2 @2042

	COMPILE ~%MOD_FOLDER%/Ramazith/JA#CURA1.BAF~
	COMPILE ~%MOD_FOLDER%/Ramazith/JA#CUJO1.BAF~
	COMPILE ~%MOD_FOLDER%/Ramazith/JA#IMP01.BAF~

	COPY ~%MOD_FOLDER%/Ramazith/AR7226.WED~ ~override/%WBaldursGate_RagefastsHouse%.WED~
	COPY ~%MOD_FOLDER%/Ramazith/AR7238.WED~ ~override/%NBaldursGate_RamazithsTower_L6%.WED~
	INCLUDE ~%MOD_FOLDER%/lib/alter_searchmap.tpa~
	LAF ALTER_SEARCHMAP
		STR_VAR
		path_to_2da_file = EVAL ~%MOD_FOLDER%/Ramazith/AR7238SR_changes.2da~
		areaname = EVAL ~%NBaldursGate_RamazithsTower_L6%~
	END

