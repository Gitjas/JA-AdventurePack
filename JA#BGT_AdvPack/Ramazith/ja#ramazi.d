// BGEE/EET
ADD_TRANS_ACTION RAMAZI
BEGIN 11 END
BEGIN 0 END
~SetGlobal("RamazithMove","GLOBAL",2)~
UNLESS ~SetGlobal("RamazithMove","GLOBAL",2)~

// BGT (better check for failed quest (similar as in BGEE/EET))
REPLACE_TRANS_TRIGGER RAMAZI
BEGIN 11 END
BEGIN 0 END
~!PartyHasItem("MISC68")~
~OR(2) Dead("Abela") Global("AbelaTeleport","GLOBAL",1)~

// BGT (add missing Enemy() and QUICK_TELEPORT when Ramazith teleports)
REPLACE_ACTION_TEXT RAMAZI
~ForceSpell(LastTalkedToBy(Myself),WIZARD_LIGHTNING_BOLT)~
~ForceSpell(LastTalkedToBy(Myself),WIZARD_LIGHTNING_BOLT) SmallWait(1) Enemy() ForceSpellPoint([169.147],QUICK_TELEPORT)~
UNLESS ~QUICK_TELEPORT~

// BGEE/EET (add missing WIZARD_LIGHTNING_BOLT when Ramazith teleports)
REPLACE_ACTION_TEXT RAMAZI
~Enemy()[%WNL%%MNL%%LNL%%TAB% ]+ForceSpellPoint(\[169.147\],QUICK_TELEPORT)~
~ForceSpell(LastTalkedToBy(Myself),WIZARD_LIGHTNING_BOLT) SmallWait(1) Enemy() ForceSpellPoint([169.147],QUICK_TELEPORT)~
UNLESS ~WIZARD_LIGHTNING_BOLT~


REPLACE_STATE_TRIGGER RAMAZI 22
~Global("HelpRamazith","GLOBAL",1) !PartyHasItem("MISC68")~
IF ~True()~

// BGT (spawn at same postion as in BGEE/EET)
REPLACE_TRANS_TRIGGER RAMAZI
BEGIN 13 END
BEGIN 0 END
~CreateCreature("ABELA",[260.170],0)~
~CreateCreature("ABELA",[376.228],2) ActionOverride("Abela",Wait(1))~


ALTER_TRANS RAMAZI
BEGIN 14 END
BEGIN 0 END
BEGIN
  "ACTION" ~AddexperienceParty(400) ReputationInc(-1) %state15_actions%~
END

ALTER_TRANS RAMAZI
BEGIN 15 END
BEGIN 0 END
BEGIN
  "ACTION" ~~
  "REPLY" ~@15~
  "EPILOGUE" ~GOTO JA#RAMAZITH_15~
END

EXTEND_BOTTOM RAMAZI 15
IF ~~ THEN REPLY @1 GOTO JA#RAMAZITH_6
IF ~~ THEN REPLY @17 GOTO JA#RAMAZITH_8
END


EXTEND_BOTTOM RAMAZI 16 18
IF ~~ THEN REPLY @17 GOTO JA#RAMAZITH_8
END


ALTER_TRANS RAMAZI
BEGIN 19 END
BEGIN 0 END
BEGIN
  "ACTION" ~~
  "REPLY" ~@14~
  "EPILOGUE" ~GOTO JA#RAMAZITH_19~
END

EXTEND_BOTTOM RAMAZI 19
IF ~~ THEN REPLY @2 GOTO JA#RAMAZITH_4
IF ~~ THEN REPLY @17 GOTO JA#RAMAZITH_8
END


ALTER_TRANS RAMAZI
BEGIN ~%default_state_during_quest%~ END // 10(BGEE/EET) or 22(BGT)
BEGIN 0 END
BEGIN
  "TRIGGER" ~Global("HelpRamazith","GLOBAL",1) !PartyHasItem("MISC68")~
  "ACTION" ~~
  "REPLY" ~@16~
  "EPILOGUE" ~GOTO JA#RAMAZITH_22~
END

EXTEND_BOTTOM RAMAZI ~%default_state_during_quest%~ // 10(BGEE/EET) or 22(BGT)
IF ~Global("HelpRamazith","GLOBAL",1) !PartyHasItem("MISC68")~ THEN
REPLY @3
GOTO JA#RAMAZITH_19
END

/*
REPLACE_ACTION_TEXT RAMAZI
~SetGlobalTimer("Ramazith","GLOBAL",[^)]+)~
~~*/

REPLACE_STATE_TRIGGER RAMAZI 16 ~AreaCheck("%NBaldursGate_RamazithsTower_L1%")
!GlobalGT("RamazithMove","GLOBAL",1)
Global("HelpRamazith","GLOBAL",3)~



APPEND RAMAZI


IF ~GlobalTimerExpired("JA#RamazithItem","GLOBAL") Global("JA#RamazithDeal","GLOBAL",1)~ THEN BEGIN JA#RAMAZITH_X0
SAY @20
IF ~~ THEN REPLY @21 DO ~ClearAllActions() StartCutSceneMode() StartCutScene("JA#CURA1")~ EXIT
IF ~~ THEN REPLY @22 GOTO JA#RAMAZITH_X1
END

IF ~~ THEN BEGIN JA#RAMAZITH_X1
SAY @23
IF ~~ THEN DO ~SetGlobal("JA#RamazithDeal","GLOBAL",10) ForceSpell(Myself,WIZARD_STONE_SKIN) Enemy()~ EXIT
END

IF ~Global("JA#RamazithDeal","GLOBAL",2)~ THEN BEGIN JA#RAMAZITH_X2
SAY @24
IF ~PartyGoldGT(199)~ THEN REPLY @25 DO ~SetGlobal("JA#RamazithDeal","GLOBAL",4)~ GOTO JA#RAMAZITH_X3
IF ~~ THEN REPLY @26 DO ~SetGlobal("JA#RamazithDeal","GLOBAL",3)~ GOTO JA#RAMAZITH_X4
END

IF ~~ THEN BEGIN JA#RAMAZITH_X3
SAY @27
IF ~~ THEN DO ~TakePartyGold(200) GiveItemCreate("CLCK07",LastTalkedToBy(Myself),0,0,0)~ EXIT
END

IF ~~ THEN BEGIN JA#RAMAZITH_X4
SAY @28
IF ~~ THEN EXIT
END

IF ~Global("JA#RamazithDeal","GLOBAL",3)~ THEN BEGIN JA#RAMAZITH_X5
SAY @29
IF ~PartyGoldGT(199)~ THEN REPLY @25 DO ~SetGlobal("JA#RamazithDeal","GLOBAL",4)~ GOTO JA#RAMAZITH_X3
IF ~~ THEN REPLY @30 EXIT
END

IF ~Global("JA#RamazithDeal","GLOBAL",4)~ THEN BEGIN JA#RAMAZITH_X6
SAY @31
IF ~~ THEN EXIT
END

IF ~GlobalTimerNotExpired("JA#RamazithItem","GLOBAL") Global("JA#RamazithDeal","GLOBAL",1)~ THEN BEGIN JA#RAMAZITH_X7
SAY @32
IF ~~ THEN EXIT
END


IF WEIGHT #0 ~AreaCheck("%NBaldursGate_RamazithsTower_L1%") OR(2) Global("HelpRamazith","GLOBAL",0) Global("HelpRamazith","GLOBAL",4)~
THEN BEGIN JA#RAMAZITH_10
SAY @33
IF ~~ THEN REPLY @34 GOTO JA#RAMAZITH_11
IF ~~ THEN REPLY @35 GOTO JA#RAMAZITH_12
IF ~~ THEN REPLY @36 GOTO JA#RAMAZITH_11
END

IF ~~ THEN BEGIN JA#RAMAZITH_11
SAY @37
COPY_TRANS_LATE RAMAZI 17 // Ramazith turns hostile
END

IF ~~ THEN BEGIN JA#RAMAZITH_12
SAY @38
COPY_TRANS_LATE RAMAZI 17 // Ramazith turns hostile
END


IF WEIGHT #0 ~AreaCheck("%NBaldursGate_RamazithsTower_L3%") GlobalGT("RamazithMove","GLOBAL",1)~
THEN BEGIN JA#RAMAZITH_1
SAY @5
IF ~~ THEN DO ~ForceSpell(LastTalkedToBy(Myself),SPIDER_SUMMON) Wait(1) ForceSpell(Myself,QUICK_TELEPORT) DestroySelf()~ EXIT
END

IF WEIGHT #0 ~AreaCheck("%NBaldursGate_RamazithsTower_L5%") GlobalGT("RamazithMove","GLOBAL",1)~
THEN BEGIN JA#RAMAZITH_3
SAY @7
IF ~~ THEN DO ~ForceSpell(Myself,QUICK_TELEPORT) DestroySelf()~ EXIT
END

IF WEIGHT #0 ~AreaCheck("%NBaldursGate_RamazithsTower_L6%") GlobalGT("RamazithMove","GLOBAL",1)~
THEN BEGIN JA#RAMAZITH_2
SAY @6
IF ~~ THEN DO ~ForceSpell(Myself,WIZARD_STONE_SKIN) Wait(1) ForceSpell(Myself,SUMMON_SHADOW)~ EXIT
END


IF ~~ THEN BEGIN JA#RAMAZITH_4
SAY @8
IF ~~ THEN EXIT
END


IF ~True()~ THEN BEGIN JA#RAMAZITH_5
SAY @9
IF ~Exists("Abela") !Dead("Abela")~ THEN REPLY @10 GOTO JA#RAMAZITH_6
IF ~~ THEN REPLY @11 EXIT
END


IF ~~ THEN BEGIN JA#RAMAZITH_6
SAY @12
COPY_TRANS_LATE RAMAZI 17 // Ramazith turns hostile
END


IF ~~ THEN BEGIN JA#RAMAZITH_8
SAY @19
IF ~~ THEN DO ~SetGlobalTimer("JA#RamazithItem","GLOBAL",TWO_DAYS)
SetGlobal("JA#RamazithDeal","GLOBAL",1)
ForceSpell(Myself,QUICK_TELEPORT)
DestroySelf()~ SOLVED_JOURNAL @18 EXIT
END


IF ~~ THEN BEGIN JA#RAMAZITH_15
SAY @13
IF ~~ THEN EXIT
END


IF ~~ THEN BEGIN JA#RAMAZITH_19
SAY @12
COPY_TRANS_LATE RAMAZI 17 // Ramazith turns hostile
END


IF ~~ THEN BEGIN JA#RAMAZITH_22
SAY @4
IF ~~ THEN EXIT
END


END // APPEND RAMAZI
