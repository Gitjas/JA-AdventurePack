// GERVISSE

ADD_STATE_TRIGGER GERVIS 2 ~Global("JA#GERVIS_QUEST","GLOBAL",1)~
ADD_STATE_TRIGGER GERVIS 3 ~Global("JA#GERVIS_QUEST","GLOBAL",1)~


REPLACE_STATE_TRIGGER GERVIS 0 ~NumTimesTalkedTo(0)~

ALTER_TRANS GERVIS
BEGIN 0 END
BEGIN 0 END
BEGIN
  "ACTION" ~SetGlobal("JA#KNOWGERVIS","%NWBaldursGate_LaertasHouse_L1%",1)~
  "EPILOGUE" ~GOTO 1~
END


ADD_TRANS_TRIGGER GERVIS 1 ~False()~ // Will be used in COPY_TRANS

EXTEND_BOTTOM GERVIS 1
IF ~~ THEN REPLY @83 GOTO JA_GERVIS_5
IF ~~ THEN REPLY @3 DO ~SetGlobal("JA#WHY","LOCALS",1)~ GOTO JA_GERVIS_1
IF ~GlobalGT("JA#VOLT_QUEST","GLOBAL",0)~ THEN REPLY @4 DO ~SetGlobal("JA#OFFEND","LOCALS",1)~ GOTO JA_GERVIS_2
IF ~~ THEN REPLY @5 GOTO JA_GERVIS_3
END


ALTER_TRANS GERVIS
BEGIN 3 END
BEGIN 0 END
BEGIN
  "REPLY" ~@85~
END

EXTEND_TOP GERVIS 3 #1
IF ~~ THEN REPLY @6 GOTO 4
IF ~Global("JA#VOLT_QUEST","GLOBAL",2)~ THEN REPLY @7 GOTO 4
END


ADD_TRANS_ACTION GERVIS
BEGIN 4 END
BEGIN 0 END
~SetGlobal("JA#GERVIS_QUEST","GLOBAL",10) Enemy()~


APPEND GERVIS

IF WEIGHT #1
~Dead("Voltine") Global("JA#GERVIS_QUEST","GLOBAL",1)~ THEN voltine_is_dead
SAY @91
IF ~~ THEN REPLY @92 DO ~SetGlobal("JA#GERVIS_QUEST","GLOBAL",2)~ GOTO 2
IF ~~ THEN REPLY @6 GOTO 4
END


IF ~~ THEN BEGIN JA_GERVIS_1
  SAY @8
  IF ~~ THEN REPLY @9 GOTO JA_GERVIS_5
  IF ~GlobalGT("JA#VOLT_QUEST","GLOBAL",0) Global("JA#OFFEND","LOCALS",0)~ THEN REPLY @4 DO ~SetGlobal("JA#OFFEND","LOCALS",1)~ GOTO JA_GERVIS_2
  IF ~Global("JA#OFFEND","LOCALS",1)~ THEN REPLY @10 GOTO 4
  IF ~~ THEN REPLY @11 GOTO JA_GERVIS_4
  IF ~~ THEN REPLY @5 GOTO JA_GERVIS_3
END

IF ~~ THEN BEGIN JA_GERVIS_2
  SAY @12
  IF ~~ THEN REPLY @9 GOTO JA_GERVIS_5
  IF ~Global("JA#WHY","LOCALS",0)~ THEN REPLY @3 DO ~SetGlobal("JA#WHY","LOCALS",1)~ GOTO JA_GERVIS_1
  IF ~GlobalGT("JA#VOLT_QUEST","GLOBAL",0)~ THEN REPLY @10 GOTO 4
  IF ~~ THEN REPLY @11 GOTO JA_GERVIS_4
  IF ~~ THEN REPLY @5 GOTO JA_GERVIS_3
END

IF ~~ THEN BEGIN JA_GERVIS_3
  SAY @13
  IF ~~ THEN DO ~SetGlobal("JA#GERVIS_QUEST","GLOBAL",10)~ EXIT
END

IF ~~ THEN BEGIN JA_GERVIS_4
  SAY @14
  IF ~~ THEN DO ~SetGlobal("JA#GERVIS_QUEST","GLOBAL",10)~ EXIT
END

IF ~~ THEN BEGIN JA_GERVIS_5
  SAY @15 = @16
  COPY_TRANS GERVIS 1 // DO ~SetGlobal("JA#GERVIS_QUEST","GLOBAL",1)~ EXIT
END


IF ~Global("JA#GERVIS_QUEST","GLOBAL",10)~ THEN BEGIN JA_GERVIS_6
  SAY @17
  IF ~!Dead("Voltine")Global("JA#VOLT_QUEST","GLOBAL",2)~ THEN REPLY @18 GOTO 4
  IF ~Dead("Voltine")~ THEN REPLY @19 GOTO 4
  IF ~!Dead("Voltine")~ THEN REPLY @20 GOTO JA_GERVIS_5
  IF ~~ THEN REPLY @21 EXIT
END


IF ~Global("JA#GERVIS_QUEST","GLOBAL",2)~ THEN BEGIN JA_GERVIS_7
  SAY @22
  IF ~~ THEN REPLY @23 GOTO JA_GERVIS_9
  IF ~~ THEN REPLY @24 GOTO JA_GERVIS_8
  IF ~~ THEN REPLY @25 GOTO 4
END

IF ~~ THEN BEGIN JA_GERVIS_8
  SAY @26
  IF ~~ THEN REPLY @27 GOTO JA_GERVIS_9
  IF ~~ THEN REPLY @25 GOTO 4
END

IF ~~ THEN BEGIN JA_GERVIS_9
  SAY @28
  IF ~~ THEN EXIT
END

END // APPEND GERVIS







// VOLTINE

REPLACE_STATE_TRIGGER VOLTIN 0 ~AreaCheck("JA#C08")~

ADD_TRANS_ACTION VOLTIN
BEGIN 0 END
BEGIN END
~SetGlobal("JA#VOLT_QUEST","GLOBAL",1)~


ADD_TRANS_ACTION VOLTIN
BEGIN 4 END
BEGIN 0 END
~Enemy()~

/* Faldorn will not tolerate if Voltine is attacked. Taken from bg1npc Project */
INTERJECT_COPY_TRANS2 VOLTIN 4 JA#FalMadVoltine
== %FALDORN_JOINED% IF ~InParty("faldorn") InMyArea("faldorn") !StateCheck("faldorn",CD_STATE_NOTVALID)~ THEN @90
DO ~ActionOverride("voltine",Enemy()) LeaveParty() SetLeavePartyDialogFile() ChangeAIScript("",DEFAULT) Enemy()~
END


APPEND VOLTIN

IF WEIGHT #0
~Dead("Gervisse") Global("HelpVoltine","GLOBAL",0)~ THEN BEGIN gervisse_is_dead
SAY @88
IF ~~ THEN REPLY @89 GOTO 3
IF ~~ THEN REPLY @29 GOTO 4
END


IF WEIGHT #0
~Global("JA#VOLT_QUEST","GLOBAL",0) !Dead("Gervisse")~ THEN BEGIN JA_VOLTIN_1
  SAY @30
  IF ~Global("JA#GERVIS_QUEST","GLOBAL",1)~ THEN REPLY @31 GOTO JA_VOLTIN_2
  IF ~Global("JA#GERVIS_QUEST","GLOBAL",10)~ THEN REPLY @32 GOTO JA_VOLTIN_3
  IF ~~ THEN REPLY @33 GOTO 4
  IF ~~ THEN REPLY @34 GOTO JA_VOLTIN_5
END

IF ~~ THEN BEGIN JA_VOLTIN_2
  SAY @35
  IF ~~ THEN REPLY @36 GOTO 4
  IF ~~ THEN REPLY @37 GOTO JA_VOLTIN_3
  IF ~~ THEN REPLY @34 GOTO JA_VOLTIN_5
END

IF ~~ THEN BEGIN JA_VOLTIN_3
  SAY @38 = @39
  IF ~~ THEN REPLY @36 GOTO 4
  IF ~~ THEN REPLY @40 GOTO JA_VOLTIN_4
  IF ~~ THEN REPLY @41 GOTO 4
  IF ~~ THEN REPLY @34 GOTO JA_VOLTIN_5
END

IF ~~ THEN BEGIN JA_VOLTIN_4
  SAY @42
  IF ~~ THEN DO ~SetGlobal("JA#VOLT_QUEST","GLOBAL",2)~ UNSOLVED_JOURNAL @43 EXIT
END

IF ~~ THEN BEGIN JA_VOLTIN_5
  SAY @44
  IF ~~ THEN EXIT
END


IF WEIGHT #0
~Global("JA#VOLT_QUEST","GLOBAL",1) !Dead("Gervisse")~ THEN BEGIN JA_VOLTIN_6
  SAY @46
  IF ~Global("JA#GERVIS_QUEST","GLOBAL",1)~ THEN REPLY @31 GOTO JA_VOLTIN_2
  IF ~~ THEN REPLY @32 GOTO JA_VOLTIN_3
  IF ~~ THEN REPLY @33 GOTO 4
  IF ~~ THEN REPLY @34 GOTO JA_VOLTIN_5
END


IF WEIGHT #0
~Global("JA#VOLT_QUEST","GLOBAL",2) !Dead("Gervisse")~ THEN BEGIN JA_VOLTIN_7
  SAY @47
  IF ~~ THEN REPLY @48 GOTO JA_VOLTIN_8
  IF ~~ THEN REPLY @49 GOTO 4
  IF ~Dead("Gervisse")~ THEN REPLY @50 GOTO 4
  IF ~~ THEN REPLY @34 GOTO JA_VOLTIN_5
END

IF ~~ THEN BEGIN JA_VOLTIN_8
  SAY @51
  IF ~~ THEN EXIT
END

END // APPEND VOLTIN



// LAERTA

ALTER_TRANS LAERTA
BEGIN 1 END
BEGIN 0 END
BEGIN
  "REPLY" ~@87~
END

EXTEND_TOP LAERTA 1 #1
IF ~~ THEN REPLY @53 GOTO JA_LAERTA_3
IF ~~ THEN REPLY @54 GOTO JA_LAERTA_1
IF ~Global("JA#LAERTA_GERV","%NWBaldursGate_LaertasHouse_L1%",0)Global("JA#KNOWGERVIS","%NWBaldursGate_LaertasHouse_L1%",1)~ THEN REPLY @55 DO ~SetGlobal("JA#LAERTA_GERV","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_8
IF ~Global("JA#LAERTA_VOLT","%NWBaldursGate_LaertasHouse_L1%",0)Global("JA#VOLT_SPWN","%NWBaldursGate_LaertasHouse_L1%",52)~ THEN REPLY @56 DO ~SetGlobal("JA#LAERTA_VOLT","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_9
END


// Have to assume that state 4 has initially one response!
EXTEND_BOTTOM LAERTA 4
COPY_TRANS LAERTA 4
COPY_TRANS LAERTA 4
COPY_TRANS LAERTA 4
END

ALTER_TRANS LAERTA
BEGIN 4 END
BEGIN 1 END
BEGIN
  "SOLVED_JOURNAL" ~@57~
END

ALTER_TRANS LAERTA
BEGIN 4 END
BEGIN 2 END
BEGIN
  "SOLVED_JOURNAL" ~@58~
END

ALTER_TRANS LAERTA
BEGIN 4 END
BEGIN 3 END
BEGIN
  "SOLVED_JOURNAL" ~@81~
END

ADD_TRANS_TRIGGER LAERTA 4 ~Dead("Voltine")~  DO 1 3
ADD_TRANS_TRIGGER LAERTA 4 ~Dead("Gervisse")~ DO 2 3


APPEND LAERTA

IF ~~ THEN BEGIN JA_LAERTA_1
  SAY @59
  IF ~~ THEN REPLY @60 GOTO JA_LAERTA_2
  IF ~~ THEN REPLY @61 EXIT
END

IF ~~ THEN BEGIN JA_LAERTA_2
  SAY @62
  IF ~~ THEN REPLY @63 GOTO JA_LAERTA_3
  IF ~Global("JA#LAERTA_GERV","%NWBaldursGate_LaertasHouse_L1%",0) Global("JA#KNOWGERVIS","%NWBaldursGate_LaertasHouse_L1%",1)~ THEN REPLY @55 DO ~SetGlobal("JA#LAERTA_GERV","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_8
  IF ~Global("JA#LAERTA_VOLT","%NWBaldursGate_LaertasHouse_L1%",0) Global("JA#VOLT_SPWN","%NWBaldursGate_LaertasHouse_L1%",52)~ THEN REPLY @56 GOTO JA_LAERTA_9
  IF ~~ THEN REPLY @64 EXIT
END

IF ~~ THEN BEGIN JA_LAERTA_3
  SAY @65
  IF ~~ THEN EXTERN LOUISE JA_LOUISE_1
END

IF ~~ THEN BEGIN JA_LAERTA_4
  SAY @66 = @67
  IF ~Global("JA#LAERTA_SPOK","%NWBaldursGate_LaertasHouse_L1%",0)~ THEN REPLY @68 DO ~SetGlobal("JA#LAERTA_SPOK","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_5
  IF ~Global("JA#LAERTA_FACE","%NWBaldursGate_LaertasHouse_L1%",0)~ THEN REPLY @69 DO ~SetGlobal("JA#LAERTA_FACE","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_6
  IF ~Global("JA#LAERTA_SEX","%NWBaldursGate_LaertasHouse_L1%",0)~ THEN REPLY @70 DO ~SetGlobal("JA#LAERTA_SEX","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_7
  IF ~Global("JA#LAERTA_GERV","%NWBaldursGate_LaertasHouse_L1%",0) Global("JA#KNOWGERVIS","%NWBaldursGate_LaertasHouse_L1%",1)~ THEN REPLY @55 DO ~SetGlobal("JA#LAERTA_GERV","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_8
  IF ~Global("JA#LAERTA_VOLT","%NWBaldursGate_LaertasHouse_L1%",0) Global("JA#VOLT_SPWN","%NWBaldursGate_LaertasHouse_L1%",52)~ THEN REPLY @56 DO ~SetGlobal("JA#LAERTA_VOLT","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_9
  IF ~~ THEN REPLY @71 EXIT
END

IF ~~ THEN BEGIN JA_LAERTA_5
  SAY @72
  IF ~Global("JA#LAERTA_FACE","%NWBaldursGate_LaertasHouse_L1%",0)~ THEN REPLY @69 DO ~SetGlobal("JA#LAERTA_FACE","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_6
  IF ~Global("JA#LAERTA_SEX","%NWBaldursGate_LaertasHouse_L1%",0)~ THEN REPLY @70 DO ~SetGlobal("JA#LAERTA_SEX","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_7
  IF ~Global("JA#LAERTA_GERV","%NWBaldursGate_LaertasHouse_L1%",0) Global("JA#KNOWGERVIS","%NWBaldursGate_LaertasHouse_L1%",1)~ THEN REPLY @55 DO ~SetGlobal("JA#LAERTA_GERV","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_8
  IF ~Global("JA#LAERTA_VOLT","%NWBaldursGate_LaertasHouse_L1%",0) Global("JA#VOLT_SPWN","%NWBaldursGate_LaertasHouse_L1%",52)~ THEN REPLY @56 DO ~SetGlobal("JA#LAERTA_VOLT","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_9
  IF ~~ THEN REPLY @71 EXIT
END

IF ~~ THEN BEGIN JA_LAERTA_6
  SAY @73
  IF ~Global("JA#LAERTA_SPOK","%NWBaldursGate_LaertasHouse_L1%",0)~ THEN REPLY @68 DO ~SetGlobal("JA#LAERTA_SPOK","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_5
  IF ~Global("JA#LAERTA_SEX","%NWBaldursGate_LaertasHouse_L1%",0)~ THEN REPLY @70 DO ~SetGlobal("JA#LAERTA_SEX","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_7
  IF ~Global("JA#LAERTA_GERV","%NWBaldursGate_LaertasHouse_L1%",0) Global("JA#KNOWGERVIS","%NWBaldursGate_LaertasHouse_L1%",1)~ THEN REPLY @55 DO ~SetGlobal("JA#LAERTA_GERV","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_8
  IF ~Global("JA#LAERTA_VOLT","%NWBaldursGate_LaertasHouse_L1%",0) Global("JA#VOLT_SPWN","%NWBaldursGate_LaertasHouse_L1%",52)~ THEN REPLY @56 DO ~SetGlobal("JA#LAERTA_VOLT","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_9
  IF ~~ THEN REPLY @71 EXIT
END

IF ~~ THEN BEGIN JA_LAERTA_7
  SAY @74
  IF ~Global("JA#LAERTA_SPOK","%NWBaldursGate_LaertasHouse_L1%",0)~ THEN REPLY @68 DO ~SetGlobal("JA#LAERTA_SPOK","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_5
  IF ~Global("JA#LAERTA_FACE","%NWBaldursGate_LaertasHouse_L1%",0)~ THEN REPLY @69 DO ~SetGlobal("JA#LAERTA_FACE","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_6
  IF ~Global("JA#LAERTA_GERV","%NWBaldursGate_LaertasHouse_L1%",0) Global("JA#KNOWGERVIS","%NWBaldursGate_LaertasHouse_L1%",1)~ THEN REPLY @55 DO ~SetGlobal("JA#LAERTA_GERV","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_8
  IF ~Global("JA#LAERTA_VOLT","%NWBaldursGate_LaertasHouse_L1%",0) Global("JA#VOLT_SPWN","%NWBaldursGate_LaertasHouse_L1%",52)~ THEN REPLY @56 DO ~SetGlobal("JA#LAERTA_VOLT","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_9
  IF ~~ THEN REPLY @71 EXIT
END

IF ~~ THEN BEGIN JA_LAERTA_8
  SAY @75
  IF ~Global("JA#LAERTA_SPOK","%NWBaldursGate_LaertasHouse_L1%",0)~ THEN REPLY @68 DO ~SetGlobal("JA#LAERTA_SPOK","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_5
  IF ~Global("JA#LAERTA_FACE","%NWBaldursGate_LaertasHouse_L1%",0)~ THEN REPLY @69 DO ~SetGlobal("JA#LAERTA_FACE","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_6
  IF ~Global("JA#LAERTA_SEX","%NWBaldursGate_LaertasHouse_L1%",0)~ THEN REPLY @70 DO ~SetGlobal("JA#LAERTA_SEX","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_7
  IF ~Global("JA#LAERTA_VOLT","%NWBaldursGate_LaertasHouse_L1%",0) Global("JA#VOLT_SPWN","%NWBaldursGate_LaertasHouse_L1%",52)~ THEN REPLY @56 DO ~SetGlobal("JA#LAERTA_VOLT","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_9
  IF ~~ THEN REPLY @71 EXIT
END

IF ~~ THEN BEGIN JA_LAERTA_9
  SAY @76
  IF ~Global("JA#LAERTA_SPOK","%NWBaldursGate_LaertasHouse_L1%",0)~ THEN REPLY @68 DO ~SetGlobal("JA#LAERTA_SPOK","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_5
  IF ~Global("JA#LAERTA_FACE","%NWBaldursGate_LaertasHouse_L1%",0)~ THEN REPLY @69 DO ~SetGlobal("JA#LAERTA_FACE","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_6
  IF ~Global("JA#LAERTA_SEX","%NWBaldursGate_LaertasHouse_L1%",0)~ THEN REPLY @70 DO ~SetGlobal("JA#LAERTA_SEX","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_7
  IF ~Global("JA#LAERTA_GERV","%NWBaldursGate_LaertasHouse_L1%",0) Global("JA#KNOWGERVIS","%NWBaldursGate_LaertasHouse_L1%",1)~ THEN REPLY @55 DO ~SetGlobal("JA#LAERTA_GERV","%NWBaldursGate_LaertasHouse_L1%",1)~ GOTO JA_LAERTA_8
  IF ~~ THEN REPLY @71 EXIT
END

END // APPEND LAERTA



// LOUISE

ADD_STATE_TRIGGER LOUISE 1 ~Global("HelpLaerta","GLOBAL",0) !Dead("Laerta")~

ALTER_TRANS LOUISE
BEGIN 1 END
BEGIN 0 END
BEGIN
  "REPLY" ~@87~
END

EXTEND_TOP LOUISE 1 #1
IF ~~ THEN REPLY @53 EXTERN LAERTA JA_LAERTA_3
IF ~~ THEN REPLY @54 EXTERN LAERTA JA_LAERTA_1
IF ~Global("JA#LAERTA_GERV","%NWBaldursGate_LaertasHouse_L1%",0)Global("JA#KNOWGERVIS","%NWBaldursGate_LaertasHouse_L1%",1)~ THEN REPLY @55 DO ~SetGlobal("JA#LAERTA_GERV","%NWBaldursGate_LaertasHouse_L1%",1)~ EXTERN LAERTA JA_LAERTA_8
IF ~Global("JA#LAERTA_VOLT","%NWBaldursGate_LaertasHouse_L1%",0)Global("JA#VOLT_SPWN","%NWBaldursGate_LaertasHouse_L1%",52)~ THEN REPLY @56 DO ~SetGlobal("JA#LAERTA_VOLT","%NWBaldursGate_LaertasHouse_L1%",1)~ EXTERN LAERTA JA_LAERTA_9
END


APPEND LOUISE

IF ~Global("HelpLaerta","GLOBAL",1) !Dead("Laerta")~ THEN BEGIN JA_LOUISE_2
  SAY @77
  IF ~~ THEN EXTERN LAERTA 4
END

IF ~Global("HelpLaerta","GLOBAL",2)~ THEN BEGIN JA_LOUISE_SAVED
  SAY @78
  IF ~~ THEN EXIT
END


IF ~~ THEN BEGIN JA_LOUISE_1
  SAY @79 = @80
  IF ~~ THEN EXTERN LAERTA JA_LAERTA_4
END

END // APPEND LOUISE
