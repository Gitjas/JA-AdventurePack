//////////////////////////////
//Gervisse und Voltine
//////////////////////////////

COPY_EXISTING ~LAERTA.CRE~ ~override~
              ~LOUISE.CRE~ ~override~
	WRITE_ASCII 0x268	~NONE~	#8

COPY_EXISTING ~VOLTIN.CRE~ ~override~
	WRITE_LONG 0x14 720
	WRITE_ASCII 0x248	~JA#VOLTI~	#8
	WRITE_ASCII 0x250	~DRUID8B~	#8
	REMOVE_KNOWN_SPELL ~SPPR102~ ~SPPR304~ ~SPPR301~
	REMOVE_MEMORIZED_SPELL ~SPPR102~ ~SPPR304~ ~SPPR301~
	ADD_KNOWN_SPELL ~SPPR111~ #0 ~priest~
	ADD_KNOWN_SPELL ~SPPR110~ #0 ~priest~
	ADD_KNOWN_SPELL ~SPPR302~ #2 ~priest~
	ADD_KNOWN_SPELL ~SPPR402~ #3 ~priest~
	ADD_MEMORIZED_SPELL ~SPPR111~ #0 ~priest~
	ADD_MEMORIZED_SPELL ~SPPR110~ #0 ~priest~
	ADD_MEMORIZED_SPELL ~SPPR302~ #2 ~priest~
	ADD_MEMORIZED_SPELL ~SPPR402~ #3 ~priest~

COPY ~JA#BGT_AdvPack/Gervisse_Voltine/JA#VOLTI.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Gervisse_Voltine/JA#VOLTJ.CRE~ ~override~


COPY_EXISTING ~%NWBaldursGate%.ARE~ ~override~
	//add region triggers
	LPF fj_are_structure
		INT_VAR
		fj_type         = 2     //travel
		fj_box_left     = 4000  //kleinstes x
		fj_box_top      = 1780  //kleinstes y
		fj_box_right    = 4200  //größtes x
		fj_box_bottom   = 1950  //größtes y
		fj_cursor_idx   = 30    //door
		fj_vertex_0     = 4000 + (1780 << 16)   //kleinstes x +  kleinstes y
		fj_vertex_1     = 4200 + (1780 << 16)   //größtes x + kleinstes y
		fj_vertex_2     = 4200 + (1950 << 16)   //größtes x + größtes y
		fj_vertex_3     = 4000 + (1950 << 16)   //kleinstes x + größtes y
		STR_VAR
		fj_structure_type   = region
		fj_name             = DOORJA#C07
		fj_destination_area = JA#C07
		fj_destination_name = FR7200
	END
	//add entrance
	LPF fj_are_structure
		INT_VAR
		fj_loc_x       = 4135
		fj_loc_y       = 1805
		fj_orientation = 8
		STR_VAR
		fj_structure_type = entrance
		fj_name           = Frja#c07
	END

COPY ~JA#BGT_AdvPack/Gervisse_Voltine/JA#C07.ARE~ ~override~
	PATCH_IF GAME_IS ~bgee eet~ BEGIN
		WRITE_ASCIIE 0x8 ~%NWBaldursGate_House3_L1%~ #8
		LPF ALTER_AREA_REGION
			STR_VAR region_name			= ~Door7200~
					destination_area	= EVAL ~%NWBaldursGate%~
		END
	END

COPY ~JA#BGT_AdvPack/Gervisse_Voltine/JA#C08.ARE~ ~override~
	PATCH_IF GAME_IS ~bgee eet~ BEGIN
		WRITE_ASCIIE 0x8 ~%NWBaldursGate_House3_L2%~ #8
	END

COPY_EXISTING ~AMB_E14A.WAV~ ~override~


COPY_EXISTING ~LAERTA.DLG~ ~override~
	LPF READ_JOURNAL_STRREF
		INT_VAR state = 0
		RET strref
	END
BUT_ONLY

<<<<<<<< ...inlined/ja#replace_action.d
REPLACE_ACTION_TEXT_PROCESS LAERTA // BGEE/EET
~EraseJournalEntry(%strref%)~
~EraseJournalEntry(@43) EraseJournalEntry(%strref%)~
GERVIS VOLTIN // BGT

REPLACE_ACTION_TEXT GERVIS
~CreateCreature("VOLTIN",\[[0-9]+\.[0-9]+\],[^,)]+)~
~SetGlobal("JA#GERVIS_QUEST","GLOBAL",1)~
>>>>>>>>

COMPILE EVALUATE_BUFFER ~...inlined/ja#replace_action.d~ USING ~JA#BGT_AdvPack/languages/%s/ja#voltin.tra~

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Gervisse_Voltine/JA#GERVIS.D~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Gervisse_Voltine/JA#VOLTIN.D~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Gervisse_Voltine/JA#TWINS.D~

ADD_JOURNAL EXISTING @43 @57 @58 @81 USING ~JA#BGT_AdvPack/languages/%s/ja#voltin.tra~
                                           ~JA#BGT_AdvPack/languages/%s/ja#twins.tra~

COMPILE ~JA#BGT_AdvPack/Gervisse_Voltine/JA#C08.BAF~
COMPILE ~JA#BGT_AdvPack/Gervisse_Voltine/JA#CUVOL.BAF~
COMPILE ~JA#BGT_AdvPack/Gervisse_Voltine/JA#CUVOM.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Gervisse_Voltine/JA#VOLTJ.BAF~
EXTEND_TOP ~%NWBaldursGate_LaertasHouse_L1%.BCS~ ~JA#BGT_AdvPack/Gervisse_Voltine/AR7262.BAF~ EVALUATE_BUFFER
