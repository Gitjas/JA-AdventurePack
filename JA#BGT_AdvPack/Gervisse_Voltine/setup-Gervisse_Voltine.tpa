/////////////////////////
// Gervisse and Voltine
/////////////////////////


COPY ~JA#BGT_AdvPack/Gervisse_Voltine/JA#C07.ARE~ ~override~
	WRITE_ASCIIE 0x8 ~%NWBaldursGate_House3_L1%~ #8 // WED resource
	LPF ALTER_AREA_REGION
		STR_VAR
		region_name = ~Door7200~
		destination_area = EVAL ~%NWBaldursGate%~
	END

COPY ~JA#BGT_AdvPack/Gervisse_Voltine/JA#C08.ARE~ ~override~
	WRITE_ASCIIE 0x8 ~%NWBaldursGate_House3_L2%~ #8 // WED resource


COPY ~JA#BGT_AdvPack/Gervisse_Voltine/JA#VOLTI.CRE~ ~override~
COPY ~JA#BGT_AdvPack/Gervisse_Voltine/JA#VOLTJ.CRE~ ~override~


COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Gervisse_Voltine/JA#C08.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Gervisse_Voltine/JA#CUVOL.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Gervisse_Voltine/JA#CUVOM.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Gervisse_Voltine/JA#VOLTI.BAF~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Gervisse_Voltine/JA#VOLTJ.BAF~

EXTEND_TOP ~%NWBaldursGate_LaertasHouse_L1_BCS%.BCS~ ~JA#BGT_AdvPack/Gervisse_Voltine/AR7262.BAF~ EVALUATE_BUFFER


LAF GET_DLG_WEIGHT
	INT_VAR idx_state = 1
	STR_VAR dlg_file = ~GERVIS.DLG~
	RET gervis_state01_weight = weight
END
LAF GET_DLG_STRREF
	INT_VAR idx_state = 1
	STR_VAR dlg_file = ~GERVIS.DLG~
	RET gervis_state01_strref = state_strref
		gervis_state01_journal = journal_strref
END

<<<<<<<< ...inlined/laerta_quest_finish.d
REPLACE_ACTION_TEXT_PROCESS LAERTA
~EraseJournalEntry(%gervis_state01_journal%)~
~EraseJournalEntry(%gervis_state01_journal%) EraseJournalEntry(@1043)~
GERVIS VOLTIN // BGT
>>>>>>>>
COMPILE EVALUATE_BUFFER ~...inlined/laerta_quest_finish.d~

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Gervisse_Voltine/ja#gervis.d~
COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Gervisse_Voltine/ja#voltin.d~

LAF GET_DLG_SCRIPT
	INT_VAR idx_state = 4
	STR_VAR dlg_file = ~LAERTA.DLG~
	RET state04_response0_trigger = response_trigger
		state04_response0_action = response_action
END

COMPILE EVALUATE_BUFFER ~JA#BGT_AdvPack/Gervisse_Voltine/ja#twins.d~

ADD_JOURNAL EXISTING @57 @58 @81 @1043 USING ~JA#BGT_AdvPack/languages/%s/ja#twins.tra~


COPY_EXISTING ~LAERTA.CRE~ ~override~
              ~LOUISE.CRE~ ~override~
	LPF REMOVE_CRE_SCRIPT STR_VAR script_old = ~WDRUNSGT~ END
BUT_ONLY

COPY_EXISTING ~VOLTIN.CRE~ ~override~
	WRITE_LONG 0x14 720
	LPF INSERT_CRE_SCRIPT INT_VAR offset = SCRIPT_OVERRIDE STR_VAR script_new = ~JA#VOLTI~ END
	REMOVE_KNOWN_SPELL ~SPPR301~ ~SPPR501~
	REMOVE_MEMORIZED_SPELL ~SPPR301~ ~SPPR501~
	ADD_KNOWN_SPELL ~SPPR402~ #3 ~priest~
	ADD_MEMORIZED_SPELL ~SPPR402~ #3 ~priest~ (2)
BUT_ONLY


COPY_EXISTING ~%NWBaldursGate%.ARE~ ~override~
	// add region triggers
	LPF fj_are_structure
		INT_VAR
		fj_type         = 2     // travel
		fj_box_left     = 4000  // kleinstes x
		fj_box_top      = 1780  // kleinstes y
		fj_box_right    = 4200  // größtes x
		fj_box_bottom   = 1950  // größtes y
		fj_cursor_idx   = 30    // door
		fj_vertex_0     = 4000 + (1780 << 16)   // kleinstes x +  kleinstes y
		fj_vertex_1     = 4200 + (1780 << 16)   // größtes x + kleinstes y
		fj_vertex_2     = 4200 + (1950 << 16)   // größtes x + größtes y
		fj_vertex_3     = 4000 + (1950 << 16)   // kleinstes x + größtes y
		STR_VAR
		fj_structure_type   = region
		fj_name             = ~DOORJA#C07~
		fj_destination_area = ~JA#C07~
		fj_destination_name = ~FR7200~
	END
	// add entrance
	LPF fj_are_structure
		INT_VAR
		fj_loc_x       = 4135
		fj_loc_y       = 1805
		fj_orientation = 8
		STR_VAR
		fj_structure_type = entrance
		fj_name           = ~Frja#c07~
	END
BUT_ONLY
